;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https:;;www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljx.parser.reader-test
  (:require [tcljx.parser.reader :as reader]
            [tinyclj.string :as str]
            [tcljx.alpha.ptest :refer :all])
  (:import (tcljx.model Resolver)
           (tcljx.parser.wrong WrongInfo)
           (tcljx.parser.reader FormBuilder TokenParser)))

(deftype ResolverStub [^String ns-str
                       ^map ns-alias-map
                       ^map import-map
                       ^map global-qname-map]
  Resolver
  (ns-str [_]
    ns-str)
  (lookup-namespace [_ alias]
    (.valAt ns-alias-map alias nil))
  (lookup-class [_ sym]
    (.valAt import-map sym nil))
  (lookup-global-qname [_ nm]
    (.valAt global-qname-map nm nil)))

(defn stub-resolver
  (^Resolver []
   (stub-resolver "NO_NS" {}))
  (^Resolver [^String ns-str ^map ns-alias]
   (ResolverStub. ns-str ns-alias {} {}))
  (^Resolver [^String ns-str ^map ns-alias
              ^map import-map ^map global-qname-map]
   (ResolverStub. ns-str ns-alias import-map global-qname-map)))

(defn read-all
  (^seq [^String cs]
   (read-all cs (stub-resolver)))
  (^seq [^String cs ^Resolver resolver]
   (seq (reader/read-all cs resolver))))

(defn read1
  ([^String cs]
   (read1 cs (stub-resolver)))
  ([^String cs ^Resolver resolver] ;one form, no syntax-quote processing
   (let [forms (reader/read-all cs resolver)]
     (case (alength forms)
       0 (throw (IllegalArgumentException. "no data in input"))
       1 (aget forms 0)
       #_else (throw (IllegalArgumentException.
                      "unexpected data after form"))))))

;;; ------------------------------------------------------------------------

(defn- walk-meta [inner outer form]
  (letfn [(out [x]
            (let [y (outer x)]
              (if-some [m (meta form)]
                (with-meta y m)
                y)))]
    (cond
      (list? form) (out (apply list (map inner form)))
      (instance? clojure.lang.IMapEntry form) (out (vec (map inner form)))
      (seq? form) (out (doall (map inner form)))
      (instance? clojure.lang.IRecord form)
      (out (reduce (fn [r x] (conj r (inner x))) form form))
      (coll? form) (out (into (empty form) (map inner form)))
      :else (out form))))

(defn postwalk-meta [f form]
  (walk-meta (partial postwalk-meta f) f form))

(defn deterministic-gensyms [form]
  (let [gensym-map (atom {})]
    (letfn [(add-replacement [m sym prefix suffix]
              (let [sym' (symbol nil (str prefix "$" (inc (count m)) "$" suffix))]
                (assoc m sym sym')))
            (replacement [x]
              (cond
                (symbol? x)
                (or (when-some [y (@gensym-map x)]
                      (with-meta y (meta x))) ;preserve meta of replaced symbol
                    (if-some [[_ p n s] (re-matches #"(.*[^$0-9])(\d+)([^$0-9]*)"
                                                    (str x))]
                      (let [i (Integer/parseInt n)]
                        (if (or (>= i 100)
                                (str/ends-with? p "__")
                                (str/starts-with? s "__")
                                (str/ends-with? s "__"))
                          ;; likely produced by Clojure gensym on the
                          ;; compiler side, i.e. not deterministic
                          (do (swap! gensym-map add-replacement x p s)
                              (recur x))
                          x))
                      x))

                (string? x)
                (if-some [[_ p s] (re-matches #"^(LOCAL \d+: .* )(.+)$" x)]
                  (str p (-> (symbol nil s) (replacement)))
                  x)

                :else x))]
      (postwalk-meta replacement form))))

;;; ------------------------------------------------------------------------

(defn sqs-resolver ^Resolver [^symbol this-namespace]
  (let [ns-alias-map {"my.name.space" "my.name.space" ;required namespace
                      "space" "my.name.space"}        ;namespace alias
        ns-str (name this-namespace)
        import-map {"Object" Object}
        global-qname-map {"imported-var"
                          'my.name.space/remote-var}]
    (stub-resolver ns-str ns-alias-map import-map global-qname-map)))

(defn rdr                            ;pre: `cs` is a syntax-quote form
  ([^String cs]
   (rdr cs 'name.space))
  ([^String cs ^symbol this-namespace]
   (read1 cs (sqs-resolver this-namespace))))
(defn rdr-det [^String cs]
  (-> (rdr cs 'name.space) (deterministic-gensyms)))

(deftest reader-autoquote-test
  (is (= nil (rdr "`nil")))
  (is (= :foo (rdr "`:foo")))
  (is (= "foo" (rdr "`\"foo\"")))
  (is (= (object true) (rdr "`true")))
  (is (= (object 123) (rdr "`123"))))

(deftest autoquote-test
  (let [auto? (fn ^boolean [form]
                (identical? form (reader/template form)))]
    (is (auto? nil))
    (is (auto? Boolean/TRUE))           ;aka true
    (is (auto? Boolean/FALSE))          ;aka false
    (is (auto? "foo"))                  ;string
    
    (is (auto? (Integer/valueOf 1)))
    (is (auto? (Long/valueOf 1)))
    (is (auto? (Float/valueOf 1)))
    (is (auto? (Double/valueOf 1)))
    
    (is (auto? (Character/valueOf \a)))
    (is (auto? :foo))                   ;keyword
    (is (auto? #"foo"))                 ;regex
    
    ;; not produced by reader, but listed for completeness:
    (is (auto? Object))                 ;class
    (is (auto? (Byte/valueOf (byte 1))))
    (is (auto? (Short/valueOf (short 1))))))

(deftest symbol-test
  (is (= '(quote name.space/a)
         (rdr "`a"))))

(deftest map-test
  (is (= '(quote {name.space/a name.space/b})
         (rdr "`{a b}"))))

(deftest vector-test
  (is (= '(quote [name.space/a name.space/b])
         (rdr "`[a b]"))))

(deftest hash-set-test
  (is (= '(quote #{name.space/a name.space/b})
         (rdr "`#{a b}"))))

(deftest list-test
  (is (= '(quote (name.space/a name.space/b))
         (rdr "`(a b)")))
  (is (= '(quote (quote name.space/a))
         (rdr "`'a"))))


(deftest this-namespace-test
  (is (= '(quote name.space/a) (rdr "`a")))
  (is (= '(quote other.name.space/a) (rdr "`a" 'other.name.space))))

(deftest reader-autoquote-test
  (is (= nil (rdr "`nil")))
  (is (= :foo (rdr "`:foo")))
  (is (= "foo" (rdr "`\"foo\"")))
  (is (= (object true) (rdr "`true")))
  (is (= (object 123) (rdr "`123"))))

(deftest meta-test
  ;; Note: ^{:line ... :column ...} map entries are always dropped
  ;; from meta, even in the presence of other keys.
  (is (= '(quote (1 2)) (rdr "`(1 2)")))
  (is (= nil (-> (rdr "`(1 2)") (second) (meta))))    
  (is (= '(quote (1 2)) (rdr "`^:foo (1 2)")))
  (is (= '{:foo true} (-> (rdr "`^:foo (1 2)") (second) (meta))))

  (is (= '(tinyclj.core/with-meta
            (quote (1 2))
            (tinyclj.core/apply
             tinyclj.core/hash-map
             (tinyclj.core/seq
              (tinyclj.core/list :foo bar))))
         (rdr "`^{:foo ~bar} (1 2)"))))

(deftest quote-test
  (is (= '(quote name.space/a)
         (rdr "`a")))
  (is (= '(quote (quote name.space/a))
         (rdr "`'a")))
  (is (= '(quote (quote name.space/a))
         (rdr "``a")))
  (is (= '(quote (quote (quote name.space/a)))
         (rdr "``'a"))))

(deftest unquote-test
  (is (= 'a
         (rdr "`~a")))
  (is (= 'a
         (rdr "``~~a")))
  (is (= '(quote a)
         (rdr "`~'a")))
  (is (= '(quote a)
         (rdr "``~~'a")))
  
  (is (= '(tinyclj.core/seq
           (tinyclj.core/list (quote name.space/a) (quote name.space/b) x))
         (rdr "`(a b ~x)")))
  (is (= '(tinyclj.core/seq
           (tinyclj.core/list (quote name.space/a) x (quote name.space/b)))
         (rdr "`(a ~x b)")))
  (is (= '(tinyclj.core/seq
           (tinyclj.core/list x (quote name.space/a) (quote name.space/b)))
         (rdr "`(~x a b)"))))

(deftest quoted-unquote-test
  (is (= '(quote (tinyclj.core/unquote a))
         (rdr "'~a"))))

(deftest unquote-splicing-test
  (is (= '(tinyclj.core/seq
           (tinyclj.core/concat (tinyclj.core/list (quote name.space/a))
                                (tinyclj.core/list (quote name.space/b))
                                [x y]))
         (rdr "`(a b ~@[x y])")))
  (is (= '(tinyclj.core/seq
           (tinyclj.core/concat (tinyclj.core/list (quote name.space/a))
                                [x y]
                                (tinyclj.core/list (quote name.space/b))))
         (rdr "`(a ~@[x y] b)")))
  (is (= '(tinyclj.core/seq
           (tinyclj.core/concat [x y]
                                (tinyclj.core/list (quote name.space/a))
                                (tinyclj.core/list (quote name.space/b))))
         (rdr "`(~@[x y] a b)")))

  (is (thrown-with-msg? WrongInfo
                        #"unquote-splicing ~@ outside of list"
                        (rdr "`~@[x y]"))))


(deftest gensym-literal-test
  (is (= '(quote foo__$1$__auto__)
         (rdr-det "`foo#")))
  (is (= '(quote (foo__$1$__auto__ foo__$1$__auto__))
         (rdr-det "`(foo# foo#)")))

  (is (= '(quote (foo__$1$__auto__ (quote foo__$2$__auto__)))
         (rdr-det "`(foo# `foo#)")))
  (is (= '(quote (foo__$1$__auto__
                  (quote foo__$2$__auto__)
                  foo__$1$__auto__))
         (rdr-det "`(foo# `foo# foo#)")))

  ;; Disallow gensym literal outside of syntax-quote, in analogy to
  ;; the unquotes.  Clojure 1.10.1 accepts both, although the
  ;; LispReader source code has a similar error message that is not
  ;; commented out.
  #_
  (is (thrown-with-msg? WrongInfo
                        #"1:0: gensym literal outside of syntax quote `\.\.\."
                        (rdr "foo#")))
  #_
  (is (thrown-with-msg? WrongInfo
                        #"1:2: gensym literal outside of syntax quote `\.\.\."
                        (rdr "`~foo#"))))

(deftest new-shorthand-test
  (is (= '(quote java.lang.Object.)
         (rdr "`Object.")))
  (is (= '(quote java.lang.Object.)
         (rdr "`java.lang.Object.")))

  (is (= '(quote NoSuchClass.)
         (rdr "`NoSuchClass.")))
  (is (= '(quote java.lang.NoSuchClass.)
         (rdr "`java.lang.NoSuchClass."))))

(deftest class-name-test
  (is (= '(quote java.lang.Object)
         (rdr "`Object")))
  (is (= '(quote java.lang.Object)
         (rdr "`java.lang.Object")))

  (is (= '(quote name.space/NoSuchClass)
         (rdr "`NoSuchClass")))
  (is (= '(quote java.lang.NoSuchClass)
         (rdr "`java.lang.NoSuchClass"))))

(deftest class-member-test
  (is (= '(quote java.lang.Object/foo)
         (rdr "`Object/foo")))
  (is (= '(quote java.lang.Object/foo)
         (rdr "`java.lang.Object/foo")))

  (is (= '(quote NoSuchClass/foo)
         (rdr "`NoSuchClass/foo")))
  (is (= '(quote java.lang.NoSuchClass/foo)
         (rdr "`java.lang.NoSuchClass/foo"))))

(deftest method-name-test
  (is (= '(quote .foo)
         (rdr "`.foo"))))

(deftest namespaced-global-test
  (is (= '(quote my.name.space/foo)
         (rdr "`my.name.space/foo")))
  (is (= '(quote my.name.space/foo)
         (rdr "`space/foo")))

  (is (= '(quote no.such.name.space/foo)
         (rdr "`no.such.name.space/foo")))
  (is (= '(quote nsns/foo)
         (rdr "`nsns/foo"))))

(deftest globals-test
  (is (= '(quote name.space/no-such-var)
         (rdr "`no-such-var")))
  (is (= '(quote name.space/local-var)
         (rdr "`local-var")))
  (is (= '(quote my.name.space/remote-var)
         (rdr "`imported-var")))

  (is (= '(quote ..)
         (rdr "`.."))))

;;; ------------------------------------------------------------------------

(deftest unreadable-test
  (is (thrown-with-msg? WrongInfo #"unreadable form"
                        (read1 "#<"))))

(deftest discard-test
  (is (= nil (read-all "#_a")))
  
  ;; discards stack
  (is (= 'b (read1 "#_a b")))
  (is (= 'c (read1 "#_a #_b c")))
  (is (= 'c (read1 "#_#_a b c")))

  ;; discard at end of collection is ok
  (is (= '() (read1 "(#_{})")))
  (is (= '() (read1 "(#_#_{}{})")))

  ;; errors in discarded forms are ignored by the reader
  (is (= 'b (read1 "#_#< b"))))

(deftest empty-list-test
  (is (identical? () (read1 "()")))
  (is (identical? () (read1 "( )")))
  (is (identical? () (read1 "(\n)"))))

(deftest integer-value-test
  ;; test for expected box type: match against Integer only
  (is (.equals (Integer/valueOf 0) (read1 "0")))
  (is (.equals (Integer/valueOf 2147483647) (read1 "2147483647")))
  (is (.equals (Integer/valueOf -2147483648) (read1 "-2147483648")))
  (is (.equals (Integer/valueOf 0) (read1 "10r0")))
  (is (.equals (Integer/valueOf 2147483647) (read1 "10r2147483647")))
  (is (.equals (Integer/valueOf -2147483648) (read1 "-10r2147483648")))

  (is (= (object 2) (read1 "2r10")))
  (is (= (object 8) (read1 "010")))
  (is (= (object 8) (read1 "8r10")))
  (is (= (object 16) (read1 "0x10")))
  (is (= (object 16) (read1 "16r10")))

  (is (= (object 2) (read1 "+2r10")))
  (is (= (object 8) (read1 "+010")))
  (is (= (object 8) (read1 "+8r10")))
  (is (= (object 16) (read1 "+0x10")))
  (is (= (object 16) (read1 "+16r10")))

  (is (= (object -2) (read1 "-2r10")))
  (is (= (object -8) (read1 "-010")))
  (is (= (object -8) (read1 "-8r10")))
  (is (= (object -16) (read1 "-0x10")))
  (is (= (object -16) (read1 "-16r10"))))

(deftest float-value-test
  (is (.equals (Double/valueOf 0.123) (read1 "0.123")))
  (is (.equals (Double/valueOf 0.123) (read1 "+0.123")))
  (is (.equals (Double/valueOf -0.123) (read1 "-0.123")))
  (is (.equals (Double/valueOf 1.23e4) (read1 "1.23e4")))
  (is (.equals (Double/valueOf 1.23e4) (read1 "1.23E4")))
  (is (.equals (Double/valueOf 1.23e4) (read1 "+1.23e4")))
  (is (.equals (Double/valueOf -1.23e4) (read1 "-1.23e4"))))

(deftest suffixed-value-test
  ;; suffix [lL] produces instance of Long (invalid in Clojure)
  (is (.equals (Long/valueOf 0) (read1 "0l")))
  (is (.equals (Long/valueOf 0) (read1 "0L")))
  (is (.equals (Long/valueOf 0) (read1 "+0l")))
  (is (.equals (Long/valueOf 0) (read1 "-0l")))
  (is (.equals (Long/valueOf 123) (read1 "123l")))
  ;; [fF] produces instance of Float (invalid in Clojure)
  (is (.equals (Float/valueOf "0.0") (read1 "0.0f")))
  (is (.equals (Float/valueOf "0.0") (read1 "0.0F")))
  (is (.equals (Float/valueOf "0.0") (read1 "+0.0f")))
  (is (.equals (Float/valueOf "-0.0") (read1 "-0.0f")))
  (is (.equals (Float/valueOf "1.23") (read1 "1.23f")))
  ;; these are parsed as floats, *not* as integers:
  (is (.equals (Float/valueOf "0.0") (read1 "0f")))
  (is (.equals (Float/valueOf "0.0") (read1 "0F")))
  (is (.equals (Float/valueOf "123.0") (read1 "123f"))))

(deftest number-error-test
  (is (thrown-with-msg? WrongInfo #"invalid integer number: 0128"
                        (read1 "0128")))
  (is (thrown-with-msg? WrongInfo #"invalid number: 1e"
                        (read1 "1e")))
  (is (thrown-with-msg? WrongInfo #"number format error: Radix out of range"
                        (read1 "1r0")))
  (is (thrown-with-msg? WrongInfo #"number format error: Radix out of range"
                        (read1 "37r0"))))

(deftest symbol-value-test
  (is (= (symbol nil "abc") (read1 "abc")))
  (is (= (symbol "abc" "def") (read1 "abc/def")))

  ;; single slash is allowed and mapped to symbol
  (is (= (symbol nil "/") (read1 "/")))

  ;; any other token starting or ending with a slash in an error
  (is (thrown-with-msg? WrongInfo #"invalid token: /a"
                        (read1 "/a")))
  (is (thrown-with-msg? WrongInfo #"invalid token: //"
                        (read1 "//")))
  (is (thrown-with-msg? WrongInfo #"invalid token: a/"
                        (read1 "a/")))
  
  ;; two or more slashes are allowed
  (is (= (symbol "a" "/") (read1 "a//")))
  (is (= (symbol "a" "/b") (read1 "a//b")))
  (is (= (symbol "a" "//b") (read1 "a///b"))))

(deftest keyword-value-test
  (is (= :abc (read1 ":abc")))
  (is (= :abc/def (read1 ":abc/def")))
  (is (= :nil (read1 ":nil")))
  (is (= :nil/nil (read1 ":nil/nil"))))

(deftest keyword-auto-resolving-test
  (let [ns-str "this-namespace"
        ns-alias {"alias" "aliased-namespace"
                  "known-namespace" "known-namespace"}]
    (is (= [:this-namespace/bar]
           (read-all "::bar" (stub-resolver ns-str ns-alias))))
    (is (= [:aliased-namespace/bar]
           (read-all "::alias/bar" (stub-resolver ns-str ns-alias))))
    (is (= [:known-namespace/bar]
           (read-all "::known-namespace/bar" (stub-resolver ns-str ns-alias))))

    (is (thrown-with-msg? WrongInfo #"no such namespace: foo"
                          (read1 "::foo/bar")))))

(deftest symbolic-values-test
  (is (= nil (read1 "nil")))
  (is (= (object true) (read1 "true")))
  (is (= (object false) (read1 "false")))

  (is (= (object Double/POSITIVE_INFINITY) (read1 "##Inf")))
  (is (= (object Double/NEGATIVE_INFINITY) (read1 "##-Inf")))
  (is (.isNaN ^Double (read1 "##NaN")))

  (is (thrown-with-msg? WrongInfo #"unknown symbolic value: ##nan"
                        (read1 "##nan"))))

(deftest character-value-test
  (letfn [(readc ^int [^String s]
            (int ^char (read1 s)))]
    (is (= 32 (readc "\\space")))
    (is (= 40 (readc "\\(")))
    
    ;; from test/clojure/test_clojure/reader.cljc
    (is (= 0x6f (readc "\\o")))
    (is (= 0 (readc "\\o0")))
    (is (= 0 (readc "\\o000")))
    (is (= 047 (readc "\\o47")))
    (is (= 0377 (readc "\\o377")))

    (is (= 0x75 (readc "\\u")))
    (is (= 0x41 (readc "\\u0041")))
    (is (= 0 (readc "\\u0000")))
    (is (= 0xd7ff (readc "\\ud7ff")))
    (is (= 0xe000 (readc "\\ue000")))
    (is (= 0xffff (readc "\\uffff")))))

(deftest character-error-test
  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\u\+0009"
                        (read1 "\\u+0009")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\o\+40"
                        (read1 "\\o+40")))
  
  ;; from test/clojure/test_clojure/reader.cljc
  (is (thrown-with-msg? WrongInfo
                        #"unsupported character: \\"
                        (read1 "\\")))
  (is (thrown-with-msg? WrongInfo
                        #"unsupported character: \\00"
                        (read1 "\\00")))
  (is (thrown-with-msg? WrongInfo
                        #"unsupported character: \\0009"
                        (read1 "\\0009")))

  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\o378"
                        (read1 "\\o378")))
  (is (thrown-with-msg? WrongInfo
                        #"octal escape sequence must be in range \[0, 377\]"
                        (read1 "\\o400")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\o800"
                        (read1 "\\o800")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\oand"
                        (read1 "\\oand")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid octal escape sequence length: \\o0470"
                        (read1 "\\o0470")))

  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\u0"
                        (read1 "\\u0")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\ug"
                        (read1 "\\ug")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\u000"
                        (read1 "\\u000")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid surrogate character: \\ud800"
                        (read1 "\\ud800")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid surrogate character: \\udfff"
                        (read1 "\\udfff")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\u004"
                        (read1 "\\u004")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\u00041"
                        (read1 "\\u00041")))
  (is (thrown-with-msg? WrongInfo
                        #"invalid character code: \\u004g"
                        (read1 "\\u004g"))))

(defn reads [^String s]
  (mapv #(int ^char %) (read1 (str "\"" s "\""))))

(deftest string-value-test
  ;; ok in jshell (OpenJDK 14), error in Clojure (as of 1.10.1)
  (is (= [0 56 48 48] (reads "\\0800")))
  (is (= [0 56 48] (reads "\\0080")))
  (is (= [2 97 110 100] (reads "\\2and")))
  
  ;; from test/clojure/test_clojure/reader.cljc
  (is (= [] (reads "")))
  (is (= [34] (reads "\\\"")))
  (is (= [10] (reads "\\n")))

  (is (= [0] (reads "\\0")))
  (is (= [0] (reads "\\000")))
  (is (= [3] (reads "\\3")))
  (is (= [3] (reads "\\03")))
  (is (= [3] (reads "\\003")))
  (is (= [0 51] (reads "\\0003")))
  (is (= [3 48] (reads "\\0030")))
  (is (= [0377] (reads "\\377")))
  (is (= [0 56] (reads "\\0008")))

  (is (= [0] (reads "\\u0000")))
  (is (= [0xd7ff] (reads "\\ud7ff")))
  (is (= [0xd800] (reads "\\ud800")))
  (is (= [0xdfff] (reads "\\udfff")))
  (is (= [0xe000] (reads "\\ue000")))
  (is (= [0xffff] (reads "\\uffff")))
  (is (= [4 49] (reads "\\u00041"))))

(deftest string-error-test
  ;; from test/clojure/test_clojure/reader.cljc
  
  (is (thrown-with-msg? WrongInfo #"unterminated string"
                        (reads "\\")))
  (is (thrown-with-msg? WrongInfo #"unsupported escape character: \\o"
                        (reads "\\o")))

  (is (thrown-with-msg? WrongInfo #"octal escape sequence must be in range \[0, 377\]"
                        (reads "\\400")))
  (is (thrown-with-msg? WrongInfo #"invalid character code: \\8"
                        (reads "\\8")))
  (is (thrown-with-msg? WrongInfo #"invalid character code: \\800"
                        (reads "\\8000")))

  (is (thrown-with-msg? WrongInfo #"invalid character code: \\u"
                        (reads "\\u")))
  (is (thrown-with-msg? WrongInfo #"invalid character code: \\ug"
                        (reads "\\ug")))
  (is (thrown-with-msg? WrongInfo #"invalid character code: \\ug000"
                        (reads "\\ug000")))
  (is (thrown-with-msg? WrongInfo #"invalid character code: \\u0"
                        (reads "\\u0")))
  (is (thrown-with-msg? WrongInfo #"invalid character code: \\u004"
                        (reads "\\u004")))
  (is (thrown-with-msg? WrongInfo #"invalid character code: \\u004g"
                        (reads "\\u004g"))))

(defn readr [^String s]
  (second (read1 (str "#\"" s "\""))))

(deftest regex-value-test
  (is (= "" (readr "")))
  (is (= "abc" (readr "abc")))
  (is (= "\n" (readr "\n")))

  (is (= "\\\"" (readr "\\\"")))
  (is (= "\\\\" (readr "\\\\"))))

;;; ------------------------------------------------------------------------

(deftest collection-value-test
  (is (= '() (read1 "()")))
  (is (= '(a) (read1 "(a)")))

  (is (= '[] (read1 "[]")))
  (is (= '[a] (read1 "[a]")))

  (is (= '{} (read1 "{}")))
  (is (= '{a b} (read1 "{a b}")))

  (is (= '#{} (read1 "#{}")))
  (is (= '#{a} (read1 "#{a}"))))

(deftest collection-error-test
  (is (thrown-with-msg? WrongInfo #"invalid start of token"
                        (read1 "{a}")))
  (is (thrown-with-msg? WrongInfo #"duplicate key: a"
                        (read1 "{a 1, a 2}")))
  (is (thrown-with-msg? WrongInfo #"duplicate key: a"
                        (read1 "#{a a}"))))



(deftest prefix-value-test
  (is (= '(quote a) (read1 "'a")))
  (is (= '(deref a) (read1 "@a")))
  
  (is (= '(tinyclj.core/unquote a)
         (read1 "~a")))
  (is (= '(tinyclj.core/unquote a)
         (read1 "`~~a")))
  (is (= '(tinyclj.core/unquote-splicing a)
         (read1 "~@a")))
  (is (= '(tinyclj.core/unquote-splicing a)
         (read1 "`~~@a"))))

(deftest meta-value-test
  (is (= '{:k v} (meta (read1 "^{:k v} a"))))

  ;; Meta maps to the left overwrite maps to the right.  This happens
  ;; early, in the reader.  As a consequence, superseeded map entries
  ;; are not passed on to the compiler and are never evaluated.
  (is (= 'a (read1 "^{:k 1} ^{:k 2, :l 2} a")))
  (is (= '{:k 1, :l 2} (meta (read1 "^{:k 1} ^{:k 2, :l 2} a"))))
  (is (= '(quote a) (read1 "^{:k 1} ' ^{:k 2, :l 2} a")))
  
  (is (= '{:k 1, :line 1}
         (meta (read1 "^{:k 1} ' ^{:k 2, :l 2} a"))))

  (is (thrown-with-msg? WrongInfo
                        #"metadata must be symbol, keyword, string, map, or vector"
                        (read1 "^123 a")))
  (is (thrown-with-msg? WrongInfo
                        #"metadata can only be applied to IMeta instances"
                        (read1 "^:foo nil"))))

(deftest meta-discard-test
  (is (= 'a (read1 "#_^:foo bar a")))
  (is (= '{:foo true} (meta (read1 "^:foo #_b a")))))

(deftest meta-param-tags-test
  (is (= '{:param-tags []} (meta (read1 "^[] foo"))))
  (is (= '{:param-tags [int]} (meta (read1 "^[int] foo"))))
  (is (= '{:param-tags [double int]} (meta (read1 "^[double int] foo"))))
  (is (= '{:param-tags [_]} (meta (read1 "^[_] foo"))))
  (is (= '{:param-tags [_ _]} (meta (read1 "^[_ _] foo"))))
  (is (= '{:param-tags [_ long]} (meta (read1 "^[_ long] foo")))))

(deftest line-column-test
  ;; Note: Clojure's reader does not add implicit :line/:column meta
  ;; to (), which means its identity is preserved until meta is
  ;; explicitly applied.
  (is (= nil (-> (read1 "()") (meta))))
  (is (= (object 1) (-> (read1 "(1 2 3)") (meta) :line))))

;;; ------------------------------------------------------------------------

(deftest synquote-wrapping-test
  ;; Suffix "...#" has no special meaning for keywords.
  (is (= :foo/bar# (read1 ":foo/bar#")))
  (is (= :foo# (read1 ":foo#")))
  
  (is (= '(quote :foo/bar#) (read1 "':foo/bar#")))
  (is (= '(quote :foo#) (read1 "':foo#")))

  (is (= :foo/bar# (read1 "`:foo/bar#")))
  (is (= :foo# (read1 "`:foo#")))
  
  ;; In Clojure (as of 1.11-rc1) the symbol suffix "...#" has no
  ;; special meaning outside of a syntax quote form "`...".
  (is (= 'foo/bar# (read1 "foo/bar#")))
  (is (= 'foo# (read1 "foo#")))
  
  (is (= (list 'quote (symbol "foo" "bar#")) (read1 "'foo/bar#")))
  (is (= (list 'quote (symbol nil "foo#")) (read1 "'foo#")))
  
  (is (= (list 'quote (symbol "foo" "bar#"))
         (read1 "`foo/bar#")))
  (is (= (list 'quote 'foo__$1$__auto__)
         (-> (read1 "`foo#")
             (deterministic-gensyms))))
  (is (= (list 'quote (list 'quote 'foo__$1$__auto__))
         (-> (read1 "'`foo#")
             (deterministic-gensyms))))
  (is (= (list 'quote (list 'quote 'foo__$1$__auto__))
         (-> (read1 "`'foo#")
             (deterministic-gensyms)))))

(deftest fnlit-test
  ;; no parameters
  (is (= '(fn* [] ())
         (read1 "#()")))
  (is (= 'auto-return-type (-> (read1 "#()") (second) (meta) :tag)))

  ;; one parameter
  (is (= '(fn* [p$1$__fnlit__] (p$1$__fnlit__))
         (-> (read1 "#(%)")
             (deterministic-gensyms))))
  (is (= '(fn* [p$1$__fnlit__] (p$1$__fnlit__))
         (-> (read1 "#(%1)")
             (deterministic-gensyms))))
  (is (= '(fn* [p$1$__fnlit__] (+ p$1$__fnlit__ p$1$__fnlit__))
         (-> (read1 "#(+ % %1)")
             (deterministic-gensyms))))

  ;; two parameters
  (is (= '(fn* [p$1$__fnlit__ p$2$__fnlit__] (p$1$__fnlit__ p$2$__fnlit__))
         (-> (read1 "#(%1 %2)")
             (deterministic-gensyms))))
  (is (= '(fn* [p$1$__fnlit__ p$2$__fnlit__] (p$2$__fnlit__ p$1$__fnlit__))
         (-> (read1 "#(%2 %1)")
             (deterministic-gensyms))))
  (is (= '(fn* [p$1$__fnlit__ p$2$__fnlit__] (p$2$__fnlit__))
         (-> (read1 "#(%2)")
             (deterministic-gensyms))))

  ;; rest parameter
  (is (= '(fn* [& rest__fnlit__] (rest__fnlit__))
         (-> (read1 "#(%&)")
             (deterministic-gensyms))))
  (is (= '(fn* [p$1$__fnlit__ & rest__fnlit__] (p$1$__fnlit__ rest__fnlit__))
         (-> (read1 "#(%1 %&)")
             (deterministic-gensyms))))
  (is (= '(fn* [p$1$__fnlit__ p$2$__fnlit__ & rest__fnlit__] (rest__fnlit__ p$2$__fnlit__))
         (-> (read1 "#(%& %2)")
             (deterministic-gensyms))))

  (is (thrown-with-msg? WrongInfo
                        #"nested fn literals #\(\.\.\.\) are not allowed"
                        (read1 "#(#())")))
  (is (thrown-with-msg? WrongInfo
                        #"arg literal must be %, %& or %integer"
                        (read1 "#(%+1)"))))

(deftest fnlit-type-tag-test
  ;; type tag moved into parameter and meta is retained by argument
  ;; use
  (is (= '(fn* [p$1$__fnlit__] (p$1$__fnlit__))
         (-> (read1 "#(^Object %)")
             (deterministic-gensyms))))
  (is (= 'Object
         (-> (read1 "#(^Object %)") (second) (first) (meta) :tag)))
  (is (= '{:tag Object}
         (-> (read1 "#(^Object %)") (nth 2) (first) (meta))))

  ;; all uses must agree
  (is (thrown-with-msg?
       WrongInfo
       #"conflicting type tags for arg literal: \(none\) int"
       (-> (read1 "#(+ % ^int %)") (second) (first) (meta) :tag)))
  (is (thrown-with-msg?
       WrongInfo
       #"conflicting type tags for arg literal: float int"
       (read1 "#(+ ^int % ^float %)")))

  ;; second meta (to left) overwrites first meta (to right)
  (is (= 'int
         (-> (read1 "#(^int ^long/1 %)") (second) (first) (meta) :tag)))
  (is (thrown-with-msg?
       WrongInfo
       #"conflicting type tags for arg literal: int long"
       (-> (read1 "#(+ ^long % ^int ^long %)") (second) (first) (meta) :tag))))
