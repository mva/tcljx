(ns tcljx.alpha.ptest-test
  (:require [tcljx.alpha.ptest :refer :all]
            [tcljx.alpha.ptest__impl :as impl])
  (:import (tcljx.alpha.ptest__impl Report)))

(defmacro defmockup-test [nm & body]
  (assert (symbol? nm))
  (assert (contains? (meta nm) :line))
  ;; do not mark as :test and use given line number
  `(defn ~(vary-meta nm assoc :var true) ^void []
     ~@body))

(defn throw-whatever []
  (throw (Exception. "whatever")))

(defn run-test [^String suffix]         ;returns [Report String]
  ;; Note: This test module can only run specific test vars identified
  ;; by name (i.e., the ones defined via `defmockup-test`).  A full
  ;; namespace scan would pick up the non-mockup tests, leading to
  ;; cyclic havoc.
  (assert (.startsWith suffix "/"))
  (let [spec (str "tcljx.alpha.ptest-test" suffix)
        nmspv (impl/parse-scope-spec spec)
        vreport (volatile! nil)
        out (with-out-str
              (vreset! vreport (impl/run-tests nmspv)))]
    ;; (println "##-start-##" :run-test nmspv "###")
    ;; (println out)
    ;; (println "##-end-##" :run-test nmspv "###")
    [@vreport out]))



(defmockup-test ^{:line 1000} empty-test-mockup
  nil)
(deftest empty-test
  (let [[^Report rep] (run-test "/empty-test-mockup")]
    (is (= 1 (.tests rep)))             ;counted as one test?
    (is (= 0 (impl/is-count rep)))))    ;no `is` asserts are counted

(defmockup-test ^{:line 1100} throw-outside-is-mockup
  (throw-whatever))
(deftest throw-outside-is
  (let [[^Report rep out] (run-test "/throw-outside-is-mockup")]
    (is (= (.is-error rep) 1))          ;counted in report?
    (is (re-find #"ERROR" out))         ;has an ERROR header
    (is (re-find #"Exception: whatever" out)))) ;msg from printStackTrace

(defmockup-test ^{:line 1200} throw-inside-is-mockup
  ^{:line 1205} (is (throw-whatever)))
(deftest throw-inside-is
  (let [[^Report rep out] (run-test "/throw-inside-is-mockup")]
    (is (= (.is-error rep) 1))          ;counted in report?
    (is (re-find #"ERROR" out))         ;has an ERROR header
    (is (re-find #"Exception: whatever" out)))) ;msg from printStackTrace



(defmockup-test ^{:line 2000} is-boolean-true-mockup
  ^{:line 2005} (is true))
(deftest is-boolean-true
  (let [[^Report rep out] (run-test "/is-boolean-true-mockup")]
    (is (= (.is-pass rep) 1))))         ;counted in report?

(defmockup-test ^{:line 2100} is-boolean-false-mockup
  ^{:line 2105} (is false))
(deftest is-boolean-false
  (let [[^Report rep out] (run-test "/is-boolean-false-mockup")]
    (is (= (.is-fail rep) 1))         ;counted in report?
    (is (re-find #"expression evaluates to false" out))
    (is (re-find #"expected:.* .*true" out))
    (is (re-find #"actual:.* .*false" out))))



(defmockup-test ^{:line 3000} is-equivalent-mockup
  ^{:line 3005} (is (= [1 2 3] [1 2 3])))
(deftest is-equivalent
  (let [[^Report rep out] (run-test "/is-equivalent-mockup")]
    (is (= (.is-pass rep) 1))))

(defmockup-test ^{:line 3100} is-not-equivalent-mockup
  ^{:line 3105} (is (= [1 2 3] [1 2 4])))
(deftest is-not-equivalent
  (let [[^Report rep out] (run-test "/is-not-equivalent-mockup")]
    (is (= (.is-fail rep) 1))
    (is (re-find #"expected not equivalent to actual value" out))
    (is (re-find #"expected:.* .*\[1 2 .*3.*\]" out))
    (is (re-find #"actual:.* .*\[1 2 .*4.*\]" out))))



(defmockup-test ^{:line 4000} is-thrown-mockup
  ^{:line 4005} (is (thrown? Exception (throw-whatever))))
(deftest is-thrown
  (let [[^Report rep out] (run-test "/is-thrown-mockup")]
    (is (= (.is-pass rep) 1))))

(defmockup-test ^{:line 4100} is-not-thrown-mockup
  ^{:line 4105} (is (thrown? Error (throw-whatever))))
(deftest is-not-thrown
  (let [[^Report rep out] (run-test "/is-not-thrown-mockup")]
    (is (= (.is-fail rep) 1))
    (is (re-find #"unexpected exception type" out))
    (is (re-find #"expected:.* .*class java.lang.Error" out))
    (is (re-find #"actual:.* .*class java.lang.Exception" out))))



(defmockup-test ^{:line 5000} is-thrown-with-msg-mockup
  ^{:line 5005} (is (thrown-with-msg? Exception #"what"
                                      (throw-whatever))))
(deftest is-thrown-with-msg
  (let [[^Report rep out] (run-test "/is-thrown-with-msg-mockup")]
    (is (= (.is-pass rep) 1))))

(defmockup-test ^{:line 5100} is-not-thrown-with-type-mockup
  ^{:line 5105} (is (thrown-with-msg? Error #"what"
                                      (throw-whatever))))
(deftest is-not-thrown-with-type
  (let [[^Report rep out] (run-test "/is-not-thrown-with-type-mockup")]
    (is (= (.is-fail rep) 1))
    (is (re-find #"unexpected exception type" out))
    (is (re-find #"expected:.* .*class java.lang.Error" out))
    (is (re-find #"actual:.* .*class java.lang.Exception" out))))

(defmockup-test ^{:line 5200} is-not-thrown-with-type-mockup
  ^{:line 5205} (is (thrown-with-msg? Exception #"whatsoever"
                                      (throw-whatever))))
(deftest is-not-thrown-with-type
  (let [[^Report rep out] (run-test "/is-not-thrown-with-type-mockup")]
    (is (= (.is-fail rep) 1))
    (is (re-find #"unexpected exception message" out))
    (is (re-find #"expected:.* .*whatsoever" out))
    (is (re-find #"actual:.* .*whatever" out))))



(defmockup-test ^{:line 6000} output-pass-mockup
  ^{:line 6005} (is (do (println "to out")
                        (binding [*out* *err*] (println "to err"))
                        true)))
(deftest output-pass
  (let [[^Report rep out] (run-test "/output-pass-mockup")]
    (is (= (.is-pass rep) 1))
    (is (re-find #"to out\nto err\n" out))))

(defmockup-test ^{:line 6000} output-fail-mockup
  ^{:line 6005} (is (do (println "to out")
                        (binding [*out* *err*] (println "to err"))
                        false)))
(deftest output-fail
  (let [[^Report rep out] (run-test "/output-fail-mockup")]
    (is (= (.is-fail rep) 1))
    (is (re-find #"to out\nto err\n" out))))



(defmockup-test ^{:line 7000} select-is-by-line-mockup
  ^{:line 7005} (is (= :a :a))
  ^{:line 7010} (is (= :a :b)))
(deftest select-is-by-line-of-test
  ;; line number of the test matches the var but no `is` tests
  (let [[^Report rep out] (run-test "/select-is-by-line-mockup:7000")]
    (is (= 0 (impl/is-count rep))))
  ;; line number of one of the the `is` selects only this one
  (let [[^Report rep out] (run-test "/select-is-by-line-mockup:7005")]
    (is (= 1 (impl/is-count rep)))
    (is (= 1 (.is-pass rep))))
  (let [[^Report rep out] (run-test "/select-is-by-line-mockup:7010")]
    (is (= 1 (impl/is-count rep)))
    (is (= 1 (.is-fail rep))))
  ;; any other line number selects none
  (let [[^Report rep out] (run-test "/select-is-by-line-mockup:7999")]
    (is (= 0 (impl/is-count rep)))))
