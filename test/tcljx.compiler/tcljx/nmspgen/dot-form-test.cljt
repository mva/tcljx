;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljx.nmspgen.dot-form-test
  (:require [tcljx.nmspgen.bootstrap :refer [asm-expr call-expr nmsp
                                              is-asm is-asm* is-error]]
            [tcljx.alpha.ptest :refer :all]))

;;; [The Dot special form]
;;; https://clojure.org/reference/java_interop#_the_dot_special_form
;;; [Member access]
;;; https://clojure.org/reference/java_interop#_member_access

(deftest dot-instance-test
  (let [exp-asm '[(LDC "fred")
                  (INVOKEVIRTUAL String "toUpperCase" "()Ljava/lang/String;")
                  (ARETURN)]]
    ;; syntax variants
    (is-asm* (. "fred" toUpperCase) exp-asm)
    (is-asm* (. "fred" (toUpperCase)) exp-asm)
    (is-asm* (.toUpperCase "fred") exp-asm))
  ;; evaluation
  (is (= "FRED"
         (call-expr (. "fred" toUpperCase))))
  ;; error cases
  (is-error (. "fred" no_such_method 123)
            #"no virtual method matching .*no_such_method.*")
  
  (is-error (. "fred" (no_such_method))
            #"no virtual member matching .*no_such_method.*")
  
  (is-error (. "fred" no_such_method)
            #"no virtual member matching .*no_such_method.*")
  
  (is-error (. "fred" -toUpperCase)
            #"no virtual field matching .*toUpperCase.*")
  
  ;; what about "exotic" instance values?
  (is-error (. nil no_such_method)
            #"no virtual member matching .*no_such_method.*")
  
  (is-error (. 123 no_such_method)
            #"no virtual member matching .*no_such_method.*")
  
  (is-error (. nil (no_such_method))
            #"no virtual member matching .*no_such_method.*")
  
  (is-error (. 123 (no_such_method))
            #"no virtual member matching .*no_such_method.*")
  
  ;; syntax errors
  (is-error (. "fred" (toUpperCase) 123) #"unexpected arguments at end")
  (is-error (. "fred") #"expect unqualified symbol")
  (is-error (.) #"expect unqualified symbol"))

(deftest dot-instance-class-test
  (let [exp-asm '[(LDC String)
                  (INVOKEVIRTUAL Class "getName" "()Ljava/lang/String;")
                  (ARETURN)]]
    ;; syntax variants
    (is-asm* (. (identity java.lang.String) getName) exp-asm)
    (is-asm* (. (identity java.lang.String) (getName)) exp-asm)
    (is-asm* (.getName java.lang.String) exp-asm))
  ;; evaluation
  (is (= "java.lang.String"
         (call-expr (. (identity java.lang.String) getName)))))

(deftest dot-instance-field-test
  (is-error (. "fred" no_such_field)
            #"no virtual member matching .*no_such_field.*")
  
  (is-error (. "fred" -no_such_field)
            #"no virtual field matching .*no_such_field.*")
  
  ;; exists as static field:
  (is-error (. "fred" CASE_INSENSITIVE_ORDER)
            #"no virtual member matching .*CASE_INSENSITIVE_ORDER.*"))


(deftest dot-classname-method-test
  (let [exp-asm '[(LDC "java.vm.version")
                  (INVOKESTATIC System "getProperty"
                                "(Ljava/lang/String;)Ljava/lang/String;")
                  (ARETURN)]]
    ;; syntax variants
    (is-asm* (. java.lang.System getProperty "java.vm.version") exp-asm)
    (is-asm* (. java.lang.System (getProperty "java.vm.version")) exp-asm)
    (is-asm* (java.lang.System/getProperty "java.vm.version") exp-asm)
    (is-asm* (System/getProperty "java.vm.version") exp-asm))
  ;; evaluation
  (is (string? (call-expr (. java.lang.System getProperty "java.vm.version"))))
  ;; error cases
  (is-error (. java.lang.String toUpperCase 123)
            #"no static method matching .*toUpperCase.*")
  
  (is-error (. java.lang.String (toUpperCase))
            #"no static member matching .*toUpperCase.*")
  
  (is-error (. java.lang.String toUpperCase)
            #"no static member matching .*toUpperCase.*"))

(deftest dot-classname-field-test
  (let [exp-asm '[(GETSTATIC String "CASE_INSENSITIVE_ORDER"
                             "Ljava/util/Comparator;")
                  (ARETURN)]]
    ;; syntax variants
    (is-asm* (. java.lang.String CASE_INSENSITIVE_ORDER) exp-asm)
    (is-asm* (. java.lang.String -CASE_INSENSITIVE_ORDER) exp-asm)
    (is-asm* (. String CASE_INSENSITIVE_ORDER) exp-asm)
    (is-asm* java.lang.String/CASE_INSENSITIVE_ORDER exp-asm)
    (is-asm* String/CASE_INSENSITIVE_ORDER exp-asm)
    ;; evaluation
    (is (= System/out (call-expr (. java.lang.System out))))))


(deftest dot-macro-test
  (is-error (..) #"missing argument")
  (is-error (.. x) #"missing argument")
  (is-error (.x) #"missing argument"))

(deftest dot-call-inexact
  ;; widen float argument to double before calling `sqrt`
  (is (= '[(DCONST_1)
           (INVOKESTATIC Math "sqrt" "(D)D")
           (DRETURN)]
         (asm-expr (Math/sqrt (float 1.0))))))

(deftest static-call-inexact-match
  (is (= '[["LOCAL 0: Object name"]
           (ALOAD_0)
           (CHECKCAST String)
           (INVOKESTATIC Symbol "intern" "(Ljava/lang/String;)Lclojure/lang/Symbol;")
           (ARETURN)]
         (asm-expr [^Object name] (clojure.lang.Symbol/intern name)))))

(deftest polymorphic-signature-virtual
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invoke" "()Ljava/lang/Object;")
           (ARETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   (.invoke mh))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invokeExact" "()Ljava/lang/Object;")
           (ARETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   (.invokeExact mh))))

  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (ICONST_1)
           (INVOKEVIRTUAL MethodHandle "invoke" "(Z)Ljava/lang/Object;")
           (ARETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   (.invoke mh true))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (ICONST_1)
           (BIPUSH 123)
           (INVOKEVIRTUAL MethodHandle "invoke" "(ZI)Ljava/lang/Object;")
           (ARETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   (.invoke mh true 123))))
  
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invoke" "()I")
           (IRETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   ^int (.invoke mh))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invoke" "()Z")
           (IRETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   ^boolean (.invoke mh))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invoke" "()C")
           (IRETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   ^char (.invoke mh))))

  ;; A type hint like ^int reaches into a polymorphic form and changes
  ;; its invocation signature.  A type conversion like (int ...) is
  ;; always applied to the argument without changing it.
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invoke" "()Ljava/lang/Object;")
           (CHECKCAST Number)
           (INVOKEVIRTUAL Number "intValue" "()I")
           (IRETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   (int (.invoke mh)))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invoke" "()Ljava/lang/Object;")
           (INVOKESTATIC RT "booleanCast" "(Ljava/lang/Object;)Z")
           (IRETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   (boolean (.invoke mh)))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invoke" "()Ljava/lang/Object;")
           (CHECKCAST Character)
           (INVOKEVIRTUAL Character "charValue" "()C")
           (IRETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   (char (.invoke mh)))))

  ;; type hint ^void does *not* change the invocation signature, but a
  ;; void *context* is pushed into the invoke
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invoke" "()Ljava/lang/Object;")
           (ARETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   ^void (.invoke mh))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invoke" "()V")
           (ACONST_NULL)
           (ARETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh]
                   (do (.invoke mh) nil))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           (ALOAD_0)
           (INVOKEVIRTUAL MethodHandle "invoke" "()V")
           (RETURN)]
         (asm-expr ^void [^java.lang.invoke.MethodHandle mh]
                   (.invoke mh)))))

(deftest polymorphic-signature-vs-static-varargs-signature
  (is (= '[["LOCAL 0: MethodHandle mh"]
           ["LOCAL 1: Object[] objs"]
           (ALOAD_0)
           (ALOAD_1)
           (INVOKEVIRTUAL MethodHandle "invoke"
                          "([Ljava/lang/Object;)Ljava/lang/Object;")
           (ARETURN)]
         ;; match static signature including result type
         (asm-expr [^java.lang.invoke.MethodHandle mh ^Object/1 objs]
                   (.invoke mh objs))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           ["LOCAL 1: Object[] objs"]
           (ALOAD_0)
           (ALOAD_1)
           (INVOKEVIRTUAL MethodHandle "invoke"
                          "([Ljava/lang/Object;)I")
           (IRETURN)]
         ;; match static signature but not result type
         (asm-expr ^int [^java.lang.invoke.MethodHandle mh ^Object/1 objs]
                   ^int (.invoke mh objs))))
  
  (is (= '[["LOCAL 0: MethodHandle mh"]
           ["LOCAL 1: Object obj"]
           (ALOAD_0)
           (ALOAD_1)
           (INVOKEVIRTUAL MethodHandle "invoke"
                          "(Ljava/lang/Object;)Ljava/lang/Object;")
           (ARETURN)]
         ;; match result type but not static signature
         (asm-expr [^java.lang.invoke.MethodHandle mh ^Object obj]
                   (.invoke mh obj))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           ["LOCAL 1: Object obj"]
           (ALOAD_0)
           (ALOAD_1)
           (INVOKEVIRTUAL MethodHandle "invoke"
                          "(Ljava/lang/Object;)I")
           (IRETURN)]
         ;; match neither result type nor static signature
         (asm-expr ^int [^java.lang.invoke.MethodHandle mh ^Object obj]
                   ^int (.invoke mh obj)))))

;; (deftest class-in-op-position
;;   ;; happens when (clojure.lang.MapEntry. ...) is missing the dot
;;   (is (thrown-with-msg? Exception #"does not implement `IFn`"
;;                         (asm-expr []
;;                                   (clojure.lang.MapEntry "foo" "bar")))))

;;; ------------------------------------------------------------------------

(deftest dot-call-varargs-inexact-1
  (is (= '[(ICONST_0)
           (ANEWARRAY "Object[]")
           (INVOKESTATIC Objects "hash" "([Ljava/lang/Object;)I")
           (IRETURN)]
         (asm-expr (java.util.Objects/hash))))
  (is (= '[(ICONST_1)
           (ANEWARRAY "Object[]")
           (DUP)
           (ICONST_0)
           (LDC "a")
           (AASTORE)
           (INVOKESTATIC Objects "hash" "([Ljava/lang/Object;)I")
           (IRETURN)]
         (asm-expr (java.util.Objects/hash "a"))))
  (is (= '[(ICONST_2)
           (ANEWARRAY "Object[]")
           (DUP)
           (ICONST_0)
           (LDC "a")
           (AASTORE)
           (DUP)
           (ICONST_1)
           (LDC "b")
           (AASTORE)
           (INVOKESTATIC Objects "hash" "([Ljava/lang/Object;)I")
           (IRETURN)]
         (asm-expr (java.util.Objects/hash "a" "b")))))

(deftest dot-call-varargs-inexact-2
  (is (= '[(LDC "")
           (ICONST_0)
           (ANEWARRAY "String[]")
           (INVOKESTATIC Paths "get" "(Ljava/lang/String;[Ljava/lang/String;)Ljava/nio/file/Path;")
           (ARETURN)]
         ;; there is also a one-argument `get` taking an URI
         (asm-expr (java.nio.file.Paths/get "")))))

(deftest dot-call-varargs-exact
  (is (= '[(ICONST_0)
           (ANEWARRAY "Object[]")
           (INVOKESTATIC Objects "hash" "([Ljava/lang/Object;)I")
           (IRETURN)]
         (asm-expr (java.util.Objects/hash (new Object/1 0)))))
  (is (= '[(ICONST_0)
           (ANEWARRAY "String[]")
           (INVOKESTATIC Objects "hash" "([Ljava/lang/Object;)I")
           (IRETURN)]
         (asm-expr (java.util.Objects/hash (new String/1 0))))))

(deftest dot-call-varargs-array-to-array
  (is (= '[(ICONST_1)
           (ANEWARRAY "Object[]")
           (DUP)
           (ICONST_0)
           (ICONST_0)
           (NEWARRAY "int[]")
           (AASTORE)
           (INVOKESTATIC Objects "hash" "([Ljava/lang/Object;)I")
           (IRETURN)]
         (asm-expr (java.util.Objects/hash (new int/1 0)))))
  ;; At the moment, ints cannot be passed to an objects vararg
  ;; directly.  This will probably change with Valhalla.  (Also
  ;; applies to call to variadic function.)
  (is (not (.isAssignableFrom Object/1 int/1))))

(deftest dot-call-varargs-newOutputStream
  (is (= '[["LOCAL 0: Path path"]
           (ALOAD_0)
           (ICONST_2)
           (ANEWARRAY "OpenOption[]")
           (DUP)
           (ICONST_0)
           (GETSTATIC StandardOpenOption "WRITE"
                      "Ljava/nio/file/StandardOpenOption;")
           (AASTORE)
           (DUP)
           (ICONST_1)
           (GETSTATIC StandardOpenOption "CREATE_NEW"
                      "Ljava/nio/file/StandardOpenOption;")
           (AASTORE)
           (INVOKESTATIC Files "newOutputStream"
                         "(Ljava/nio/file/Path;[Ljava/nio/file/OpenOption;)Ljava/io/OutputStream;")
           (ARETURN)]
         (asm-expr [^java.nio.file.Path path]
                   (java.nio.file.Files/newOutputStream
                    path
                    java.nio.file.StandardOpenOption/WRITE
                    java.nio.file.StandardOpenOption/CREATE_NEW)))))

(deftest dot-varargs-empty
  (is (= '[["LOCAL 0: String nm"]
           (LDC Object)
           (ALOAD_0)
           (ICONST_0)
           (ANEWARRAY "Class[]")
           (INVOKEVIRTUAL Class "getMethod"
                          "(Ljava/lang/String;[Ljava/lang/Class;)Ljava/lang/reflect/Method;")
           (ARETURN)]

         (asm-expr [^String nm]
                   (.getMethod Object nm)))))

(deftest dot-vector-passed-to-list
  (is (= '[["LOCAL 0: MethodHandle mh"]
           ["LOCAL 1: Object x"]
           (ICONST_2)
           (ANEWARRAY "Object[]")
           (DUP)
           (ICONST_0)
           (ACONST_NULL)
           (AASTORE)
           (DUP)
           (ICONST_1)
           (ALOAD_1)
           (AASTORE)
           (INVOKESTATIC Literal "vector"
                         "([Ljava/lang/Object;)Lclojure/lang/IPersistentVector;")
           (ASTORE_2)
           ["LOCAL 2: IPersistentVector args"]
           (ALOAD_0)
           (ALOAD_2)
           (CHECKCAST List)
           (INVOKEVIRTUAL MethodHandle "invokeWithArguments"
                          "(Ljava/util/List;)Ljava/lang/Object;")
           (ARETURN)]
         (asm-expr [^java.lang.invoke.MethodHandle mh ^Object x]
                   (let* [args [nil x]]
                     (.invokeWithArguments mh ^java.util.List args)))))
  (is (= '[["LOCAL 0: MethodHandle mh"]
           ["LOCAL 1: Object x"]
           (ALOAD_0)
           (ICONST_1)
           (ANEWARRAY "Object[]")
           (DUP)
           (ICONST_0)
           (ICONST_2)
           (ANEWARRAY "Object[]")
           (DUP)
           (ICONST_0)
           (ACONST_NULL)
           (AASTORE)
           (DUP)
           (ICONST_1)
           (ALOAD_1)
           (AASTORE)
           (INVOKESTATIC Literal "vector"
                         "([Ljava/lang/Object;)Lclojure/lang/IPersistentVector;")
           (AASTORE)
           (INVOKEVIRTUAL MethodHandle "invokeWithArguments"
                          "([Ljava/lang/Object;)Ljava/lang/Object;")
           (ARETURN)]
         ;; Awkward: IPersistentVector does not implement java.util.List!
         (asm-expr [^java.lang.invoke.MethodHandle mh ^Object x]
                   (.invokeWithArguments mh [nil x])))))

(deftest invokevirtual-on-enum-value-test
  (is (= '[(GETSTATIC Opcode "DUP" "Ljava/lang/classfile/Opcode;")
           (INVOKEVIRTUAL Opcode "bytecode" "()I")
           (GETSTATIC Opcode "SWAP" "Ljava/lang/classfile/Opcode;")
           (INVOKEVIRTUAL Opcode "bytecode" "()I")
           (IF_ICMPGE L:0)
           (ICONST_1)
           (IRETURN)
           [L:0]
           (ICONST_0)
           (IRETURN)]
         (asm-expr (< (.bytecode java.lang.classfile.Opcode/DUP)
                      (.bytecode java.lang.classfile.Opcode/SWAP)))))

  (is (= '[[(CLASS Vx [PUBLIC ABSTRACT] "pkg/ns0/_10")
            [(FIELD [PUBLIC STATIC FINAL] "x" "Z")]
            
            [(METHOD [PUBLIC STATIC] "<clinit>" "()V")
             (LDC "pkg.ns0")
             (INVOKESTATIC RT "createNamespace" "(Ljava/lang/String;)V")
             (GETSTATIC Opcode "DUP" "Ljava/lang/classfile/Opcode;")
             (INVOKEVIRTUAL Opcode "bytecode" "()I")
             (GETSTATIC Opcode "SWAP" "Ljava/lang/classfile/Opcode;")
             (INVOKEVIRTUAL Opcode "bytecode" "()I")
             (IF_ICMPGE L:0)
             (ICONST_1)
             (GOTO L:1)
             [L:0]
             (ICONST_0)
             [L:1]
             (PUTSTATIC . "x" "Z")
             (RETURN)]]
           
           [(CLASS Vx [PUBLIC ABSTRACT] "pkg/ns0/___" nil "pkg/ns0/_10")
            [(METHOD [PUBLIC STATIC FINAL SYNTHETIC] "requires~1" "()Ljava/lang/String;")
             (LDC "clojure.core")
             (ARETURN)]]]
         (nmsp '[(ns pkg.ns0)
                 (import* 'java.lang.classfile.Opcode)
                 (def x (< (.bytecode Opcode/DUP) (.bytecode Opcode/SWAP)))]))))
