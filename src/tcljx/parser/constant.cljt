(ns tcljx.parser.constant
  (:require
   (tcljx.data [wrong :as wrong] [type :as type] [ir :as ir]))
  (:import
   (tcljx.data.ir Expr Parser)
   (java.lang.constant ConstantDesc ConstantDescs)
   (java.util.regex Pattern)))

;;; Given a primitive type's box class, return the primitive type.
;;; Also returns String for String, and nil for all other classes.
(defn primitive-type-of [^Class tp]
  (condp identical? tp
    ;; instances of ConstantDesc
    Integer Integer/TYPE
    Long Long/TYPE
    Float Float/TYPE
    Double Double/TYPE
    String String
    ;; not numeric and not ConstantDesc
    Boolean Boolean/TYPE
    Character Character/TYPE
    ;; numeric but not ConstantDesc
    Byte Byte/TYPE
    Short Short/TYPE
    #_else nil))

;;; Note: This function caters for a superset of the form types that
;;; are produced by the reader.  It additionally accepts some values
;;; that may be injected via macros, like Byte or Short.
(defn parse-constant-nometa ^Expr [^Parser parser form]
  ;; pre: (not (instance? IMeta form))
  (let [etf (.entity-factory parser)]
    (cond
      (nil? form)
      (ir/new-constant etf ConstantDescs/NULL type/poly-reference)
      
      (instance? ConstantDesc form)
      (ir/new-constant etf form (primitive-type-of (.getClass form)))
      
      (keyword? form)
      (.new-loadable etf form type/keyword)
      
      (boolean? form)
      (ir/new-constant etf (int ^boolean form) Boolean/TYPE)
      
      (char? form)
      (ir/new-constant etf (int ^char form) Character/TYPE)
      
      (number? form)      ;any non-ConstantDesc number: Byte and Short
      (ir/new-constant etf (.intValue ^Number form)
                          (primitive-type-of (.getClass form)))

      (instance? Pattern form)
      (.new-loadable etf form Pattern)

      :else (wrong/other "unsupported literal value" (class form)))))
