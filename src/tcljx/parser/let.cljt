(ns tcljx.parser.let
  (:require
   (tcljx.data [wrong :as wrong] [model :as model])
   (tcljx.parser [syntax :as syntax] [parse :as parse] [interop :as interop]))
  (:import
   (tcljx.data.model Expr ExprFactory Binding Entity Parser)))

(letfn [(binding-for-entity ^Expr [^ExprFactory expr-factory ^long fctx
                                   ^String name ^Entity ent ^Object ent-form]
          (if (model/alias-when-let-value? ent)
            (.binding-alias expr-factory fctx name ent)
            (let [x (.value-of expr-factory ent ent-form)]
              (.binding-fresh expr-factory fctx nil name x))))

        (binding-for-init ^Expr [^ExprFactory expr-factory ^long fctx
                                 ^Class opt-type ^String name ^Expr init]
          (if (and (nil? opt-type) (model/alias-when-let-value? (.emitter init)))
            (.binding-alias expr-factory fctx name (.emitter init))
            (.binding-fresh expr-factory fctx opt-type name init)))]

  (defn parse-let* ^Expr [^Parser parser ^long fctx [_ bindings & body :as form]]
    (let [bindings (syntax/vector bindings true)
          n (quot (count bindings) 2)
          ef (.expr-factory parser)
          bnd-xs (.new-exprs ef n)]
      (loop [parser parser, i 0]
        (if (< i n)
          (let [nm-sym (nth bindings (* i 2))
                nm (syntax/simple-symbol-str nm-sym)
                init (nth bindings (inc (* i 2)))
                opt-type (interop/resolve-tag (.resolver parser) nm-sym nil)
                ent (when (and (nil? opt-type) (symbol? init))
                      (or (parse/lookup parser init)
                          (interop/lookup-class-entity parser init)))
                bnd-x (if (some? ent)
                        (binding-for-entity ef fctx nm ent init)
                        (binding-for-init ef fctx opt-type nm
                                          (parse/arg parser fctx init)))]
            (.set bnd-xs i bnd-x)
            (recur (model/with-local-binding parser bnd-x) (inc i)))
          (.block-let ef fctx bnd-xs (parse/progn parser fctx body)))))))
