(ns tcljx.parser.let
  (:require
   (tcljx.data [wrong :as wrong] [ir :as ir])
   (tcljx.parser [syntax :as syntax] [parse :as parse] [interop :as interop]))
  (:import
   (tcljx.data.ir Expr Entity Binding FnUsage ExprFactory EntityFactory Parser)))

(letfn [(binding-for-entity ^Binding [^EntityFactory entity-factory ^long fctx
                                      ^String name ^Entity ent ^Object ent-form]
          (if (ir/alias-when-let-value? ent)
            (.binding-alias entity-factory fctx name ent)
            (let [x (.value-of entity-factory ent ent-form)]
              (assert (not (instance? FnUsage ent)))
              (.binding-fresh entity-factory fctx nil name x))))
        (binding-for-init ^Binding [^EntityFactory entity-factory ^long fctx
                                    ^Class opt-type ^String name ^Expr init]
          (if (and (nil? opt-type) (ir/alias-when-let-value? (.emitter init)))
            (.binding-alias entity-factory fctx name (.emitter init))
            (.binding-fresh entity-factory fctx opt-type name init)))]

  (defn parse-let* ^Expr [^Parser parser ^long fctx [_ bindings & body :as form]]
    (let [bindings (syntax/vector bindings true)
          n (quot (count bindings) 2)
          xf (.expr-factory parser)
          etf (.entity-factory parser)
          bnds (new Binding/1 n)]
      (loop [parser parser, i 0]
        (if (< i n)
          (let [nm-sym (nth bindings (* i 2))
                nm (syntax/simple-symbol-str nm-sym)
                init (nth bindings (inc (* i 2)))
                opt-type (interop/resolve-tag (.resolver parser) nm-sym nil)
                ent (when (and (nil? opt-type) (symbol? init))
                      (or (parse/lookup parser init)
                          (interop/lookup-class-entity parser init)))
                bnd (if (some? ent)
                      (binding-for-entity etf fctx nm ent init)
                      (binding-for-init etf fctx opt-type nm
                                        (parse/arg parser fctx init)))]
            (aset bnds i bnd)
            (recur (ir/with-local-binding parser bnd) (inc i)))
          (.block-bindings xf fctx bnds false (parse/progn parser fctx body)))))))
