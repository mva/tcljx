(ns tcljx.parser.let
  (:require
   (tcljx.data [wrong :as wrong] [context :as context] [model :as model])
   (tcljx.parser [syntax :as syntax] [interop :as interop]))
  (:import
   (tcljx.data.model Expr Binding Scope)))

(defn parse-let* ^Expr [^Scope scope ^long fctx [_ bindings & body :as form]]
  (let [bindings (syntax/vector bindings true)
        n (quot (count bindings) 2)
        ef (.expr-factory scope)
        bnd-xs (.new-exprs ef n)]
    (loop [scope scope, i 0]
      (if (< i n)
        (let [nm-sym (nth bindings (* i 2))
              nm (syntax/simple-symbol-str nm-sym)
              init (nth bindings (inc (* i 2)))
              opt-type (interop/resolve-tag (.resolver scope) nm-sym nil)
              bnd-x (.new-local ef fctx opt-type nm
                                (or (context/lookup-if-alias nm-sym scope init)
                                    (context/parse-arg scope fctx init)))]
          (.set bnd-xs i bnd-x)
          (recur (context/with-local-binding scope bnd-x) (inc i)))
        (.block-let ef fctx bnd-xs (context/parse-progn scope fctx body))))))
