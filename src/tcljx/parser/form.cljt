(ns tcljx.parser.form
  (:require
   (tcljx.data
    [meta :as meta] [wrong :as wrong] [type :as type] [context :as context]
    [ir :as ir])
   (tcljx.parser
    [interop :as interop] [syntax :as syntax] [parse :as parse] [def :as def]
    [constant :as constant] [let :as let] [loop :as loop] [new :as new]
    [trycatch :as trycatch] [fn :as fn] [deftype :as deftype]))
  (:import
   (tcljx.data.ir Resolver Expr Exprs ExprFactory Entity Binding Parser
                  MethodSpec EntityFactory)
   (clojure.lang IMeta)
   (java.util ArrayList)))

(deftype ParserImpl [^ExprFactory expr-factory
                     ^EntityFactory entity-factory
                     ^Resolver resolver
                     ^map bindings*]
  java.lang.Record
  
  Parser
  (parse-form [this fctx form]
    #_(binding [*print-meta* #_false true]
        (prn :parse-expression form))
    
    (letfn [(parse-meta ^Expr [^long fctx ^map form-meta]
              (when form-meta
                (parse-collection-literal fctx form-meta (meta form-meta))))
            (parse-collection-literal ^Expr [^long fctx form ^map form-meta]
              (let [x-meta (parse-meta fctx form-meta)
                    coll-tp (cond
                              (vector? form) type/vector
                              (map? form) type/map
                              (set? form) type/set
                              (seq? form) type/list
                              :else (throw (IllegalStateException.)))
                    x (->> (if (identical? type/map coll-tp)
                             (mapcat identity form)
                             (seq form))
                           (parse/args this fctx)
                           (.new-collection entity-factory coll-tp))]
                (if (some? x-meta)
                  (.attach-meta-to expr-factory x-meta x)
                  x)))

            (parse-quoted-form ^Expr [form]
              (if (instance? IMeta form)
                ;; symbol, list, vector, map, set
                (let [tp (type/of-quoted-form form)]
                  (.new-loadable entity-factory form tp))
                ;; adjust type to primitive if possible
                (constant/parse-constant-nometa this form)))
            (parse-quote ^Expr [[_ & args]]
              (syntax/empty-tail (next args))
              (parse-quoted-form (syntax/first args)))

            (parse-if ^Expr [^long fctx [_ & formr]]
              (syntax/first (next formr)) ;test and first path exist?
              (syntax/empty-tail 3 formr) ;nothing after test and two paths?
              (let [xs (.new-exprs expr-factory (count formr))]
                (.set xs 0 (parse/arg this fctx (first formr)))
                (.set xs 1 (parse/form this fctx (first (next formr))))
                (when (= (.length xs) 3)
                  (.set xs 2 (parse/form this fctx (first (nnext formr)))))
                (.if-expr expr-factory fctx xs)))
            (parse-case*-guard ^Exprs [form]
              (if (seq? form)           ;accept empty list!
                (let [gs (.new-exprs expr-factory (count form))]
                  (loop [xs form, i 0]
                    (when (< i (.length gs))
                      (.set gs i (parse-quoted-form (first xs)))
                      (recur (rest xs) (inc i))))
                  gs)
                (doto (.new-exprs expr-factory 1)
                  (.set 0 (parse-quoted-form form)))))
            (parse-case* ^Expr [^long fctx [_ & formr :as form]]
              (let [select (parse/arg this fctx (syntax/first formr))
                    n (- (count formr) 1)
                    has-default? (odd? n)
                    n-paths (quot n 2)
                    gs (new Exprs/1 n-paths)
                    ps (.new-exprs expr-factory (+ n-paths (int has-default?)))
                    ecs (new seq/1 n-paths)]
                (loop [[g p & gpr :as gps] (next formr), i 0]
                  (if (< i n-paths)
                    (do (aset gs i (parse-case*-guard g))
                        (.set ps i (parse/form this fctx p))
                        (aset ecs i (or (when (seq? g) g) (when (seq? p) p)
                                        form))
                        (recur gpr (inc i)))
                    (do (when has-default?
                          (.set ps i (parse/form this fctx g)))
                        (.case-expr expr-factory select gs ps fctx ecs))))))
            (parse-do ^Expr [^long fctx [_ & forms]]
              (parse/progn this fctx forms))
            
            (parse-throw ^Expr [^long fctx [_ & args]]
              (let [x (parse/arg this fctx (syntax/first args))]
                (syntax/empty-tail (next args))
                (.throw-expr expr-factory fctx x)))
            (parse-instanceof* ^Expr [^long fctx [_ & args]]
              (let [xs (parse/args this fctx args)]
                (syntax/empty-tail 2 args)
                (if-some [tp (ir/type-literal (.get xs 0) (first args))]
                  (.instanceof-expr expr-factory fctx tp (.get xs 1))
                  (.invoke-virtual-member expr-factory fctx (.get xs 0)
                                          nil "isInstance" (.drop xs 1)))))
            
            (parse-import* ^Expr [[_ & formr :as form]]
              (let [specs (map syntax/strip-quote formr)
                    rimports (syntax/flatten-imports nil "" specs form)]
                (.process-imports (parse/namespace-builder this)
                                  (reverse rimports))
                (.empty-expr expr-factory)))
            
            (unpack-member-args ^seq [^seq formr]
              (if (seq? (first formr))
                (do (syntax/empty-tail (next formr))
                    (first formr))
                formr))
            (field-members-only ^String [^String mnm ^Exprs xs]
              (when (and (> (.length mnm) 1) (= (.charAt mnm 0) \-)
                         (.empty? xs))
                (.substring mnm 1)))
            (parse-dot ^Expr [^long fctx [_ owner & formr]]
              (let [[member & args] (unpack-member-args formr)
                    mnm (syntax/simple-symbol-str member) ;implies owner exists
                    owner-expr (parse/arg this fctx owner)
                    xs (parse/args this fctx args)
                    fnm (field-members-only mnm xs)]
                (if-some [owner (ir/type-literal owner-expr owner)]
                  (if fnm
                    (.get-static-field expr-factory fctx owner fnm)
                    (.invoke-static-member expr-factory fctx false owner mnm xs))
                  (if fnm
                    (.get-virtual-field expr-factory fctx owner-expr fnm)
                    (.invoke-virtual-member expr-factory fctx owner-expr
                                            nil mnm xs)))))
            (parse-dot-dot-or-member ^Expr [^String nm ^long fctx [_ & formr]]
              (if (= nm "..")
                (->> (reduce #(list '. %1 %2)
                             (list '. (syntax/first formr)
                                   (syntax/first (next formr)))
                             (nnext formr))
                     (parse-dot fctx))
                (let [mnm (.substring nm 1)
                      receiver (parse/arg this fctx (syntax/first formr))
                      xs (parse/args this fctx (rest formr))]
                  (if-some [fnm (field-members-only mnm xs)]
                    (.get-virtual-field expr-factory fctx receiver fnm)
                    (.invoke-virtual-member expr-factory fctx receiver
                                            nil mnm xs)))))
            (parse-class-dot ^Expr [^String nm ^long fctx [_ & formr]]
              (let [clnm (.substring nm 0 (dec (.length nm)))]
                (parse-dot fctx (list* \. (symbol nil clnm) 'new formr))))
            
            (wr-info-unresolved-qualified-sym ^Throwable [^symbol op]
              (wrong/wr-info
               (if (parse/lookup-namespace this (namespace op))
                 ;; namespace exists: def lookup must have failed
                 (str "no public def " (wrong/q op))
                 (str "no namespace or class " (wrong/q-namespace op)))))
            (invoke-entity ^Expr [^long fctx ^seq form ^Entity inv ^Exprs args]
              (let [specs (.invoke-methods inv)]
                (condp identical? specs
                  ir/invoke-methods-primitive-fn
                  (.invoke* inv fctx form nil -1 args)
                  ir/invoke-methods-dynamic
                  (.invoke-value expr-factory fctx (.value-of entity-factory inv (first form)) args)
                  #_else
                  (let [inv* (.invocation-of entity-factory inv form)]
                    (.invoke-arity expr-factory fctx form inv inv* args)))))
            (parse-invocation-qname ^Expr [^symbol op ^long fctx ^seq form
                                           ^String nmsp ^String nm]
              ;; `op` is qualified symbol: either a reference to
              ;; a namespace def, a static class member, or an
              ;; instance member
              (if-some [cl (parse/lookup-class this nmsp)]
                (if-some [ent (when (some? (interop/param-tags-of op))
                                (interop/lookup-class-entity this op))]
                  (invoke-entity fctx form ent
                                 (parse/args this fctx (rest form)))
                  (if (= (.charAt nm 0) \.) ;syntax is Class/.member
                    (.invoke-virtual-member
                     expr-factory fctx
                     (parse/arg this fctx (syntax/second form))
                     cl (.substring nm 1)
                     (parse/args this fctx (nnext form)))
                    (.invoke-static-member
                     expr-factory fctx true cl nm
                     (parse/args this fctx (rest form)))))
                (condp = op
                  'tinyclj.lang.primitive/list
                  ;; FIXME... drop this later
                  (parse-collection-literal fctx (rest form) nil)
                  
                  #_else
                  (throw (wr-info-unresolved-qualified-sym op)))))
            (parse-invocation-other ^Expr [op ^long fctx ^seq form]
              ;; any non-symbol `op`
              (let [op (parse/arg this fctx (first form))
                    ce (.emitter op)
                    args (parse/args this fctx (rest form))]
                (if (ir/entity? ce)
                  (invoke-entity fctx form ce args)
                  (.invoke-value expr-factory fctx op args))))
            (parse-try-or-extract ^Expr [^long fctx ^seq form]
              (if (context/can-inline-tcf? fctx)
                (trycatch/parse-try this fctx form)
                (->> (list 'fn* (with-meta [] {:tag meta/auto-return-type})
                           form)
                     (list)
                     (.parse-form this fctx))))
            (parse-definterface* ^Expr [^long fctx ^seq form]
              (deftype/parse-definterface* resolver entity-factory fctx form))
            (parse-deftype* ^Expr [^long fctx ^seq form]
              (deftype/parse-deftype* this fctx form))
            (parse-invocation ^Expr [op ^long fctx ^seq form] ;discards meta
              (if (symbol? op)
                (let [nm (name op), nmsp (namespace op)]
                  (if (nil? nmsp)
                    (case nm
                      ;; FIXME... implement these as proper primitive
                      ;; functions
                      "identity" (parse/arg this fctx (syntax/second form))
                      
                      "def" (def/parse-def this fctx form)
                      "loop*" (loop/parse-loop* this fctx form)
                      "recur" (loop/parse-recur this fctx form)
                      "if" (parse-if fctx form)
                      "case*" (parse-case* fctx form)
                      "let*" (let/parse-let* this fctx form)
                      "letfn*" (fn/parse-letfn* this fctx form)
                      "fn*" (fn/parse-fn* this fctx form)
                      "do" (parse-do fctx form)
                      "quote" (parse-quote form)
                      ;; "var" (var-form form)
                      "import*" (parse-import* form)
                      "." (parse-dot fctx form)
                      ;; "set!" (set!-form form)
                      "deftype*" (parse-deftype* fctx form)
                      
                      "reify*" (let [m (or (parse-meta fctx (meta form))
                                           (.empty-expr expr-factory))]
                                 (deftype/parse-reify* this fctx m form))
                      "try" (parse-try-or-extract fctx form)
                      "throw" (parse-throw fctx form)
                      ;; "monitor-enter" (monitor-form Opcode/MONITORENTER form)
                      ;; "monitor-exit" (monitor-form Opcode/MONITOREXIT form)
                      ;; "catch" is only special within `try-form`
                      ;; "finally" is only special within `try-form`
                      "new" (let [arg (syntax/second form)
                                  tp (interop/parse-type-symbol resolver arg)]
                              (new/parse-new this tp fctx form))
                      ;; "&" is only special within arity signature
                      
                      ;; special forms that are not special in Clojure:
                      "definterface*" (parse-definterface* fctx form)
                      "instanceof*" (parse-instanceof* fctx form)
                      #_else (cond
                               (= (.charAt nm 0) \.)
                               (parse-dot-dot-or-member nm fctx form)
                               (= (.charAt nm (dec (.length nm))) \.)
                               (parse-class-dot nm fctx form)
                               :else
                               (if-some [inv (parse/lookup this op)]
                                 (invoke-entity fctx form inv
                                                (parse/args this fctx (rest form)))
                                 (throw (wrong/info-unresolved op)))))
                    (parse-invocation-qname op fctx form nmsp nm)))
                (parse-invocation-other op fctx form)))
            
            (parse-symbol ^Expr [^symbol sym]
              (if-some [ent (parse/lookup this sym)]
                (.value-of entity-factory ent sym)
                (if-some [x (interop/lookup-class-expr this sym)]
                  x
                  (throw (wrong/info-unresolved sym)))))]

      (if (instance? IMeta form)
        (let [form-meta (.meta ^IMeta form)
              form-tag (meta/get-tag form-meta)]
          (-> (cond
                ;; reference to global def, local binding, defined
                ;; class, class member, or instance field (when in a
                ;; deftype); close to 90% hit rate for "is a symbol"
                (symbol? form) (parse-symbol form)
                
                ;; special form, macro form, or some function call variant
                (and (seq? form) (some? (seq form)))
                (context/with-line-from-meta [fctx form-meta]
                  (parse-invocation (first form) fctx form))
                
                ;; vector, map, set, or empty list
                :else (parse-collection-literal fctx form form-meta))
              (cond-> (some? form-tag)
                (interop/cast-to-tag this fctx form-tag))))
        
        ;; keyword, pattern, number, character, string, boolean, nil,
        ;; or (via expansion) a class instance
        (constant/parse-constant-nometa this form))))

  (with-bindings* [_ bindings]
    (ParserImpl. expr-factory entity-factory resolver bindings))
  (with-expr-factory [_ xf]
    (ParserImpl. xf entity-factory resolver bindings*))
  (with-entity-factory [_ etf]
    (ParserImpl. expr-factory etf resolver bindings*)))

(def mk-parser ParserImpl/new)
