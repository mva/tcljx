(ns tcljx.files
  (:import
   (java.net URL)
   (java.nio.file Path Files)
   (java.nio.file.attribute PosixFilePermission PosixFilePermissions)
   (java.util Set)))

(def source-suffix ".cljt")
(def class-suffix ".class")

(defn path-url ^URL [^Path p]
  (-> p (.toUri) (.toURL)))

(defn path-of
  (^Path [^String x]
   (Path/of x))
  (^Path [^String x & ^String/1 more]
   (Path/of x more)))

(defn default-project-name ^String []
  (-> (path-of (System/getProperty "user.dir"))
      (.getFileName)
      (.toString)))

(defn tmp-dest-dir ^Path [^String dir-name]
  (path-of (System/getProperty "java.io.tmpdir")
           (System/getProperty "user.name") "tinyclj" dir-name))

;;; ------------------------------------------------------------------------

(def ^:private create-directory-permissions
  (-> (Set/of PosixFilePermission/OWNER_READ
              PosixFilePermission/OWNER_WRITE
              PosixFilePermission/OWNER_EXECUTE)
      (PosixFilePermissions/asFileAttribute)))

(defn package-directory ^Path [^Path dest-dir ^String ns-str]
  (.resolve dest-dir (path-of (.replace ns-str "." "/"))))

(defn prepare-package-directory ^Path [^Path dest-dir ^String ns-str]
  (when (some? dest-dir)
    (Files/createDirectories (package-directory dest-dir ns-str)
                             create-directory-permissions)))
