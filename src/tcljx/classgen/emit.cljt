(ns tcljx.classgen.emit
  (:require
   (tcljx.data [config :as cfg] [type :as type] [model :as model]))
  (:import
   (tcljx.data.model CodeEmitter)
   (java.lang.classfile CodeBuilder TypeKind)))

(defn- to? ^boolean [^Class opnd-type ^Class requested-type ^CodeBuilder xb]
  (cond
    (type/exits-method? opnd-type)
    false         ;instruction does not return: cannot continue at all
    
    ;; FIXME... optimize for this "nothing to do" case?  it should be
    ;; the most common scenario.  handle this in the caller of this
    ;; function?
    (identical? opnd-type requested-type)
    (type/continues? opnd-type)
    
    (type/exits-method? requested-type)
    (do (.return_ xb (TypeKind/from opnd-type)) false)
    
    :else (do (assert false)
              true)))

;;; Emit instruction to `xb` and adapt the stack operand it produces
;;; to `requested-type`.  Returns false if the instruction returns
;;; control to the invoking method, and true if execution continues in
;;; this method.
(defn more?
  (^boolean [^CodeBuilder xb ^Class requested-type ^CodeEmitter ce]
   (more? xb requested-type ce cfg/no-line-number))
  (^boolean [^CodeBuilder xb ^Class requested-type ^CodeEmitter ce ^int line]
   ;; FIXME... special case (identical? opnd-type requested-type)?
   (-> (.emit-code* ce xb requested-type line)
       (to? requested-type xb))))
(defn method-code ^void [^CodeBuilder xb ^CodeEmitter ce]
  (more? xb type/exits-method ce))

(defn more-of-type*? ^boolean [^CodeBuilder xb ^Class requested-type
                               ^CodeEmitter/1 ces ^int line]
  (let [end (dec (alength ces))]
    (loop [i 0]
      (let [ce (aget ces i)]
        (if (< i end)
          (if (more? xb Void/TYPE ce line)
            (recur (inc i))
            false)
          (more? xb requested-type ce line))))))

(defn more-of-types*? ^boolean [^CodeBuilder xb ^Class/1 requested-types
                                ^CodeEmitter/1 ces ^int line]
  (loop [i 0]
    (if (< i (alength requested-types))
      (if (more? xb (aget requested-types i) (aget ces i) line)
        (recur (inc i))
        false)
      true)))
