(ns tcljx.classgen.codegen
  (:require
   (tcljx.data
    [config :as cfg] [wrong :as wrong] [type :as type] [model :as model]
    [resolve :as resolve] [reflect :as reflect])
   (tcljx.classgen
    [classfile :as classfile] [runtime :as rt] [emit :as emit] [insn :as insn]
    [entity :as entity] [condgen :as condgen] [defgen :as defgen]
    [segmgen :as segmgen] [methodgen :as methodgen]))
  (:import
   (tcljx.data.model Expr Exprs ExprFactory CodeEmitter ArityType
                     NamespaceBuilder)
   (tcljx.data.resolve Match)
   (tcljx.classgen.classfile ClassElementEmitter)
   (tcljx.classgen.insn Insn Insns)
   (tcljx.classgen.methodgen FnLiteral)
   (java.lang.classfile ClassBuilder CodeBuilder Opcode TypeKind)
   (java.lang.constant ConstantDesc ClassDesc MethodTypeDesc MethodHandleDesc
                       ConstantDescs)
   (java.lang.reflect Member Constructor Field)
   (java.util ArrayList)
   (java.util.function Consumer)))

(letfn [(find-factory ^Match [^String method-name]
          (resolve/runtime-method tinyclj.lang.Literal method-name
                                  (doto (new Class/1 1) (aset 0 Object/1))))]
  (def ^:private literal-vector (find-factory "vector")) ;type/vector
  (def ^:private literal-map (find-factory "map"))       ;type/map
  (def ^:private literal-set (find-factory "set"))       ;type/set
  (def ^:private literal-list (find-factory "list"))     ;type/list
  (def ^:private literal-with-meta
    (resolve/runtime-method tinyclj.lang.Literal "with_meta"
                            (doto (new Class/1 2)
                              (aset 0 clojure.lang.IObj)
                              (aset 1 clojure.lang.IPersistentMap)))))

(defn- with-line-number ^Insn [^Insn insn ^int line]
  (-> (^CodeEmitter fn [xb _ consumed-type then-leave?]
       (when (emit/insn? xb line consumed-type (.emitter insn) then-leave?)
         consumed-type))
      (insn/mk-insn (insn/expr-type insn))))

;;; ------------------------------------------------------------------------

(defn process-functions! ^void [^ArrayList segment-elements
                                ^ArrayList pending-functions]
  (doseq [^FnLiteral fn-literal pending-functions
          :when (nil? (aget (.arity-redirections fn-literal) 0))]
    (.add segment-elements
          (^ClassElementEmitter fn [cb]
           (let [at (aget (.arity-types fn-literal) 0)
                 mnm (str (.mangled-name fn-literal) "~" (.parameter-count at))
                 mtd (model/mtd-of-arity-type at)]
             (classfile/with-method-body-> cb
               [mnm mtd classfile/acc-method-static]
               ;; pick up any fixed auto-return-type
               (insn/emit-return (.return-type at)
                                 (.get (.arity-bodies fn-literal) 0)))))))
  (.clear pending-functions))

;;; ------------------------------------------------------------------------

(defn- invoke-single-match ^Insn [^seq ms ^Insn receiver ^Insns args]
  (when (nil? (next ms))
    (when-some [m (first ms)]
      (insn/invoke-member m receiver args))))

(defn- info-unresolved-member ^Throwable [^seq ms ^Class owner ^String mnm
                                          ^Insns args ^String label]
  ;; label is one of "static", "virtual", or "constructor"
  (let [entity (str label (when (not= label "constructor")
                            (if (.empty? args) " member" " method")))
        mnm (cond->> mnm (= label "virtual") (str "."))
        member (wrong/q-as-symbol owner mnm)]
    (wrong/wr-info (if (nil? (seq ms))
                     (str "no " entity " matching " member)
                     (str "multiple " entity "s matching " member)))))

;;; ------------------------------------------------------------------------

;;; Note: To save work, this code generator assumes that it can use
;;; types available at compile time to refer to runtime types of the
;;; compiled application.  For example, it uses tinyclj.lang.StaticFn
;;; as expression type, instead of the type (currently of the same
;;; name) used by the application to hold a static function instance.
;;; Likewise for symbols, keywords, vectors, and so on.  This works
;;; only as long as compiler and application agree on the names of
;;; their runtime classes.
(deftype ExprFactoryImpl [^ArrayList pending-functions ;shared with NmspBuilder
                          ]
  ExprFactory
  (empty-expr [_]
    insn/insn-empty)
  (new-exprs [_ n]
    (if (zero? n)
      insn/empty-insns
      (Insns. (new CodeEmitter/1 n) (new Class/1 n))))
  (line-number [_ line insn]
    (cond-> insn ;preserve visibility of constant `insn` by not wrapping it
      (and (some? line) (not (model/const? insn)))
      (with-line-number ^int line)))
  
  (new-constant [_ expr-type value]
    (entity/new-constant value expr-type))

  (attach-meta-to [this meta arg]
    (if (and (model/const? arg) (model/const? meta))
      (->> (with-meta (model/const-value arg) (model/const-value meta))
           (.new-constant this (insn/expr-type arg)))
      (insn/invoke-member literal-with-meta nil (insn/insns-of arg meta))))
  (new-vector [this args]
    (let [args ^Insns args]
      (if (entity/constants? args)
        (->> (entity/as-typed-constant-descriptions args)
             (vec)
             (.new-constant this type/vector))
        (insn/invoke-member literal-vector nil args))))
  (new-map [this args]
    (let [args ^Insns args]
      (if (entity/constants? args)
        (->> (entity/as-typed-constant-descriptions args)
             (apply array-map)
             (.new-constant this type/map))
        (insn/invoke-member literal-map nil args))))
  (new-set [this args]
    (let [args ^Insns args]
      (if (entity/constants? args)
        (->> (entity/as-typed-constant-descriptions args)
             (set)
             (.new-constant this type/set))
        (insn/invoke-member literal-set nil args))))
  (new-list [this args]
    (let [args ^Insns args]
      (if (entity/constants? args)
        (.new-constant this type/list
                       (-> (entity/as-typed-constant-descriptions args)
                           (seq)
                           (or ())))
        (insn/invoke-member literal-list nil args))))
  (new-array [_ array-type dims]
    (let [dims ^Insns dims]
      (dotimes [i (.length dims)]
        (insn/ensure-assignable Integer/TYPE dims i))
      (-> (^CodeEmitter fn [xb line _ _]
           (when (emit/insns? xb line Integer/TYPE (.emitters dims))
             (if (= (.length dims) 1)
               (let [component-type (.getComponentType array-type)]
                 (if (.isPrimitive component-type)
                   (.newarray xb (TypeKind/from component-type))
                   (.anewarray xb (reflect/describe-class component-type))))
               (.multianewarray xb (reflect/describe-class array-type)
                                (.length dims)))
             array-type))
          (insn/mk-insn array-type))))
  
  (progn-expr [_ xs x]
    (insn/progn xs x))
  (if-expr [_ xs]
    (let [xs ^Insns xs
          tp (condgen/join-type (.expr-types xs) 1 (.length xs))]
      (-> (condgen/mk-if (.emitter (condgen/logical-truth (.get xs 0)))
                         (insn/emitter xs 1)
                         (if (= (.length xs) 3)
                           (insn/emitter xs 2)
                           (.emitter insn/insn-empty)))
          (insn/mk-insn tp))))
  (invoke [this form inv args]
    (.invoke* inv form args))
  
  (new-parameter [_ param-no name type]
    (Insn. (entity/parameter-binding param-no name type) Void/TYPE))
  (new-local [_ opt-type name init-or-binding]
    (when opt-type    ;presence of type tag means this is not an alias
      (insn/ensure-assignable opt-type init-or-binding))
    (insn/mk-insn (entity/local-binding opt-type name init-or-binding) Void/TYPE))
  (block-let [_ bindings body]
    (let [block (insn/progn bindings body)]
      (-> (^CodeEmitter fn [xb line consumed-type then-leave?]
           ;; FIXME... emit lineNumber here?
           (.block xb (^Consumer fn ^void [^CodeBuilder xb]
                       ;; FIXME... or emit lineNumber here?
                       (emit/insn? xb line consumed-type (.emitter block) then-leave?)))
           (when-not then-leave?     ;can the insn? above return true?
             consumed-type))
          (insn/mk-insn (insn/expr-type block)))))
  
  (new-function [this mangled-name arity-types arity-bodies arity-redirections]
    ;; update body expressions to match return types and overwrite any
    ;; auto-return-marker return types
    (dotimes [i (alength arity-types)]
      (let [at (aget arity-types i)]
        (if (type/auto-return-marker? (.return-type at))
          (let [expr-tp (insn/expr-type arity-bodies i)
                logical-tp (type/expr-to-logical expr-tp)]
            (aset arity-types i (.with-return-type at logical-tp)))
          (insn/ensure-assignable (.return-type at) arity-bodies i))))
    
    (let [fn-literal (FnLiteral. mangled-name arity-types
                                 arity-bodies arity-redirections)]
      (.add pending-functions fn-literal)
      (let [owner (ClassDesc/of "pkg.ns0._10")]
        (.new-constant this tinyclj.lang.StaticFn
                       (methodgen/static-fn-desc owner fn-literal)))))
  
  (type-cast [this target-type arg]
    (let [arg-tp (insn/expr-type arg)]
      (letfn [(wr-info-invalid-cast ^Throwable []
                (wrong/wr-info (str "cannot cast to " (wrong/q-type target-type)
                                    (wrong/got-type arg-tp))))
              (checkcast ^Insn [^Class ref-target ^Insn ref-arg]
                (let [arg-tp (insn/expr-type ref-arg)]
                  (if (or (.isInterface ref-target)
                          (.isAssignableFrom ref-target arg-tp) ;empty
                          (.isAssignableFrom arg-tp ref-target)) ;checkcast
                    (insn/cast-to ref-arg ref-target)
                    (throw (wr-info-invalid-cast)))))
              (box-arg-into ^Insn [^Class prim-tp]
                (insn/cast-to arg (.class-reference (type/of prim-tp))))]
        (cond
          (or (type/polymorphic? target-type) ;force void context
              (type/polymorphic? arg-tp))
          (insn/cast-to arg target-type)

          (type/same? target-type arg-tp)
          arg
          
          (.isPrimitive target-type)
          (if (.isPrimitive arg-tp)
            (throw (wr-info-invalid-cast)) ;no cast between different primitives
            (checkcast arg-tp (box-arg-into target-type)))
          
          ;; here holds: `target-type` is reference
          (.isPrimitive arg-tp)
          (checkcast target-type (box-arg-into arg-tp))
          
          ;; here holds: both `target-type` and `arg-tp` are reference
          :else (checkcast target-type arg)))))
  (invoke-constructor [_ owner args]
    (let [args ^Insns args
          mnm resolve/constructor-name
          ms (resolve/members-by-arg-types false owner mnm (.expr-types args))]
      (or (invoke-single-match ms nil args)
          (throw (info-unresolved-member ms owner mnm args "constructor")))))
  (invoke-static-member [_ invoke-member-if-field? owner mnm args]
    (let [args ^Insns args
          ms (resolve/members-by-arg-types true owner mnm (.expr-types args))]
      ;; (prn :et (seq (.expr-types args)))
      ;; (prn :ms (seq ms))
      ;; (prn :invoke-member-if-field? invoke-member-if-field?)
      (or (when-some [insn (invoke-single-match ms nil args)]
            (if (and invoke-member-if-field?
                     (instance? Field (.member ^Match (first ms))))
              (assert false "not yet implemented")
              insn))
          (throw (info-unresolved-member ms owner mnm args "static")))))
  (invoke-virtual-member [_ receiver mnm args]
    (let [receiver ^Insn receiver
          args ^Insns args
          owner (insn/expr-type receiver)
          ms (resolve/members-by-arg-types false owner mnm (.expr-types args))]
      (or (invoke-single-match ms receiver args)
          (throw (info-unresolved-member ms owner mnm args "virtual")))))

  (get-static-field [_ owner mnm]
    (let [args insn/empty-insns
          ms (resolve/field-exact nil true owner mnm resolve/empty-class-array)]
      (or (invoke-single-match ms nil args)
          (throw (info-unresolved-member ms owner mnm args "static")))))
  (get-virtual-field [_ receiver mnm]
    (let [receiver ^Insn receiver
          args insn/empty-insns
          owner (insn/expr-type receiver)
          ms (resolve/field-exact nil false owner mnm (.expr-types args))]
      (or (invoke-single-match ms receiver args)
          (throw (wrong/wr-info (str "no virtual field matching "
                                     (wrong/q-as-symbol owner mnm)))))))

  (instanceof-expr [_ type arg]
    (let [arg ^Insn arg]
      (-> (^CodeEmitter fn [xb line _ _]
           (when (insn/emit-insn-logical? xb arg)
             (.instanceOf xb (reflect/describe-class type))
             Boolean/TYPE))
          (insn/mk-insn Boolean/TYPE))))
  (throw-expr [_ arg]
    (let [arg ^Insn arg]
      (if (.isAssignableFrom Throwable (insn/logical-type arg))
        (-> (^CodeEmitter fn [xb line _ _]
             (when (emit/insn? xb line Throwable (.emitter arg) false)
               (-> xb (emit/line-number line) (.athrow))
               nil))                  ;type seen by the code generator
            (insn/mk-insn Void/TYPE)) ;type seen by the parser
        (throw (insn/info-type "expect instance of Throwable" arg)))))
  
  (mk-expr [_ form]
    (throw (UnsupportedOperationException. (str form)))))

(defn mk-code-generator ^ExprFactory [^ArrayList pending-functions]
  (ExprFactoryImpl. pending-functions))
