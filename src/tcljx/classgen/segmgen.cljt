(ns tcljx.classgen.segmgen
  (:require
   (tcljx [config :as cfg] [files :as files])
   (tcljx.classgen [runtime :as rt] [loader :as loader]
                   [classfile :as classfile])
   [tinyclj.string :as str])
  (:import
   (tcljx.classgen.loader PackageBuilder ClassBytes)
   (java.lang.classfile ClassFile ClassBuilder CodeBuilder)
   (java.lang.constant ClassDesc ConstantDescs MethodTypeDesc)
   (java.util ArrayList)
   (java.util.function Consumer)))

(def first-segm-id 10)
(def capstone-class-name "___")
(def segment-class-prefix "_") ;followed by number: not parsed as symbol if empty
(def segment-class-flags (bit-or ClassFile/ACC_PUBLIC ClassFile/ACC_ABSTRACT))

;;; ------------------------------------------------------------------------

(defn- with-clinit ^ClassBuilder [^ClassBuilder cb ^String create-namespace
                                  ^Consumer clinit-handler ^String core-init]
  (.withMethodBody
   cb ConstantDescs/CLASS_INIT_NAME ConstantDescs/MTD_void
   (bit-or ClassFile/ACC_PUBLIC ClassFile/ACC_STATIC)
   (^Consumer fn ^void [^CodeBuilder xb]
    (when (some? create-namespace)
      (.ldc xb create-namespace)
      (.invokestatic xb rt/RT "createNamespace"
                     (MethodTypeDesc/of ConstantDescs/CD_void
                                        ConstantDescs/CD_String)))
    (.accept clinit-handler xb)
    (when (some? core-init)
      (.ldc xb core-init)
      (.invokestatic xb rt/RT "markCoreInitialization"
                     (MethodTypeDesc/of ConstantDescs/CD_void
                                        ConstantDescs/CD_String)))
    (.return_ xb))))

(defn- with-requires ^ClassBuilder [^ClassBuilder cb ^String req-str]
  (.withMethodBody
   cb rt/name-requires (MethodTypeDesc/of ConstantDescs/CD_String)
   (bit-or classfile/acc-method-static ClassFile/ACC_SYNTHETIC)
   (^Consumer fn ^void [^CodeBuilder xb]
    (-> (.ldc xb req-str)
        (.areturn)))))

;;; ------------------------------------------------------------------------

(deftype Segment [^String ns-str ;also the package name of the output class
                  ^int segm-id
                  ^String source-file
                  ^PackageBuilder pkg-build]
  java.lang.Record)

(defn mk-first-segment ^Segment [^String ns-str ^String source-file
                                 ^PackageBuilder pkg-build]
  (Segment. ns-str first-segm-id source-file pkg-build))

(defn- segment-cld
  (^ClassDesc [^Segment segm]
   (segment-cld segm 0))
  (^ClassDesc [^Segment segm ^int id-delta]
   (let [segm-id (+ (.segm-id segm) id-delta)]
     (ClassDesc/of (.ns-str segm) (str segment-class-prefix segm-id)))))
(defn define-segment ^ClassBytes [^Segment segm ^Consumer clinit-handler]
  (let [ns-str (.ns-str segm)
        cld (segment-cld segm)]
    (->> (^Consumer fn ^void [^ClassBuilder cb]
          (with-clinit cb
            (when (= (.segm-id segm) first-segm-id) ns-str)
            clinit-handler
            (when (cfg/core-lib? ns-str) (str ns-str "." (.displayName cld)))))
         (loader/build-and-write
          (.pkg-build segm) cld segment-class-flags
          ConstantDescs/CD_Object (.source-file segm)))))

(defn- capstone-cld ^ClassDesc [^Segment segm]
  (ClassDesc/of (.ns-str segm) capstone-class-name))
(defn define-capstone ^ClassBytes [^Segment last-segm]
  (let [req-str (if (cfg/core-lib? (.ns-str last-segm))
                  ""            ;suppress internal details of core lib
                  ;; FIXME... use real requires
                  (str/join "," (sort (list "tinyclj.core"))))]
    (->> (^Consumer fn ^void [^ClassBuilder cb]
          (-> cb
              (with-requires req-str)))
         (loader/build-and-write
          (.pkg-build last-segm) (capstone-cld last-segm) segment-class-flags
          (segment-cld last-segm) (.source-file last-segm)))))
