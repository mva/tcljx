(ns tcljx.classgen.segmgen
  (:require
   (tcljx.data [config :as cfg] [files :as files] [type :as type] [meta :as meta]
               [model :as model] [resolve :as resolve])
   (tcljx.classgen [runtime :as rt] [loader :as loader] [classfile :as classfile]
                   [emit :as emit] [insn :as insn] [entity :as entity])
   [tinyclj.string :as str])
  (:import
   (tcljx.data.model CodeEmitter)
   (tcljx.classgen.loader PackageBuilder ClassBytes)
   (tcljx.classgen.insn Insn)
   (java.lang.classfile ClassFile ClassBuilder CodeBuilder)
   (java.lang.constant ClassDesc ConstantDescs MethodTypeDesc)
   (java.util.function Consumer)))

(def first-segm-id 10)
(def capstone-class-name "___")
(def segment-class-prefix "_") ;followed by number: not parsed as symbol if empty
(def segment-class-flags (bit-or ClassFile/ACC_PUBLIC ClassFile/ACC_ABSTRACT))

;;; ------------------------------------------------------------------------

(defn segment-cld ^ClassDesc [^PackageBuilder pkg-build ^int segm-id]
  (ClassDesc/of (.pkg-name pkg-build) (str segment-class-prefix segm-id)))

(defn- with-requires ^ClassBuilder [^ClassBuilder cb ^String req-str]
  (classfile/with-method-body->
    cb [rt/name-requires (MethodTypeDesc/of ConstantDescs/CD_String)
        (bit-or classfile/acc-method-static ClassFile/ACC_SYNTHETIC)]
    (-> (.ldc req-str) (.areturn))))


(def ^:private rt-create-namespace
  (resolve/runtime-method tinyclj.lang.RT "createNamespace"
                          (doto (new Class/1 1) (aset 0 String))))
(defn clinit-prologue ^Insn [^String ns-str ^int segm-id]
  (if (= segm-id first-segm-id)
    (insn/invoke-member rt-create-namespace nil
                        (insn/insns-of (entity/new-constant ns-str String))
                        meta/fctx-none)
    insn/insn-empty))

(def ^:private rt-mark-core-initialization
  (resolve/runtime-method tinyclj.lang.RT "markCoreInitialization"
                          (doto (new Class/1 1) (aset 0 String))))
(defn clinit-epilogue ^Insn [^PackageBuilder pkg-build ^int segm-id]
  (if (cfg/core-lib? (.pkg-name pkg-build))
    (let [cld (segment-cld pkg-build segm-id)
          clnm (str (.packageName cld) "." (.displayName cld))]
      (insn/invoke-member rt-mark-core-initialization nil
                          (insn/insns-of (entity/new-constant clnm String))
                          meta/fctx-none))
    insn/insn-empty))

;;; ------------------------------------------------------------------------

(defn define-segment ^ClassBytes [^PackageBuilder pkg-build ^int segm-id
                                  ^String source-file
                                  ^Consumer class-builder-handler
                                  ^Insn clinit-body]
  (loader/bwdi
   pkg-build (segment-cld pkg-build segm-id) segment-class-flags
   (if (= segm-id first-segm-id)
     ConstantDescs/CD_Object
     (segment-cld pkg-build (dec segm-id)))
   source-file
   (^Consumer fn ^void [^ClassBuilder cb]
    (-> cb
        (classfile/accept-> class-builder-handler)
        (classfile/with-method-body->
          [ConstantDescs/CLASS_INIT_NAME ConstantDescs/MTD_void
           (bit-or ClassFile/ACC_PUBLIC ClassFile/ACC_STATIC)]
          (insn/emit-return Void/TYPE clinit-body))))))

(defn- capstone-cld ^ClassDesc [^PackageBuilder pkg-build]
  (ClassDesc/of (.pkg-name pkg-build) capstone-class-name))
(defn define-capstone ^ClassBytes [^PackageBuilder pkg-build ^int segm-id]
  (let [req-str (if (cfg/core-lib? (.pkg-name pkg-build))
                  ""            ;suppress internal details of core lib
                  ;; FIXME... use real requires
                  (str/join "," (sort (list "tinyclj.core"))))
        source-file nil
        cbh (^Consumer fn ^void [^ClassBuilder cb]
             (-> cb
                 (with-requires req-str)))]
    (loader/bwdi pkg-build (capstone-cld pkg-build) segment-class-flags
                 (segment-cld pkg-build (dec segm-id)) source-file cbh)))
