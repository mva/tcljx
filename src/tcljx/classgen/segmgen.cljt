(ns tcljx.classgen.segmgen
  (:require
   (tcljx [config :as cfg] [files :as files])
   (tcljx.classgen [runtime :as rt] [loader :as loader]
                   [classfile :as classfile])
   [tinyclj.string :as str])
  (:import
   (tcljx.classgen.loader PackageBuilder ClassBytes)
   (java.lang.classfile ClassFile ClassBuilder)
   (java.lang.constant ClassDesc ConstantDescs MethodTypeDesc)
   (java.util ArrayList)
   (java.util.function Consumer)))

(def first-segm-id 10)
(def capstone-class-name "___")
(def segment-class-prefix "_") ;followed by number: not parsed as symbol if empty
(def segment-class-flags (bit-or ClassFile/ACC_PUBLIC ClassFile/ACC_ABSTRACT))

;;; ------------------------------------------------------------------------

(defn- with-clinit ^ClassBuilder [^ClassBuilder cb ^String create-namespace
                                  ^Consumer clinit-handler ^String core-init]
  (classfile/with-method-body->
    cb [ConstantDescs/CLASS_INIT_NAME ConstantDescs/MTD_void
        (bit-or ClassFile/ACC_PUBLIC ClassFile/ACC_STATIC)]
    (cond-> #_cb
      (some? create-namespace)
      (-> (.ldc create-namespace)
          (.invokestatic rt/RT "createNamespace" rt/mtd-void-string))
      
      true (classfile/accept-> clinit-handler)
      
      (some? core-init)
      (-> (.ldc core-init)
          (.invokestatic rt/RT "markCoreInitialization" rt/mtd-void-string))
      
      true (.return_))))

(defn- with-requires ^ClassBuilder [^ClassBuilder cb ^String req-str]
  (classfile/with-method-body->
   cb [rt/name-requires (MethodTypeDesc/of ConstantDescs/CD_String)
       (bit-or classfile/acc-method-static ClassFile/ACC_SYNTHETIC)]
   (-> (.ldc req-str) (.areturn))))

;;; ------------------------------------------------------------------------

(defn- segment-cld ^ClassDesc [^PackageBuilder pkg-build ^int segm-id]
  (ClassDesc/of (.pkg-name pkg-build) (str segment-class-prefix segm-id)))

(defn define-segment ^ClassBytes [^PackageBuilder pkg-build ^int segm-id
                                  ^String source-file
                                  ^Consumer class-builder-handler
                                  ^Consumer clinit-handler]
  (let [ns-str (.pkg-name pkg-build)
        cld (segment-cld pkg-build segm-id)
        cbh (^Consumer fn ^void [^ClassBuilder cb]
             (.accept class-builder-handler cb)
             (with-clinit cb
               (when (= segm-id first-segm-id)
                 ns-str)
               clinit-handler
               (when (cfg/core-lib? ns-str)
                 (str ns-str "." (.displayName cld)))))]
    (loader/bwdi pkg-build cld segment-class-flags
                 (if (= segm-id first-segm-id)
                   ConstantDescs/CD_Object
                   (segment-cld pkg-build (dec segm-id)))
                 source-file cbh)))

(defn- capstone-cld ^ClassDesc [^PackageBuilder pkg-build]
  (ClassDesc/of (.pkg-name pkg-build) capstone-class-name))
(defn define-capstone ^ClassBytes [^PackageBuilder pkg-build ^int segm-id]
  (let [req-str (if (cfg/core-lib? (.pkg-name pkg-build))
                  ""            ;suppress internal details of core lib
                  ;; FIXME... use real requires
                  (str/join "," (sort (list "tinyclj.core"))))
        source-file nil
        cbh (^Consumer fn ^void [^ClassBuilder cb]
             (-> cb
                 (with-requires req-str)))]
    (loader/bwdi pkg-build (capstone-cld pkg-build) segment-class-flags
                 (segment-cld pkg-build (dec segm-id)) source-file cbh)))
