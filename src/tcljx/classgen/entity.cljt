(ns tcljx.classgen.entity
  (:require
   (tcljx.data [wrong :as wrong] [type :as type] [model :as model]
               [reflect :as reflect])
   (tcljx.classgen [emit :as emit] [insn :as insn] [constgen :as constgen]))
  (:import
   (tcljx.data.model Expr Binding Global Constant CodeEmitter)
   (tcljx.classgen.insn Insn Insns)
   (java.lang.classfile TypeKind)
   (java.lang.constant ConstantDescs)))

(deftype ConstantImpl [^Object const-value ^Class type]
  java.lang.Record
  Constant
  (emit-insn* [_ xb line consumed-type _]
    (insn/with-void-empty-if-requested consumed-type
      (constgen/load xb const-value)
      type)))

(defn mk-constant ^ConstantImpl [^Object const-value ^Class type]
  (ConstantImpl/new const-value type))

(def insn-null (insn/mk-insn (mk-constant ConstantDescs/NULL Object)
                              type/poly-reference))
(def insn-true (insn/mk-insn (mk-constant 1 Boolean/TYPE) Boolean/TYPE))
(def insn-false (insn/mk-insn (mk-constant 0 Boolean/TYPE) Boolean/TYPE))
(defn insn-boolean ^Insn [^boolean value] (if value insn-true insn-false))

(def insn-int-one (insn/mk-insn (mk-constant 1 Integer/TYPE) Integer/TYPE))
(def insn-int-zero (insn/mk-insn (mk-constant 0 Integer/TYPE) Integer/TYPE))
(def insn-float-one (insn/mk-insn (mk-constant 1.0f Float/TYPE) Float/TYPE))

(defn new-constant ^Insn [^Object value ^Class expr-type]
  #_(prn :new-constant expr-type value)
  (java.util.Objects/requireNonNull expr-type)
  (java.util.Objects/requireNonNull value)
  (cond
    (identical? ConstantDescs/NULL value)
    insn-null                          ;enforce singleton
    
    (type/same? Boolean/TYPE expr-type)
    (insn-boolean (not (zero? ^int value))) ;enforce singletons
    
    :else (insn/mk-insn (mk-constant value expr-type) expr-type)))

(defn constants? ^boolean [^Insns insns]
  (loop [i (dec (.length insns))]
    (cond (neg? i) true
          (model/const? insns i) (recur (dec i))
          :else false)))

(defn as-typed-constants ^Object/1 [^Insns insns]
  (let [a (new Object/1 (.length insns))]
    (dotimes [i (.length insns)]
      (aset a i (constgen/typed-constant-value insns i)))
    a))

;;; ------------------------------------------------------------------------

(deftype GlobalImpl [^String namespace ^String name ^CodeEmitter bound-to]
  java.lang.Record
  Global
  )

(def mk-global GlobalImpl/new)

;;; ------------------------------------------------------------------------

;;; Helper interface for the local variable protocol.  When a method's
;;; code attribute is produced, the Binding is established first: its
;;; `emit-opnd` sets a private mutable field to the allocated slot
;;; value.  The binding's `bound-value` method returns an Insn whose
;;; own `emit-opnd` closes over the Binding instance (not the mutable
;;; slot value in the mutable field!), allowing it to retrieve the
;;; previously stored value via `allocated-slot`.  Initializiation
;;; should set the undefined slot value to -1.
(definterface Local
  :extends [Binding]
  (name ^String [])                     ;passed to `localVariable`
  (type ^Class [])                      ;passed to `localVariable`
  (allocated-slot ^int []))

(deftype LocalImpl [^int param-no ^String name ^Class type ^CodeEmitter bound-to
                    ^:unsynchronized-mutable ^int slot]
  Binding
  (emit-insn* [this xb line consumed-type _]
    (if (model/ce-const? bound-to)
      Void/TYPE
      
      (let [tk (TypeKind/from type)]
        (if (some? bound-to)          ;let binding provides `bound-to`
          (when (emit/insn? xb line type bound-to false)
            (set! slot (.allocateLocal xb tk))
            (let [label (.newLabel xb)]
              (-> (.storeLocal xb tk slot)
                  (.labelBinding label)
                  (.localVariable slot name (reflect/describe-class type)
                                  label (.endLabel xb)))
              Void/TYPE))
          (do (set! slot (.parameterSlot xb param-no))
              (.localVariable xb slot name (reflect/describe-class type)
                              (.startLabel xb) (.endLabel xb))
              Void/TYPE)))))
  (bound-to [_]
    bound-to)
  (bound-value [this]
    (-> (if (model/ce-const? bound-to)
          bound-to
          (^CodeEmitter fn [xb line consumed-type _]
           (insn/with-void-empty-if-requested consumed-type
             (.loadLocal xb (TypeKind/from type) (.allocated-slot this))
             type)))
        (insn/mk-insn type)))
  
  Local
  (name [_]
    name)
  (type [_]
    type)
  (allocated-slot [_]
    slot))

(defn mk-parameter ^Local [^int param-no ^String name ^Class type]
  (LocalImpl. param-no name type nil -1))
(defn mk-local ^Local [^String name ^Expr init]
  (LocalImpl. -1 name (insn/logical-type init) (insn/emitter init) -1))
