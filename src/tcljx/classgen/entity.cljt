(ns tcljx.classgen.entity
  (:require
   (tcljx.data [wrong :as wrong] [type :as type] [context :as context]
               [model :as model] [reflect :as reflect])
   (tcljx.classgen [emit :as emit] [insn :as insn] [constgen :as constgen]))
  (:import
   (tcljx.data.model Expr CodeEmitter Binding Entity Loadable Constant Invocable)
   (tcljx.classgen.insn Insn Insns)
   (java.lang.classfile CodeBuilder TypeKind)
   (java.lang.constant ConstantDesc ClassDesc MethodTypeDesc ConstantDescs)))

(deftype LoadableImpl [^Object loadable-value ^Class opnd-type
                       ^boolean associate-meta?]
  java.lang.Record
  Loadable
  (emit-insn* [this xb consumed-type _]
    (if (type/same? Void/TYPE consumed-type)
      Void/TYPE
      (do (.ldc xb (.loadable-entry this (.constantPool xb) false))
          opnd-type)))
  (entity-type [_]
    opnd-type)
  (loadable-entry [_ cpb logical-type?]
    (if (instance? CodeEmitter/1 loadable-value)
      (if associate-meta?
        (constgen/with-meta-entry cpb opnd-type loadable-value)
        (constgen/collection-entry cpb opnd-type loadable-value))
      (constgen/loadable-entry cpb loadable-value)))
  (datafy [_]
    (letfn [(value-seq ^seq [^CodeEmitter/1 a]
              (list* (map Loadable/.datafy a)))] ;wrap LazySeq
      (cond
        (seq? loadable-value) (list* loadable-value) ;wrap LazySeq
        
        (instance? CodeEmitter/1 loadable-value)
        (let [a ^CodeEmitter/1 loadable-value]
          (if associate-meta?
            (with-meta (Loadable/.datafy (aget a 0))
              (Loadable/.datafy (aget a 1)))
            (condp identical? opnd-type
              type/vector (vec (value-seq a))
              type/map (apply array-map (value-seq a))
              type/set (set (value-seq a))
              type/list (value-seq a))))
        
        :else loadable-value))))

(def mk-loadable LoadableImpl/new)

(defn loadables? ^boolean [^Insns insns]
  (loop [i (dec (.length insns))]
    (cond (neg? i) true
          (model/loadable? insns i) (recur (dec i))
          :else false)))

;;; ------------------------------------------------------------------------

(deftype ConstantImpl [^Object computational-value ^Class opnd-type]
  java.lang.Record
  Constant
  (emit-insn* [this xb consumed-type _]
    (cond
      (type/same? Void/TYPE consumed-type)
      Void/TYPE
      
      (model/null? computational-value)
      (do (.aconst_null xb)          ;elide any additional checkcast
          (if (.isPrimitive consumed-type)
            (.class-reference (type/of consumed-type))
            consumed-type))
      
      (type/same? opnd-type consumed-type)
      (do (if (instance? ConstantDesc computational-value)
            (.loadConstant xb ^ConstantDesc computational-value)
            (.ldc xb (.loadable-entry this (.constantPool xb) false)))
          consumed-type)
      
      (instance? Number computational-value)
      (if (.isPrimitive consumed-type) ;cannot be void
        (do (->> (type/convert-from-constant consumed-type computational-value)
                 (.loadConstant xb))
            consumed-type)
        (do (.loadConstant xb ^ConstantDesc computational-value)
            opnd-type))
      
      :else
      (do (.ldc xb (.loadable-entry this (.constantPool xb) false))
          opnd-type)))
  (entity-type [_]
    opnd-type)
  (loadable-entry [this cpb logical-type?]
    (->> (if logical-type? (.datafy this) computational-value)
         (constgen/loadable-entry cpb)))
  (datafy [_]
    (when-not (model/null? computational-value)
      (if (instance? Integer computational-value)
        (let [x ^int computational-value]
          (condp identical? opnd-type
            Integer/TYPE computational-value
            Boolean/TYPE (object (not= x 0))
            Character/TYPE (object (char x))
            Byte/TYPE (object (byte x))
            Short/TYPE (object (short x))))
        computational-value))))

(defn mk-constant ^ConstantImpl [^Object computational-value ^Class type]
  (assert (or (instance? java.lang.constant.ConstantDesc computational-value)
              (instance? Class computational-value)))
  (ConstantImpl/new computational-value type))

(def insn-null (insn/mk-insn (mk-constant ConstantDescs/NULL Object)
                              type/poly-reference))

(def emitter-false (mk-constant 0 Boolean/TYPE))
(def emitter-true (mk-constant 1 Boolean/TYPE))
(def insn-false (insn/mk-insn emitter-false Boolean/TYPE))
(def insn-true (insn/mk-insn emitter-true Boolean/TYPE))
(defn insn-boolean ^Insn [^boolean value] (if value insn-true insn-false))

(def emitter-int-zero (mk-constant 0 Integer/TYPE))
(def insn-int-zero (insn/mk-insn emitter-int-zero Integer/TYPE))
(def insn-int-one (insn/mk-insn (mk-constant 1 Integer/TYPE) Integer/TYPE))
(def insn-int-m1 (insn/mk-insn (mk-constant -1 Integer/TYPE) Integer/TYPE))
(def insn-float-one (insn/mk-insn (mk-constant 1.0f Float/TYPE) Float/TYPE))

(defn new-constant ^Insn [^Object value ^Class expr-type]
  #_(prn :new-constant expr-type value)
  (java.util.Objects/requireNonNull expr-type)
  (java.util.Objects/requireNonNull value)
  (cond
    (identical? ConstantDescs/NULL value)
    insn-null                          ;enforce singleton
    
    (type/same? Boolean/TYPE expr-type)
    (insn-boolean (not (zero? ^int value))) ;enforce singletons
    
    :else (insn/mk-insn (mk-constant value expr-type) expr-type)))

;;; ------------------------------------------------------------------------

(definterface MutableEntity
  :extends [Entity]
  ;; Create an alias for this entity when it appears as value of a let
  ;; binding?
  (alias-when-let-value? ^boolean [])
  
  ;; Emits code taking one operand from the stack and overwrites the
  ;; entity's current value with it.
  (emit-store ^CodeBuilder [^CodeBuilder xb])
  (emit-iinc ^CodeBuilder [^CodeBuilder xb ^int const]) ;optional
  
  ;; Called from Binding.  Returns false if control then leaves the
  ;; method.
  (emit-setup-code? ^boolean [^CodeBuilder xb ^CodeEmitter init]))

(defn alias-when-let-value? ^boolean [^CodeEmitter ce]
  (when (instance? Entity ce)
    (if (instance? MutableEntity ce)
      (.alias-when-let-value? ^MutableEntity ce)
      true)))

;;; ------------------------------------------------------------------------

(defn- dup ^CodeBuilder [^CodeBuilder xb ^Class type]
  (case (.slotSize (TypeKind/from type))
    1 (.dup xb)
    2 (.dup2 xb)))

(defn static-field ^Entity [^ClassDesc owner ^String fnm ^Class type]
  (let [type-cld (reflect/describe-class type)]
    (reify MutableEntity
      (emit-insn* [_ xb _ _]
        (.getstatic xb owner fnm type-cld)
        type)
      (entity-type [_]
        type)
      (alias-when-let-value? [_]
        false)
      (emit-store [_ xb]
        (throw (UnsupportedOperationException.)))
      (emit-iinc [_ xb const]
        (throw (UnsupportedOperationException.)))
      (emit-setup-code? [_ xb init]
        (when (emit/insn? xb context/fctx-none type init)
          (.putstatic xb owner fnm type-cld)
          true)))))

(defn static-method ^Entity [^ClassDesc owner ^String mnm ^Class type]
  (let [mtd (MethodTypeDesc/of (reflect/describe-class type))]
    (reify Entity
      (emit-insn* [_ xb _ _]
        (.invokestatic xb owner mnm mtd)
        type)
      (entity-type [_]
        type))))

(deftype LocalVariable [^String name ^Class type
                        ^:unsynchronized-mutable ^int slot]
  MutableEntity
  (emit-insn* [_ xb consumed-type _]
    (insn/with-void-empty-if-requested consumed-type
      (.loadLocal xb (TypeKind/from type) slot)
      type))
  (entity-type [_]
    type)
  (alias-when-let-value? [_]
    true)
  (emit-store [_ xb]
    (.storeLocal xb (TypeKind/from type) slot))
  (emit-iinc [_ xb const]
    (.iinc xb slot const))
  (emit-setup-code? [_ xb init]
    (let [tk (TypeKind/from type), label (.newLabel xb)]
      (when-some [on-stack-type (.emit-insn* init xb type false)]
        (assert (identical? type on-stack-type))
        (set! slot (.allocateLocal xb tk))
        (-> (.storeLocal xb tk slot)
            (.labelBinding label)
            (.localVariable slot name (reflect/describe-class type)
                            label (.endLabel xb)))
        true))))

(defn- local-variable ^Entity [^String name ^Class type]
  (LocalVariable. name type -1))

(deftype Parameter [^String name ^Class type ^int param-no
                    ^:unsynchronized-mutable ^int slot]
  MutableEntity
  (emit-insn* [_ xb consumed-type _]
    (insn/with-void-empty-if-requested consumed-type
      (.loadLocal xb (TypeKind/from type) slot)
      type))
  (entity-type [_]
    type)
  (alias-when-let-value? [_]
    true)
  (emit-store [_ xb]
    (.storeLocal xb (TypeKind/from type) slot))
  (emit-iinc [_ xb const]
    (.iinc xb slot const))
  (emit-setup-code? [_ xb _]
    (set! slot (.parameterSlot xb param-no))
    (.localVariable xb slot name (reflect/describe-class type)
                    (.startLabel xb) (.endLabel xb))
    true))

(defn- parameter ^Entity [^String name ^Class type ^int param-no]
  (Parameter. name type param-no -1))

;;; ------------------------------------------------------------------------

(deftype BindingImpl [^Entity bound-to ^Expr bound-value
                      ^String namespace ^String name
                      ^CodeEmitter initialize-with]
  java.lang.Record
  Binding
  (emit-insn* [this xb consumed-type _]
    ;; skip setup if this binding is an alias, the bound entity is
    ;; immutable, or the entity's value is provided by other means
    (when (or (nil? initialize-with)
              (not (instance? MutableEntity bound-to))
              (.emit-setup-code? ^MutableEntity bound-to xb initialize-with))
      Void/TYPE)))

(defn- mk-binding ^Binding [^Entity bound-to ^Insn bound-value
                            ^String namespace ^String name
                            ^CodeEmitter initialize-with]
  (BindingImpl. bound-to bound-value namespace name initialize-with))

(defn parameter-binding ^Binding [^int param-no ^String name ^Class type]
  (let [ent (parameter name type param-no)]
    (mk-binding ent (insn/mk-insn ent type) nil name insn/emitter-empty)))

(defn local-binding ^Binding [^boolean fresh-entity? ^Class opt-type
                              ^String name ^Insn init ^long fctx]
  (letfn [(non-alias-local ^Binding [^Insn init]
            (let [ce (insn/emitter init)
                  tp (or opt-type (insn/logical-type init))]
              (if (and (model/ce-const? ce) (nil? opt-type) (not fresh-entity?))
                (mk-binding ce init nil name nil)
                (let [ent (local-variable name tp)
                      ce (-> init (insn/cast-to tp fctx) .emitter)]
                  (mk-binding ent (insn/mk-insn ent tp) nil name ce)))))]
    (let [ce (.emitter init)]
      (if (and (not fresh-entity?) (alias-when-let-value? ce))
        (mk-binding ce init nil name nil)
        (non-alias-local init)))))

;; FIXME... get rid of complete-non-value-invocable eventually
(defn- non-value-invocable-insn ^Insn [^Invocable inv]
  (-> (reify Invocable
        (emit-insn* [_ _ _ _]
          (throw (IllegalStateException.)))
        (entity-type [_]
          type/polymorphic)
        (invoke* [_ fctx form xs]
          (.invoke* inv fctx form xs)))
      (insn/mk-insn type/polymorphic)))

(defn global-binding
  (^Binding [^Entity bound-to ^String namespace ^String name]
   (let [inv (non-value-invocable-insn bound-to)]
     (mk-binding bound-to inv namespace name nil))) ;for predefined entities
  (^Binding [^Entity bound-to ^Insn bound-value ^String namespace ^String name
             ^CodeEmitter initialize-with]
   (mk-binding bound-to bound-value namespace name initialize-with)))
