(ns tcljx.classgen.defgen
  (:require
   (tcljx.data [config :as cfg] [reflect :as reflect] [context :as context]
               [model :as model])
   (tcljx.classgen [classfile :as classfile] [insn :as insn] [entity :as entity]
                   [emit :as emit] [envgraph :as envgraph]))
  (:import
   (tcljx.data.model CodeEmitter Entity LoadFromCopyWhenBound Invocable Binding)
   (tcljx.classgen.emit ClosureBlueprint IEmitProxy)
   (tcljx.classgen.envgraph  PreparationTask)
   (tcljx.classgen.insn Insn)
   (java.lang.classfile ClassFile ClassBuilder)
   (java.lang.constant MethodTypeDesc ClassDesc)
   (java.util.function Consumer)))

(defn- field-fragment ^Entity [^int or-flags ^ClassDesc owner ^String nm
                               ^Class tp ^CodeEmitter init-ce]
  (reify
    CodeEmitter
    (emit-insn* [_ xb consumed-type _]
      (entity/with-void-empty-if-requested consumed-type
        (.getstatic xb owner nm (emit/value-type-exact init-ce tp))
        tp))
    Entity
    (value-type [_]
      tp)
    (entity-id [_]
      model/entity-id-def)
    LoadFromCopyWhenBound
    (emit-setup-code? [_ xb]
      (or (identical? entity/emitter-empty init-ce)
          (when (emit/insn? xb context/fctx-none tp init-ce)
            (.putstatic xb owner nm (emit/value-type-exact init-ce tp))
            true)))
    PreparationTask
    (extend-segment [_ acc _]
      (->> (^Consumer fn ^void [^ClassBuilder cb]
            (.withField cb nm (emit/value-type-exact init-ce tp)
                        (bit-or ClassFile/ACC_STATIC
                                ClassFile/ACC_PUBLIC ;unless namespace private
                                or-flags)))
           (emit/add-segment-elements acc)))))

(defn- method-fragment ^Entity [^ClassDesc owner ^String nm
                                ^Class tp ^CodeEmitter init-ce]
  (reify
    CodeEmitter
    (emit-insn* [_ xb consumed-type _]
      (entity/with-void-empty-if-requested consumed-type
        (let [tpd (emit/value-type-exact init-ce tp)]
          (.invokestatic xb owner nm (MethodTypeDesc/of tpd))
          tp)))
    Entity
    (value-type [_]
      tp)
    (entity-id [_]
      model/entity-id-def)
    LoadFromCopyWhenBound
    (emit-setup-code? [_ xb]
      true)
    PreparationTask
    (extend-segment [_ acc _]
      (->> (^Consumer fn ^void [^ClassBuilder cb]
            (let [tpd (emit/value-type-exact init-ce tp)]
              (classfile/with-method-body-> cb
                [nm (MethodTypeDesc/of tpd) classfile/acc-method-static]
                (emit/then-leave? cfg/no-line-number tp init-ce true))))
           (emit/add-segment-elements acc)))))

(deftype DefOfProxy [^ClassDesc owner ^String nm ^Class tp ^IEmitProxy proxy
                     ^:unsynchronized-mutable ^LoadFromCopyWhenBound __entity]
  CodeEmitter
  (emit-insn* [_ xb consumed-type then-leave?]
    (.emit-insn* __entity xb consumed-type then-leave?))
  Entity
  (value-type [_]
    tp)
  (entity-id [_]
    model/entity-id-def)
  LoadFromCopyWhenBound
  (emit-setup-code? [_ xb]
    (.emit-setup-code? __entity xb))
  PreparationTask
  (extend-segment [_ acc class] ;decides translation pattern based on `proxy`
    (let [ce* (.get-emit-entity proxy)
          ent (if (entity/const? ce*)
                (method-fragment owner nm tp ce*)
                (field-fragment ClassFile/ACC_FINAL owner nm tp ce*))]
      (set! __entity ent)
      (envgraph/run-task acc ^PreparationTask ent class))))

(letfn [(invocable-fragment ^Entity [^LoadFromCopyWhenBound ent ^Invocable inv]
          (reify Invocable LoadFromCopyWhenBound
            (emit-insn* [_ xb consumed-type then-leave?]
              (.emit-insn* ent xb consumed-type then-leave?))
            (value-type [_]
              (.value-type ent))
            (entity-id [_]
              model/entity-id-def)
            (emit-setup-code? [_ xb]
              (.emit-setup-code? ent xb))
            (invoke* [_ fctx form _ xs]
              (.invoke* inv fctx form ent xs))))]
  
  (defn new-def-immutable ^Binding [^ClosureBlueprint segm ^String nm ^Insn init]
    (let [sink-type (insn/logical-type init)
          init-ce (insn/emitter init)
          owner (.segment-class segm)
          ent (cond
                (entity/const? init-ce)
                (method-fragment owner nm sink-type init-ce)
                (instance? IEmitProxy init-ce)
                (DefOfProxy. owner nm sink-type init-ce nil)
                :else
                (field-fragment ClassFile/ACC_FINAL owner nm sink-type init-ce))]
      (emit/stage-task! segm ent)
      (-> (if (and (model/invocable? init-ce) (not (model/invocable? ent)))
            (invocable-fragment ent init-ce)
            ^Entity ent)
          (entity/mk-binding nm false))))

  (defn new-def-mutable ^Binding [^ClosureBlueprint segm ^String nm ^Insn init]
    (let [sink-type (insn/logical-type init)
          init-ce (insn/emitter init)
          ent (field-fragment ClassFile/ACC_VOLATILE (.segment-class segm) nm
                             sink-type init-ce)]
      (emit/stage-task! segm ent)
      (entity/mk-binding ent nm false))))
