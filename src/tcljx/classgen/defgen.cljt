(ns tcljx.classgen.defgen
  (:require
   (tcljx.data
    [config :as cfg] [reflect :as reflect] [resolve :as resolve] [type :as type]
    [member :as member] [context :as context] [wrong :as wrong] [ir :as ir])
   (tcljx.classgen
    [classfile :as classfile] [util :as util] segment [runtime :as rt]
    [constgen :as constgen] [insn :as insn] [emit :as emit]))
  (:import
   (tcljx.data.ir CodeEmitter Entity FnPromise BindableEntity ImmediateEntity
                  Assignable Binding)
   (tcljx.classgen.segment CodePromise)
   (tcljx.classgen.insn Insn)
   (java.lang.classfile ClassBuilder ClassFileBuilder FieldBuilder MethodBuilder
                        CodeBuilder AnnotationElement Annotation Signature)
   (java.lang.classfile.attribute RuntimeVisibleAnnotationsAttribute
                                  SignatureAttribute)   
   (java.lang.constant MethodTypeDesc ClassDesc ConstantDescs)
   (java.util.function Consumer)))

(defn apply-private ^int [^int flags ^boolean private?]
  (cond-> flags private? (bit-and-not (ir/acc* public))))

(defn- with-annotation-macro ^ClassFileBuilder [^ClassFileBuilder b]
  (let [ae (new AnnotationElement/1 0)
        ann (doto (new Annotation/1 1)
              (aset 0 (Annotation/of rt/Macro ae)))]
    (.with b (RuntimeVisibleAnnotationsAttribute/of ann))))

(deftype DefField [^int entity-id ^int flags ^ClassDesc owner ^String mnm
                   ^Class tp ^CodeEmitter init-ce ^SignatureAttribute signature
                   ^:volatile-mutable ^Object __runtime-value]
  CodeEmitter
  (emit-insn* [this xb consumed-type _]
    (util/with-void-empty-if-requested consumed-type
      (.getstatic xb owner mnm (.value-type-exact this))
      tp))
  ImmediateEntity
  (value-type [this]
    (when-not (ir/def-macro? this)
      tp))
  (value-type-exact [_]
    (ir/value-type-exact init-ce tp))
  (entity-id [_]
    entity-id)
  (invoke-methods [_]
    (if (member/acc-final? flags)
      (ir/invoke-methods-of init-ce)
      ir/invoke-methods-dynamic))
  (invoke* [this fctx form _ m-idx xs]
    (.invoke* ^Entity init-ce fctx form this m-idx xs))
  (namespace-if-def [_]
    (.packageName owner))
  BindableEntity
  (binding-mode-let [_]
    (if (member/acc-final? flags)
      ir/bound-as-alias
      ir/bound-by-copy-to-local))
  (emit-setup-code? [this xb _ _]
    (or (constgen/emitter-empty? init-ce)
        (when (emit/insn? xb context/fctx-none tp init-ce)
          (.putstatic xb owner mnm (.value-type-exact this))
          true)))
  Assignable
  (set-expr* [_ fctx value]
    nil)
  (redef-expr* [this fctx value]
    (when-not (member/acc-final? flags)
      (insn/ensure-assignable (.value-type this) value)
      (-> (^CodeEmitter fn [xb consumed-type _]
           (when (emit/insn? xb fctx tp (.emitter value))
             (util/with-dup-unless-void-> xb consumed-type tp
               (.putstatic owner mnm (.value-type-exact this)))))
          (insn/mk-insn tp))))
  
  CodePromise
  (expr-of [this]
    (insn/mk-insn this (.value-type this)))
  (child-methods [_]
    nil)
  (!nested-parts [_]
    nil)
  (generate-and-deliver [this sb _]
    (->> (^Consumer fn ^void [^ClassBuilder cb]
          (->> (^Consumer fn ^void [^FieldBuilder fb]
                (.withFlags fb flags)
                (cond-> fb
                  (some? signature) (.with signature)
                  (ir/def-macro? this) (with-annotation-macro)))
               (.withField cb mnm (.value-type-exact this))))
         (.add-elements sb)))
  (runtime-value [this rsv]
    (if (some? __runtime-value)         ;only caches non-nil values
      __runtime-value
      (->> (.load-def-value rsv owner mnm (.value-type-exact this))
           (set! __runtime-value)))))
(defn- promise-field ^CodePromise [^int entity-id ^int flags ^ClassDesc owner
                                   ^String nm ^Class tp ^CodeEmitter init-ce
                                   ^SignatureAttribute signature]
  (assert (some? init-ce))
  (DefField. entity-id flags owner (cfg/munge-name nm) tp init-ce
    signature nil))

(deftype DefMethod [^int entity-id ^int flags ^ClassDesc owner ^String mnm
                    ^Class tp ^Entity init-ce
                    ^:volatile-mutable ^Object __runtime-value]
  CodeEmitter
  (emit-insn* [this xb consumed-type _]
    (util/with-void-empty-if-requested consumed-type
      (let [tpd (.value-type-exact this)]
        (.invokestatic xb owner mnm (MethodTypeDesc/of tpd))
        tp)))
  ImmediateEntity
  (value-type [this]
    (when-not (ir/def-macro? this)
      tp))
  (value-type-exact [_]
    (ir/value-type-exact init-ce tp))
  (entity-id [_]
    entity-id)
  (invoke-methods [_]
    (.invoke-methods init-ce))
  (invoke* [this fctx form _ m-idx xs]
    (.invoke* init-ce fctx form this m-idx xs))
  (namespace-if-def [_]
    (.packageName owner))
  BindableEntity
  (binding-mode-let [_]
    ir/bound-as-alias)
  (emit-setup-code? [_ xb _ _]
    true)
  CodePromise
  (expr-of [this]
    (insn/mk-insn this (.value-type this)))
  (child-methods [_]
    nil)
  (!nested-parts [_]
    nil)
  (generate-and-deliver [this sb _]
    (->> (^Consumer fn ^void [^ClassBuilder cb]
          (->> (^Consumer fn ^void [^MethodBuilder mb]
                (cond-> mb (ir/def-macro? this) (with-annotation-macro))
                (->> (^Consumer fn ^void [^CodeBuilder xb]
                      (emit/then-leave? xb context/fctx-none tp init-ce true))
                     (.withCode mb)))
               (.withMethod cb mnm (MethodTypeDesc/of (.value-type-exact this))
                            flags)))
         (.add-elements sb)))
  (runtime-value [this rsv]
    (if (some? __runtime-value)         ;only caches non-nil values
      __runtime-value
      (->> (.load-def-value rsv owner mnm (.value-type-exact this))
           (set! __runtime-value)))))
(defn- promise-method ^CodePromise [^int entity-id ^boolean private?
                                    ^ClassDesc owner ^String nm ^Class tp
                                    ^Entity init-ce]
  (let [flags (apply-private classfile/acc-method-static private?)]
    (DefMethod. entity-id flags owner (cfg/munge-name nm) tp init-ce nil)))
(defn- wrap-loadable-as-method? ^boolean [^CodeEmitter ce]
  (when (instance? ImmediateEntity ce) ;want JvmConstant or FnStatic only
    ;; Note: Exposing FnStatic as an on-demand LDC of a dynamic
    ;; constant avoids materializing lots and lots of fn
    ;; instances (and method handles!) that are never used as a value.
    (not= (ir/closure-level ce) ir/closure-level-def)))

(deftype DefOfPromise [^int entity-id ^boolean private? ^ClassDesc owner
                       ^String nm ^Class tp ^FnPromise init-ce
                       ^:unsynchronized-mutable ^BindableEntity __entity]
  CodeEmitter
  (emit-insn* [_ xb consumed-type then-leave?]
    (.emit-insn* __entity xb consumed-type then-leave?))
  ImmediateEntity
  (value-type [this]
    (when-not (ir/def-macro? this)
      tp))
  (value-type-exact [_]
    (.value-type-exact __entity))
  (entity-id [_]
    entity-id)
  (invoke-methods [_]
    (.invoke-methods init-ce))
  (invoke* [this fctx form opt-f m-idx xs]
    (.invoke* init-ce fctx form this m-idx xs))
  (namespace-if-def [_]
    (.packageName owner))
  BindableEntity
  (binding-mode-let [_]
    ir/bound-as-alias)
  (emit-setup-code? [_ xb param-no name]
    (.emit-setup-code? __entity xb param-no name))
  CodePromise
  (expr-of [this]
    (insn/mk-insn this tp))
  (child-methods [_]
    nil)
  (!nested-parts [_]
    nil)
  (generate-and-deliver [_ sb env-factory]
    (let [ce* (.get-emit-entity init-ce)
          p (if (wrap-loadable-as-method? ce*)
              (promise-method entity-id private? owner nm tp ce*)
              (let [flags (apply-private (ir/acc* public static final) private?)]
                (promise-field entity-id flags owner nm tp ce* nil)))]
      (set! __entity (-> p .expr-of .emitter))
      (.generate-and-deliver p sb env-factory)))
  (runtime-value [_ rsv]
    (if (some? __entity)
      (.runtime-value __entity rsv)
      (throw (tcljx.data.ir.ImplementationUnavailableException.)))))

(defn promise-def-immutable ^CodePromise [^int entity-id ^boolean private?
                                          ^ClassDesc owner ^String nm ^Insn init]
  (let [tp (insn/logical-type init), init-ce (insn/emitter init)]
    (cond
      (wrap-loadable-as-method? init-ce)
      (promise-method entity-id private? owner nm tp init-ce)
      (instance? FnPromise init-ce)
      (DefOfPromise. entity-id private? owner nm tp init-ce nil)
      :else
      (let [flags (apply-private (ir/acc* public static final) private?)]
        (promise-field entity-id flags owner nm tp init-ce nil)))))

(defn promise-def-mutable ^CodePromise [^int entity-id ^boolean private?
                                        ^ClassDesc owner ^String nm ^Insn init]
  (let [tp (insn/logical-type init), init-ce (insn/emitter init)
        flags (apply-private (ir/acc* public static volatile) private?)]
    (promise-field entity-id flags owner nm tp init-ce nil)))

(defn promise-def-alias ^CodePromise [^boolean private? ^String nm
                                      ^Entity other-ent ^String other-qname]
  (let [mnm (cfg/munge-name nm)]
    (reify CodePromise
      (expr-of [_]
        (insn/mk-insn other-ent (.value-type other-ent)))
      (child-methods [_]
        nil)
      (!nested-parts [_]
        nil)
      (generate-and-deliver [_ sb _]
        (->> (^Consumer fn ^void [^ClassBuilder cb]
              (->> (^Consumer fn ^void [^FieldBuilder fb]
                    (let [ae (AnnotationElement/ofString "of" other-qname)
                          flags (apply-private (ir/acc* public static final)
                                               private?)]
                      (-> (.withFlags fb flags)
                          (.with (RuntimeVisibleAnnotationsAttribute/of
                                  (Annotation/of rt/Alias ae))))))
                   (.withField cb mnm ConstantDescs/CD_Object)))
             (.add-elements sb))))))

;;; ------------------------------------------------------------------------

(deftype DefVarDeref [^DefField df ^Class deref-tp]
  CodeEmitter
  (emit-insn* [this xb consumed-type then-leave?]
    (when (.emit-insn* df xb clojure.lang.Var then-leave?)
      (.invokevirtual xb rt/Var "deref" rt/mtd-Object-void)
      Object))
  ImmediateEntity
  (value-type [this]
    deref-tp)
  (value-type-exact [_]
    (reflect/describe-class deref-tp))
  (entity-id [_]
    ir/entity-id-def-var)
  (invoke-methods [_]
    ir/invoke-methods-dynamic)
  (namespace-if-def [_]
    (.namespace-if-def df))
  BindableEntity
  (binding-mode-let [_]
    ir/bound-by-copy-to-local)
  (emit-setup-code? [this xb param-no name]
    (.emit-setup-code? df xb param-no name))
  Assignable
  (set-expr* [_ fctx value]
    (-> (^CodeEmitter fn [xb consumed-type _]
         (.emit-insn* df xb clojure.lang.Var false)
         (when (emit/insn? xb fctx deref-tp (.emitter value))
           (.invokevirtual xb rt/Var "set" rt/mtd-Object-Object)
           Object))
        (insn/mk-insn deref-tp)))
  (redef-expr* [_ fctx value]
    (insn/ensure-assignable deref-tp value)
    (-> (^CodeEmitter fn [xb consumed-type _]
         (when (emit/insn? xb fctx clojure.lang.Var df)
           (when (emit/insn? xb fctx Object (.emitter value))
             (util/with-dup-unless-void-> xb consumed-type Object
               (.invokevirtual rt/Var "bindRoot" rt/mtd-void-Object)))))
        (insn/mk-insn deref-tp)))
         
  CodePromise
  (expr-of [this]
    (insn/mk-insn this (.value-type this)))
  (child-methods [_]
    nil)
  (!nested-parts [_]
    nil)
  (generate-and-deliver [this sb env-factory]
    (.generate-and-deliver df sb env-factory))
  (runtime-value [this rsv]
    (throw (IllegalStateException.))))

(defn promise-def-var ^DefVarDeref [^boolean private? ^ClassDesc owner ^String nm
                                    ^Insn init-var ^Class tp-var
                                    ^boolean dynamic? ^Insn x-meta]
  (letfn [(create-var ^Insn []
            (let [x-nmsp (insn/constant (.packageName owner) String)
                  x-nm (insn/constant nm String)]
              (if (constgen/emitter-empty? (.emitter init-var))
                (insn/invoke-member resolve/create-var-2 nil
                                    (insn/insns-of x-nmsp x-nm))
                (insn/invoke-member resolve/create-var-3 nil
                                    (insn/insns-of x-nmsp x-nm init-var)))))
          (set-meta ^Insn [^Insn x]
            (insn/invoke-member resolve/set-meta x (insn/insns-of x-meta)))
          (set-dynamic ^Insn [^Insn x]
            (insn/invoke-member resolve/set-dynamic x (insn/insns-of)))]
    (let [init (cond-> (create-var)
                 (some? x-meta) (set-meta)
                 dynamic? (set-dynamic))
          signature (-> (str "Lclojure/lang/Var<"
                             (.descriptorString tp-var) ">;")
                        (Signature/parseFrom)
                        (SignatureAttribute/of))
          flags (apply-private (ir/acc* public static final) private?)]
      (-> (promise-field ir/entity-id-def flags owner nm (.expr-type init)
                         (.emitter init) signature)
          (DefVarDeref. tp-var)))))
