(ns tcljx.classgen.defgen
  (:require
   (tcljx.data [config :as cfg] [reflect :as reflect] [context :as context]
               [model :as model])
   (tcljx.classgen [classfile :as classfile] [insn :as insn] [entity :as entity]
                   [emit :as emit]))
  (:import
   (tcljx.data.model CodeEmitter Entity LoadFromCopyWhenBound Invocable Binding)
   (tcljx.classgen.emit OwnedFragment ClosureBlueprint IEmitProxy)
   (tcljx.classgen.insn Insn)
   (java.lang.classfile ClassFile ClassBuilder)
   (java.lang.constant MethodTypeDesc ClassDesc)
   (java.util.function Consumer)))

(defn- field-fragment ^OwnedFragment [^int or-flags ^ClassDesc owner ^String nm
                                      ^Class tp ^CodeEmitter init-ce]
  (reify OwnedFragment
    (emit-insn* [_ xb consumed-type _]
      (entity/with-void-empty-if-requested consumed-type
        (.getstatic xb owner nm (emit/value-type-exact init-ce tp))
        tp))
    
    (value-type [_]
      tp)
    (entity-id [_]
      model/entity-id-def)
    (emit-setup-code? [_ xb]
      (or (identical? entity/emitter-empty init-ce)
          (when (emit/insn? xb context/fctx-none tp init-ce)
            (.putstatic xb owner nm (emit/value-type-exact init-ce tp))
            true)))
    
    (prepare-for-emit! [_ acc]
      (->> (^Consumer fn ^void [^ClassBuilder cb]
            (.withField cb nm (emit/value-type-exact init-ce tp)
                        (bit-or ClassFile/ACC_STATIC
                                ClassFile/ACC_PUBLIC ;unless namespace private
                                or-flags)))
           (emit/add-segment-elements acc)))))

(defn- method-fragment ^OwnedFragment [^ClassDesc owner ^String nm
                                       ^Class tp ^CodeEmitter init-ce]
  (reify OwnedFragment
    (emit-insn* [_ xb consumed-type _]
      (entity/with-void-empty-if-requested consumed-type
        (let [tpd (emit/value-type-exact init-ce tp)]
          (.invokestatic xb owner nm (MethodTypeDesc/of tpd))
          tp)))
    
    (value-type [_]
      tp)
    (entity-id [_]
      model/entity-id-def)
    (emit-setup-code? [_ xb]
      true)
    
    (prepare-for-emit! [_ acc]
      (->> (^Consumer fn ^void [^ClassBuilder cb]
            (let [tpd (emit/value-type-exact init-ce tp)]
              (classfile/with-method-body-> cb
                [nm (MethodTypeDesc/of tpd) classfile/acc-method-static]
                (emit/then-leave? cfg/no-line-number tp init-ce true))))
           (emit/add-segment-elements acc)))))

(deftype DefOfProxy [^ClassDesc owner ^String nm ^Class tp ^IEmitProxy proxy
                       ^:unsynchronized-mutable ^OwnedFragment of]
  CodeEmitter
  (emit-insn* [_ xb consumed-type then-leave?]
    (.emit-insn* of xb consumed-type then-leave?))
  Entity
  (value-type [_]
    tp)
  (entity-id [_]
    model/entity-id-def)
  (emit-setup-code? [_ xb]
    (.emit-setup-code? of xb))
  OwnedFragment
  (prepare-for-emit! [_ acc] ;decides translation pattern based on `init`
    (let [ce* (.get-emit-entity proxy)]
      (->> (set! of (if (entity/const? ce*)
                      (method-fragment owner nm tp ce*)
                      (field-fragment ClassFile/ACC_FINAL owner nm tp ce*)))
           (emit/prepare-for-emit! acc)))))

(letfn [(invocable-fragment ^Entity [^OwnedFragment of ^Invocable inv]
          (reify Invocable LoadFromCopyWhenBound
            (emit-insn* [_ xb consumed-type then-leave?]
              (.emit-insn* of xb consumed-type then-leave?))
            (value-type [_]
              (.value-type of))
            (entity-id [_]
              model/entity-id-def)
            (emit-setup-code? [_ xb]
              (.emit-setup-code? of xb))
            (invoke* [_ fctx form _ xs]
              (.invoke* inv fctx form of xs))))]
  
  (defn new-def-immutable ^Binding [^ClosureBlueprint segm ^String nm ^Insn init]
    (let [sink-type (insn/logical-type init)
          init-ce (insn/emitter init)
          owner (.segment-class segm)
          of (cond
               (entity/const? init-ce)
               (method-fragment owner nm sink-type init-ce)
               (instance? IEmitProxy init-ce)
               (DefOfProxy. owner nm sink-type init-ce nil)
               :else
               (field-fragment ClassFile/ACC_FINAL owner nm sink-type init-ce))]
      (emit/stage-fragment! segm of)
      (-> (if (and (model/invocable? init-ce) (not (model/invocable? of)))
            (invocable-fragment of init-ce)
            ^Entity of)
          (entity/mk-binding nm false))))

  (defn new-def-mutable ^Binding [^ClosureBlueprint segm ^String nm ^Insn init]
    (let [sink-type (insn/logical-type init)
          init-ce (insn/emitter init)
          of (field-fragment ClassFile/ACC_VOLATILE (.segment-class segm) nm
                             sink-type init-ce)]
      (emit/stage-fragment! segm of)
      (entity/mk-binding of nm false))))
