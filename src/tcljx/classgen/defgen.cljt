(ns tcljx.classgen.defgen
  (:require
   (tcljx.data
    [config :as cfg] [reflect :as reflect] [context :as context]
    [ir :as ir])
   (tcljx.classgen
    [classfile :as classfile] [util :as util] [runtime :as runtime]
    [constgen :as constgen] [insn :as insn] [segment :as segment]
    [emit :as emit] fngen))
  (:import
   (tcljx.data.ir CodeEmitter Entity LoadFromCopyWhenBound ImmediateEntity
                  Binding)
   (tcljx.classgen.segment CodePromise)
   (tcljx.classgen.insn Insn)
   (tcljx.classgen.fngen FnPromise)
   (java.lang.classfile ClassBuilder FieldBuilder AnnotationElement
                        Annotation)
   (java.lang.classfile.attribute RuntimeVisibleAnnotationsAttribute)   
   (java.lang.constant MethodTypeDesc ClassDesc ConstantDescs)
   (java.util.function Consumer)))

(deftype DefField [^int or-flags ^ClassDesc owner ^String mnm ^Class tp
                   ^CodeEmitter init-ce]
  CodeEmitter
  (emit-insn* [_ xb consumed-type _]
    (util/with-void-empty-if-requested consumed-type
      (.getstatic xb owner mnm (ir/value-type-exact init-ce tp))
      tp))
  ImmediateEntity
  (value-type [_]
    tp)
  (entity-id [_]
    ir/entity-id-def)
  (invoke-methods [_]
    (if (= or-flags (ir/acc* final))
      (ir/invoke-methods-of init-ce)
      ir/invoke-methods-dynamic))
  (invoke* [this fctx form _ m-idx xs]
    (.invoke* ^Entity init-ce fctx form this m-idx xs))
  LoadFromCopyWhenBound
  (emit-setup-code? [_ xb _ _]
    (or (constgen/emitter-empty? init-ce)
        (when (emit/insn? xb context/fctx-none tp init-ce)
          (.putstatic xb owner mnm (ir/value-type-exact init-ce tp))
          true)))
  CodePromise
  (expr-of [this]
    (insn/mk-insn this (.value-type this)))
  (child-methods [_]
    nil)
  (!nested-parts [_]
    nil)
  (generate-and-deliver [_ acc]
    (->> (^Consumer fn ^void [^ClassBuilder cb]
          (.withField cb mnm (ir/value-type-exact init-ce tp)
                      (bit-or (ir/acc* public static)
                              ;;... unless namespace private?
                              or-flags)))
         (segment/add-elements acc))))
(defn- promise-field ^CodePromise [^int or-flags ^ClassDesc owner ^String nm
                                   ^Class tp ^CodeEmitter init-ce]
  (DefField. or-flags owner (cfg/munge-name nm) tp init-ce))

(deftype DefMethod [^ClassDesc owner ^String mnm ^Class tp ^Entity init-ce]
  CodeEmitter
  (emit-insn* [_ xb consumed-type _]
    (util/with-void-empty-if-requested consumed-type
      (let [tpd (ir/value-type-exact init-ce tp)]
        (.invokestatic xb owner mnm (MethodTypeDesc/of tpd))
        tp)))
  ImmediateEntity
  (value-type [_]
    tp)
  (value-type-exact [_]
    (.value-type-exact init-ce))
  (entity-id [_]
    ir/entity-id-def)
  (invoke-methods [_]
    (.invoke-methods init-ce))
  (invoke* [this fctx form _ m-idx xs]
    (.invoke* init-ce fctx form this m-idx xs))
  LoadFromCopyWhenBound
  (emit-setup-code? [_ xb _ _]
    true)
  CodePromise
  (expr-of [this]
    (insn/mk-insn this (.value-type this)))
  (child-methods [_]
    nil)
  (!nested-parts [_]
    nil)
  (generate-and-deliver [_ acc]
    (->> (^Consumer fn ^void [^ClassBuilder cb]
          (let [tpd (ir/value-type-exact init-ce tp)]
            (classfile/with-method-body-> cb
              [mnm (MethodTypeDesc/of tpd) classfile/acc-method-static]
              (emit/then-leave? context/fctx-none tp init-ce true))))
         (segment/add-elements acc))))
(defn- promise-method ^CodePromise [^ClassDesc owner ^String nm ^Class tp
                                    ^Entity init-ce]
  (DefMethod. owner (cfg/munge-name nm) tp init-ce)  )

(deftype DefOfPromise [^ClassDesc owner ^String nm ^Class tp
                       ^FnPromise init-ce
                       ^:unsynchronized-mutable ^LoadFromCopyWhenBound __entity]
  CodeEmitter
  (emit-insn* [_ xb consumed-type then-leave?]
    (.emit-insn* __entity xb consumed-type then-leave?))
  ImmediateEntity
  (value-type [_]
    tp)
  (value-type-exact [_]
    (.value-type-exact __entity))
  (entity-id [_]
    ir/entity-id-def)
  (invoke-methods [_]
    (.invoke-methods init-ce))
  (invoke* [this fctx form opt-f m-idx xs]
    (.invoke* init-ce fctx form this m-idx xs))
  LoadFromCopyWhenBound
  (emit-setup-code? [_ xb param-no name]
    (.emit-setup-code? __entity xb param-no name))
  CodePromise
  (expr-of [this]
    (insn/mk-insn this tp))
  (child-methods [_]
    nil)
  (!nested-parts [_]
    nil)
  (generate-and-deliver [_ acc]
    (let [ce* (.get-emit-entity init-ce)
          p (if (constgen/const? ce*)
              (promise-method owner nm tp ce*)
              (promise-field (ir/acc* final) owner nm tp ce*))]
      (set! __entity (-> p .expr-of .emitter))
      (.generate-and-deliver p acc))))

(defn promise-def-immutable ^CodePromise [^ClassDesc owner ^String nm ^Insn init]
  (let [tp (insn/logical-type init), init-ce (insn/emitter init)]
    (cond
      (constgen/const? init-ce)
      (promise-method owner nm tp init-ce)
      (instance? FnPromise init-ce)
      (DefOfPromise. owner nm tp init-ce nil)
      :else
      (promise-field (ir/acc* final) owner nm tp init-ce))))

(defn promise-def-mutable ^CodePromise [^ClassDesc owner ^String nm ^Insn init]
  (let [tp (insn/logical-type init), init-ce (insn/emitter init)]
    (promise-field (ir/acc* volatile) owner nm tp init-ce)))

(defn promise-def-alias ^CodePromise [^String nm ^Entity other-ent
                                      ^String other-qname]
  (let [mnm (cfg/munge-name nm)]
    (reify CodePromise
      (expr-of [_]
        (insn/mk-insn other-ent (.value-type other-ent)))
      (child-methods [_]
        nil)
      (!nested-parts [_]
        nil)
      (generate-and-deliver [_ acc]
        (->> (^Consumer fn ^void [^ClassBuilder cb]
              (->> (^Consumer fn ^void [^FieldBuilder fb]
                    (let [ae (AnnotationElement/ofString "of" other-qname)]
                      (-> (.withFlags fb (ir/acc* public static final))
                          (.with (RuntimeVisibleAnnotationsAttribute/of
                                  (Annotation/of runtime/Alias ae))))))
                   (.withField cb mnm ConstantDescs/CD_Object)))
             (segment/add-elements acc))))))
