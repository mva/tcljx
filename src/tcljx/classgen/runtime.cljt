(ns tcljx.classgen.runtime
  (:import
   (java.lang.constant ClassDesc MethodTypeDesc MethodHandleDesc ConstantDescs
                       DirectMethodHandleDesc$Kind)
   (java.lang.reflect Method)))

;;; FIXME... Does it make sense to use java.lang.ClassValue to cache
;;; generated ClassDesc values?  The cache should be wiped (or shrunk,
;;; or replaced with empty) after each build to prevent stale classes
;;; from piling up in its key set.  Either use a simple redefinable
;;; def or a scoped value for the ClassValue instance.
(defn describe-class ^ClassDesc [^Class tp]
  (.get (.describeConstable tp)))
(defn describe-class* ^ClassDesc/1 [^Class/1 atp]
  (let [a (new ClassDesc/1 (alength atp))]
    (dotimes [i (alength a)]
      (aset a i (describe-class (aget atp i))))
    a))

;;; ------------------------------------------------------------------------

(defn mtd-of-method ^MethodTypeDesc [^Method m]
  (MethodTypeDesc/of (describe-class (.getReturnType m))
                     (describe-class* (.getParameterTypes m))))

(def mtd-void-string (MethodTypeDesc/of ConstantDescs/CD_void
                                        ConstantDescs/CD_String))

;;; ------------------------------------------------------------------------

(def RT (ClassDesc/of "tinyclj.lang.RT"))

#_(def ClojureRT (ClassDesc/of "clojure.lang.RT"))

(def ^:private clnm-ifn 'clojure.lang.IFn)
(def ^:private clnm-abstract-fn 'tinyclj.lang.AFnMh)
(def ^:private clnm-static-fn 'tinyclj.lang.StaticFn)

(def ^:private MethodHandle ConstantDescs/CD_MethodHandle)
(def ^:private MethodHandleArray (.arrayType MethodHandle))
(def ^:private Lookup ConstantDescs/CD_MethodHandles_Lookup)

(def ^:private Class ConstantDescs/CD_Class)
(def ^:private String ConstantDescs/CD_String)

(def ^:private BootstrapMethod (ClassDesc/of "tinyclj.lang.BootstrapMethod"))
(def StaticFn (ClassDesc/of (name clnm-static-fn)))

(def name-requires "requires~1")

;;; ------------------------------------------------------------------------

(defmacro ^:private mtd [rtype ptypev]
  `(MethodTypeDesc/of ~rtype ~(with-meta ptypev {:tag 'java.util.List})))

(defmacro ^:private mhd-static [[owner name] rtype ptypev]
  `(MethodHandleDesc/ofMethod DirectMethodHandleDesc$Kind/STATIC
                              ~owner ~name (mtd ~rtype ~ptypev)))

(def bsm-static-fn (mhd-static [BootstrapMethod "createStaticFn"] StaticFn
                               [Lookup String Class MethodHandleArray]))
