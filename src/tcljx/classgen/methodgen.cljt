(ns tcljx.classgen.methodgen
  (:require
   (tcljx.data [model :as model])
   (tcljx.classgen [runtime :as rt] [insn :as insn]))
  (:import
   (tcljx.data.model Invocable ArityType)
   (tcljx.classgen.insn Insns)
   (java.lang.constant ClassDesc MethodTypeDesc MethodHandleDesc
                       ConstantDesc DirectMethodHandleDesc DynamicConstantDesc
                       ConstantDescs DirectMethodHandleDesc$Kind)))

(deftype FnLiteral [^String mangled-name
                    ^ArityType/1 arity-types
                    ^Insns arity-bodies
                    ^MethodHandleDesc/1 arity-redirections]
  java.lang.Record
  Invocable)


(defn static-arity-mhd
  (^DirectMethodHandleDesc [^String method-name ^ArityType at]
   (static-arity-mhd ConstantDescs/CD_Void method-name at))
  (^DirectMethodHandleDesc [^ClassDesc owner ^String method-name ^ArityType at]
   (MethodHandleDesc/ofMethod DirectMethodHandleDesc$Kind/STATIC
                              owner method-name (model/mtd-of-arity-type at))))

(defn static-fn-desc ^DynamicConstantDesc [^ClassDesc owner
                                           ^FnLiteral fn-literal]
  (let [ats (.arity-types fn-literal)
        a (new ConstantDesc/1 (alength ats))
        mnm (.mangled-name fn-literal)]
    (dotimes [i (alength a)]
      (let [at (aget ats i)
            mhd (or (aget (.arity-redirections fn-literal) i)
                    (static-arity-mhd owner (str mnm "~" (.parameter-count at))
                                      at))]
        (aset a i mhd)))
    (DynamicConstantDesc/ofNamed rt/bsm-static-fn ConstantDescs/DEFAULT_NAME
                                 rt/StaticFn a)))
