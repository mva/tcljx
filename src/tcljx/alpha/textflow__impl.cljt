;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljx.alpha.textflow__impl)

(defn text-width ^int [^String txt]
  (.codePointCount txt 0 (.length txt)))

(def color-mode-default 0)
(def color-mode-palette-4bit 1)
(def color-mode-palette-8bit 2)
(def color-mode-rgb 3)

;;; https://en.wikipedia.org/wiki/ANSI_escape_code#SGR_(Select_Graphic_Rendition)_parameters
(def intensity-normal 0)
(def intensity-bright 1)
(def intensity-faint 2)

(def conceal-off 0)
(def conceal-invisible 1)
(def conceal-XXX 2)                     ;via replacement text
(def conceal-spaces 3)                  ;via replacement text


(def ^:private shift-cmd-arg 5)
(def ^:private mask-cmd-id (dec (bit-shift-left 1 shift-cmd-arg)))
(def mask-cmd-arg (bit-not mask-cmd-id))

(defn mk-cmd ^int [^int id ^int arg]
  (bit-or (bit-and id mask-cmd-id id) (bit-shift-left arg shift-cmd-arg)))

(defn cmd-id ^int [^int cmd]
  (bit-and cmd mask-cmd-id))
(defn cmd-arg ^int [^int cmd]
  (unsigned-bit-shift-right cmd shift-cmd-arg))

(defn cmd?
  (^boolean [^int id x]
   (and (instance? Integer x) (= (cmd-id ^int x) id)))
  (^boolean [^int id ^Object/1 page ^int i]
   (cmd? id (aget page i))))

;; command identifiers
(def cmd-id-invalid 0)
(def end-current-line 1)
(def forward-to-column 2)
(def set-font-bold 3)
(def set-font-italic 4)
(def set-underline 5)
(def set-strikethrough 6)
(def set-intensity 7)
(def set-foreground 8 #_plus-color-mode)
(def set-background 12 #_plus-color-mode)
(def set-conceal 16)
(def cmd-id-last set-conceal)
(def reserved-cmd-ids 2)
(assert (<= (+ cmd-id-last reserved-cmd-ids) mask-cmd-id))


(let [shift-arg-red 16
      shift-arg-green 8
      shift-arg-blue 0
      mask-channel 0xff]

  (defn arg-red [^int cmd]
    (-> cmd (unsigned-bit-shift-right shift-arg-red) (bit-and mask-channel)))
  (defn arg-green [^int cmd]
    (-> cmd (unsigned-bit-shift-right shift-arg-green) (bit-and mask-channel)))
  (defn arg-blue [^int cmd]
    (-> cmd (unsigned-bit-shift-right shift-arg-blue) (bit-and mask-channel))))


;;; State bits from LSB to MSB:
;;; 2 (foreground mode), 24 (fg color)
;;; 2 (background mode), 24 (bg color)
;;; 2 (intensity)
;;; 1 (underline), 1 (strikethrough)
;;; 1 (bold), 1 (italic)
;;; 2 (conceal)
;;; used: 60 bits, remaining 4
;;;
;;; Note: Two bits can be freed by folding the palette color
;;; modes (default, 4 bit, 8 bit) into the 24 bits of the rgb fields.
;;; Then the foreground/background mode fields become one bit each,
;;; and the palette mode and argument is decoded from the color value.

(def shift-style-foreground 0)
(def shift-style-background 26 #_(+ shift-style-foreground 2 24))
(def shift-style-intensity 52 #_(+ shift-style-background 2 24))
(def shift-style-underline 54 #_(+ shift-style-intensity 2))
(def shift-style-strikethrough 55 #_(+ shift-style-underline 1))
(def shift-style-font-bold 56 #_(+ shift-style-strikethrough 1))
(def shift-style-font-italic 57 #_(+ shift-style-font-bold 1))
(def shift-style-conceal 58 #_(+ shift-style-font-italic 1))
(def shift-style-unused 60 #_(+ shift-style-conceal 2))

(let [mask-color-mode (dec (bit-shift-left 1l 2))
      mask-color-rgb (dec (bit-shift-left 1l 24))]
  (def mask-style-foreground-mode
    (bit-shift-left mask-color-mode shift-style-foreground))
  (def mask-style-foreground-color
    (bit-shift-left mask-color-rgb (+ shift-style-foreground 2)))
  (def mask-style-foreground (bit-or mask-style-foreground-mode
                                     mask-style-foreground-color))
  (def mask-style-background-mode
    (bit-shift-left mask-color-mode shift-style-background))
  (def mask-style-background-color
    (bit-shift-left mask-color-rgb (+ shift-style-background 2)))
  (def mask-style-background (bit-or mask-style-background-mode
                                     mask-style-background-color)))

(def mask-style-intensity (bit-shift-left 3l shift-style-intensity))
(def mask-style-underline (bit-shift-left 1l shift-style-underline))
(def mask-style-strikethrough (bit-shift-left 1l shift-style-strikethrough))
(def mask-style-font-bold (bit-shift-left 1l shift-style-font-bold))
(def mask-style-font-italic (bit-shift-left 1l shift-style-font-italic))
(def mask-style-conceal (bit-shift-left 3l shift-style-conceal))

(def style-default 0l)

(defn extract ^int [^long mask ^long style]
  (int (Long/compress style mask)))
(defn- splice ^long [^long style ^long mask ^int arg]
  (bit-or (bit-and-not style mask) (Long/expand arg mask)))

(defn apply-command ^long [^long style ^int cmd]
  (let [cid (cmd-id cmd), arg (cmd-arg cmd)]
    (case cid
      (#_end-current-line 1) style
      (#_forward-to-column 2) style

      (#_set-font-bold 3)
      (splice style mask-style-font-bold arg)
      (#_set-font-italic 4)
      (splice style mask-style-font-italic arg)
      (#_set-underline 5)
      (splice style mask-style-underline arg)
      (#_set-strikethrough 6)
      (splice style mask-style-strikethrough arg)
      
      (#_set-intensity 7)
      (splice style mask-style-intensity arg)
      
      (#_set-foreground 8 9 10 11)
      (splice style mask-style-foreground
              (-> arg (bit-shift-left 2) (bit-or (- cid set-foreground))))
      
      (#_set-background 12 13 14 15)
      (splice style mask-style-background
              (-> arg (bit-shift-left 2) (bit-or (- cid set-background))))

      (#_set-conceal 16)
      (splice style mask-style-conceal arg))))
