;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljx.alpha.textflow__ansi
  (:require [tcljx.alpha.textflow__insn :as insn]))

;;; https://en.wikipedia.org/wiki/ANSI_escape_code#SGR_(Select_Graphic_Rendition)_parameters

(def csi (str \u001b \[))

(letfn [(parameters
          (^StringBuilder [^StringBuilder sb ^int n]
           (-> sb (.append n) (.append ";")))
          (^StringBuilder [^StringBuilder sb ^int n ^int arg-1 ^int arg-2]
           (-> sb (parameters n) (parameters arg-1) (parameters arg-2)))
          (^StringBuilder [^StringBuilder sb ^int n
                           ^int arg-1 ^int arg-2 ^int arg-3 ^int arg-4]
           (-> sb (parameters n arg-1 arg-2)
               (parameters arg-3) (parameters arg-4))))
        (sgr-color ^StringBuilder [^StringBuilder sb ^int offset
                                   ^int mode ^int arg]
          (case mode
            (#_color-mode-default 0)
            (parameters sb (+ offset 9))
            
            (#_color-mode-palette-4bit 1)
            (parameters sb (+ offset arg (-> (bit-shift-right arg 3)
                                             (bit-and 1)
                                             (* (- 60 8)))))
            
            (#_color-mode-palette-8bit 2)
            (parameters sb (+ offset 8) 5 arg)
            
            (#_color-mode-rgb 3)
            (parameters sb (+ offset 8) 2 (insn/arg-red arg)
                        (insn/arg-green arg) (insn/arg-blue arg))))
        (sgr-intensity ^StringBuilder [^StringBuilder sb ^int intensity]
          (parameters sb (case intensity
                           (#_intensity-normal 0) 22
                           (#_intensity-bright 1) 1
                           (#_intensity-faint 2) 2)))
        (sgr-conceal ^StringBuilder [^StringBuilder sb ^int conceal]
          (parameters sb (if (= conceal insn/conceal-invisible) 8 28)))
        (sgr-flag ^StringBuilder [^StringBuilder sb ^int n-on ^int n-off
                                  ^long mask ^long style]
          (parameters sb (case (insn/extract style mask) 0 n-off 1 n-on)))

        (modified? ^boolean [^long mask ^long diff]
          (not (zero? (bit-and mask diff))))
        (clear-diff ^long [^long diff ^long mask]
          (bit-and-not diff mask))

        (sgr-parameters ^StringBuilder [^StringBuilder sb ^long style
                                        ^long diff]
          (loop [diff diff]
            (if (zero? diff)            ;all differences resolved?
              sb
              (condp modified? diff
                insn/mask-style-foreground
                (let [mode (insn/extract style insn/mask-style-foreground-mode)
                      color (insn/extract style insn/mask-style-foreground-color)]
                  (sgr-color sb 30 mode color)
                  (recur (clear-diff diff insn/mask-style-foreground)))

                insn/mask-style-background
                (let [mode (insn/extract style insn/mask-style-background-mode)
                      color (insn/extract style insn/mask-style-background-color)]
                  (sgr-color sb 40 mode color)
                  (recur (clear-diff diff insn/mask-style-background)))
                
                insn/mask-style-intensity
                (let [intensity (insn/extract style insn/mask-style-intensity)]
                  (sgr-intensity sb intensity)
                  (recur (clear-diff diff insn/mask-style-intensity)))

                insn/mask-style-conceal
                (let [flag (insn/extract style insn/mask-style-conceal)]
                  (sgr-conceal sb flag) 
                  (recur (clear-diff diff insn/mask-style-conceal)))

                #_else
                (let [mask (Long/lowestOneBit diff)]
                  (case (Long/numberOfTrailingZeros diff)
                    (#_flag-style-underline 56) (sgr-flag sb 4 24 mask style)
                    (#_flag-style-overline 57) (sgr-flag sb 53 55 mask style)
                    (#_flag-style-strikethrough 58) (sgr-flag sb 9 29 mask style)
                    (#_flag-style-font-bold 59) (sgr-flag sb 1 22 mask style)
                    (#_flag-style-font-italic 60) (sgr-flag sb 3 23 mask style))
                  (recur (bit-xor diff mask)))))))

        (append-sgr ^StringBuilder [^StringBuilder sb ^long style ^long diff]
          (if (= style insn/style-default)
            (.append sb (str csi 0 \m))
            (-> (.append sb csi)
                (sgr-parameters style diff)
                (.deleteCharAt (dec (.length sb))) ;drop trailing semicolon
                (.append \m))))] ;and replace it with SGR's final byte

  (defn sgr-delta ^StringBuilder [^StringBuilder sb ^long style-0 ^long style-1]
    (let [diff (bit-xor style-0 style-1)]
      (if (zero? diff)
        sb
        (append-sgr sb style-1 diff)))))
