;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljx.alpha.textflow__ansi
  (:require [tcljx.alpha.textflow__impl :as impl]))

;;; https://en.wikipedia.org/wiki/ANSI_escape_code#SGR_(Select_Graphic_Rendition)_parameters

(def prefix-csi (str \u001b \[))

(letfn [(parameters
          (^StringBuilder [^StringBuilder sb ^int n]
           (-> sb (.append n) (.append ";")))
          (^StringBuilder [^StringBuilder sb ^int n ^int arg-1 ^int arg-2]
           (-> sb (parameters n) (parameters arg-1) (parameters arg-2)))
          (^StringBuilder [^StringBuilder sb ^int n
                           ^int arg-1 ^int arg-2 ^int arg-3 ^int arg-4]
           (-> sb (parameters n arg-1 arg-2)
               (parameters arg-3) (parameters arg-4))))
        (sgr-color ^StringBuilder [^StringBuilder sb ^int offset
                                   ^int mode ^int arg]
          (case mode
            (#_color-mode-default 0)
            (parameters sb (+ offset 9))
            
            (#_color-mode-palette-4bit 1)
            (parameters sb (+ offset arg
                              (-> arg (bit-shift-right 3) (bit-and 1) (* 60))))
            
            (#_color-mode-palette-8bit 2)
            (parameters sb (+ offset 8) 5 arg)
            
            (#_color-mode-rgb 3)
            (parameters sb (+ offset 8) 2 (impl/arg-red arg)
                        (impl/arg-green arg) (impl/arg-blue arg))))
        (sgr-intensity ^StringBuilder [^StringBuilder sb ^int intensity]
          (parameters sb (case intensity
                           (#_intensity-normal 0) 22
                           (#_intensity-bright 1) 1
                           (#_intensity-faint 2) 2)))
        (sgr-conceal ^StringBuilder [^StringBuilder sb ^int conceal]
          (parameters sb (if (= conceal impl/conceal-invisible) 8 28)))
        (sgr-flag ^StringBuilder [^StringBuilder sb ^int n-on ^int n-off
                                  ^int enable?]
          (parameters sb (case enable? 0 n-off 1 n-on)))

        (modified? ^boolean [^long mask ^long diff]
          (not (zero? (bit-and mask diff))))
        (clear-difference ^long [^long diff ^long mask]
          (bit-and-not diff mask))]

  (defn cursor-forward ^StringBuilder [^StringBuilder sb ^int n]
    (if (pos? n)
      (-> sb (.append prefix-csi) (.append n) (.append \C))
      sb))
  
  (defn- sgr-parameters ^StringBuilder [^StringBuilder sb ^long style ^long diff]
    (if (zero? diff)                  ;all differences resolved?
      sb
      (condp modified? diff
        impl/mask-style-foreground
        (let [mode (impl/extract impl/mask-style-foreground-mode style)
              color (impl/extract impl/mask-style-foreground-color style)]
          (recur (sgr-color sb 30 mode color)
                 style (clear-difference diff impl/mask-style-foreground)))

        impl/mask-style-background
        (let [mode (impl/extract impl/mask-style-background-mode style)
              color (impl/extract impl/mask-style-background-color style)]
          (recur (sgr-color sb 40 mode color)
                 style (clear-difference diff impl/mask-style-background)))

        (case (Long/numberOfTrailingZeros diff)
          (#_shift-style-intensity 52 53)
          (recur (sgr-intensity sb (impl/extract impl/mask-style-intensity style))
                 style (clear-difference diff impl/mask-style-intensity))

          (#_shift-style-underline 54)
          (recur (sgr-flag sb 4 24 (impl/extract impl/mask-style-underline style))
                 style (clear-difference diff impl/mask-style-underline))

          (#_shift-style-strikethrough 55)
          (recur (sgr-flag sb 9 29 (impl/extract impl/mask-style-strikethrough style))
                 style (clear-difference diff impl/mask-style-strikethrough))

          (#_shift-style-font-bold 56)
          (recur (sgr-flag sb 1 22 (impl/extract impl/mask-style-font-bold style))
                 style (clear-difference diff impl/mask-style-font-bold))

          (#_shift-style-font-italic 57)
          (recur (sgr-flag sb 3 23 (impl/extract impl/mask-style-font-italic style))
                 style (clear-difference diff impl/mask-style-font-italic))

          (#_shift-style-conceal 58) ;Wikipedia: "Not widely supported"
          (recur (sgr-conceal sb (impl/extract impl/mask-style-conceal style)) 
                 style (clear-difference diff impl/mask-style-conceal))
          ))))

  (defn sgr-delta ^StringBuilder [^StringBuilder sb ^long style-0 ^long style-1]
    (let [diff (bit-xor style-0 style-1)]
      (cond-> sb
        (not (zero? diff))
        (-> (.append prefix-csi)
            (sgr-parameters style-1 diff)
            (.deleteCharAt (dec (.length sb))) ;eliminate trailing semicolon
            (.append \m))))))    ;and replace it with SGR's final byte

