(ns tcljx.nmspgen.destdir
  (:require
   (tcljx.data [files :as files] [ir :as ir]))
  (:import
   (tcljx.data.ir NmspClass)
   (java.nio.file Files Path)))

(definterface Destination
  (prepare-package-directory ^void [^String ns-str])
  (write-class ^void [^NmspClass ncl]))

(deftype DestNone []
  Destination
  (prepare-package-directory [_ ns-str]
    nil)
  (write-class [_ ncl]
    nil))
(def dest-none (DestNone.))


(defn- package-directory ^Path [^Path dest-dir ^String ns-str]
  (.resolve dest-dir (.replace ns-str "." "/")))

(defn delete-package-classes
  (^void [^Path pkg-dir]
   #_(prn :delete-package-classes (str pkg-dir "/*.class"))
   (doseq [^Path x (files/directory-file-seq pkg-dir)
           :when (files/class-file? x)]
     (Files/delete x)))
  (^void [^Path dest-dir ^String ns-str]
   (delete-package-classes (package-directory dest-dir ns-str))))

(deftype DestFiles [^Path dest-dir]
  Destination
  (prepare-package-directory [_ ns-str]
    (files/create-directories (package-directory dest-dir ns-str)))
  (write-class [_ ncl]
    (let [cld (.this-class ncl)
          pkg-dir (package-directory dest-dir (.packageName cld))
          fnm (str (.displayName cld) files/class-suffix)]
      (with-open [w (Files/newOutputStream (.resolve pkg-dir fnm))]
        (.write w (.class-bytes ncl))))))

(def mk-dest-files DestFiles/new)
