(ns tcljx.nmspgen.junctgen
  (:require
   (tcljx.data [type :as type] [wrong :as wrong] [ir :as ir])
   (tcljx.nmspgen [util :as util] [emit :as emit] [insn :as insn]
                  [constgen :as constgen] [condgen :as condgen] [opgen :as opgen]))
  (:import
   (tcljx.data.type Wrapper)
   (tcljx.data.ir CodeEmitter Entity Binding)
   (tcljx.nmspgen.insn Insn Insns)
   (java.lang.classfile CodeBuilder)
   (java.util.function Consumer)))

(defn- binding-of ^Binding [^Class tp ^Insns xs ^int i]
  ;; pre: `x` is non-environment value
  (let [ce (insn/emitter xs i)]
    (if (= (ir/binding-mode-let ce) ir/bound-as-alias)
      (ir/mk-binding ce "temp-alias" true)
      (-> (util/entity-local tp 0 ce)
          (ir/mk-binding "temp" false)))))

(defn- bound-value ^Insn [^Class tp ^Binding bnd ^Insns args ^int i]
  (if (.alias? bnd)
    ^Insn (.get args i)
    (insn/mk-insn (.bound-to bnd) tp)))

;;; Simplified version of `block-bindings` from tcljx.nmspgen.codegen:
(defn- block-binding ^Insn [^long fctx ^Binding bnd ^Insn body]
  (if (.alias? bnd)
    body
    (-> (^CodeEmitter fn [xb consumed-type then-leave?]
         (.block xb (^Consumer fn ^void [^CodeBuilder xb]
                     (when (emit/statm? xb bnd)
                       (emit/then-leave? xb fctx consumed-type (.emitter body)
                                         then-leave?))))
         (when-not then-leave? ;can the then-leave? above return true?
           consumed-type))
        (insn/mk-insn (.expr-type body)))))

(defn- logical-join-type ^Class [^Insns args]
  (-> (condgen/join-type (.expr-types args) 0 (.length args))
      (type/expr-to-logical)))

(defn- boolean-temp? ^boolean [^Insns args ^int i]
  (type/same? Boolean/TYPE (insn/expr-type args i)))

;;; Straightforward translation of macro clojure.core/and.
(defn nary-and ^Insn [^long fctx ^Insns args]
  (if (pos? (.length args))
    (let [tp (logical-join-type args)]
      (letfn [(test-for-and ^Insn [^int i ^Insn x-test ^CodeEmitter on-false]
                (-> (.emitter (condgen/logical-truth x-test fctx))
                    (condgen/mk-if tp (.emitter (expand (inc i))) on-false fctx)
                    (insn/mk-insn tp)))
              (expand ^Insn [^int i]
                (cond (>= i (dec (.length args)))
                      (.get args i)
                      
                      (boolean-temp? args i)
                      (test-for-and i (.get args i) constgen/entity-false)
                      
                      :else (let [bnd (binding-of tp args i)
                                  x-temp (bound-value tp bnd args i)]
                              (->> (test-for-and i x-temp (.emitter x-temp))
                                   (block-binding fctx bnd)))))]
        (expand 0)))
    insn/insn-true))

;;; Straightforward translation of macro clojure.core/or.
(defn nary-or ^Insn [^long fctx ^Insns args]
  (if (pos? (.length args))
    (let [tp (logical-join-type args)]
      (letfn [(test-for-or ^Insn [^int i ^Insn x-test ^CodeEmitter on-true]
                (-> (.emitter (condgen/logical-truth x-test fctx))
                    (condgen/mk-if tp on-true (.emitter (expand (inc i))) fctx)
                    (insn/mk-insn tp)))
              (expand ^Insn [^int i]
                (cond (>= i (dec (.length args)))
                      (.get args i)

                      (boolean-temp? args i)
                      (test-for-or i (.get args i) constgen/entity-true)

                      :else (let [bnd (binding-of tp args i)
                                  x-temp (bound-value tp bnd args i)]
                              (->> (test-for-or i x-temp (.emitter x-temp))
                                   (block-binding fctx bnd)))))]
        (expand 0)))
    insn/insn-null))


(defn nary-compare ^Insn [^int cmp-opcd ^long fctx ^Insns args form]
  (let [wtop (when-not (neg? cmp-opcd)  ;`wtop` of equiv is nil
               (or (when (= cmp-opcd condgen/cmp-eq)
                     (type/of-all (.expr-types args)))
                   (opgen/top-numeric-of form args)))]
    (case (.length args)
      0 (throw (wrong/wr-info "missing argument"))
      1 (insn/progn args insn/insn-true)
      2 (condgen/xcmp-2 wtop (.get args 0) (.get args 1) cmp-opcd fctx)
      #_else     ;all but the last argument get bindings left-to-right
      (letfn [(expand ^Insn [^Insn opnd-left ^int i-right]
                (if (>= i-right (dec (.length args)))
                  (condgen/xcmp-2 wtop opnd-left (.get args i-right)
                                  cmp-opcd fctx)
                  (let [tp (.class-primitive wtop)
                        bnd (binding-of tp args i-right)
                        x-temp (bound-value tp bnd args i-right)]
                    (->> (if (pos? i-right)
                           (-> (condgen/xcmp-2 wtop opnd-left x-temp cmp-opcd
                                               fctx) 
                               (opgen/boolean-and (expand x-temp (inc i-right))
                                                  fctx))
                           (expand x-temp 1))
                         (block-binding fctx bnd)))))]
        (expand nil 0)))))
