(ns tcljx.data.code
  (:require (tcljx.data ir))
  (:import
   (tcljx.data.ir FieldSpec)
   (java.lang.classfile ClassBuilder CodeBuilder TypeKind)
   (java.lang.constant ClassDesc MethodTypeDesc MethodHandleDesc
                       DirectMethodHandleDesc DirectMethodHandleDesc$Kind
                       ConstantDescs)
   (java.util.function Consumer)))

(defmacro acc* [& flags]
  (reduce #(bit-or ^int %1 (case %2
                             public 0x0001, private 0x0002, protected 0x0004
                             static 0x0008, final 0x0010, volatile 0x0040
                             bridge 0x0040, varargs 0x0080, interface 0x0200,
                             abstract 0x0400 synthetic 0x1000))
          0 flags))

(defn load-receiver ^CodeBuilder [^CodeBuilder xb]
  (.aload xb (.receiverSlot xb)))
(defn load-all-parameters ^CodeBuilder [^CodeBuilder xb ^MethodTypeDesc mtd]
  (dotimes [i (.parameterCount mtd)]
    (.loadLocal xb (TypeKind/from (.parameterType mtd i)) (.parameterSlot xb i)))
  xb)

(defn load-parameter ^CodeBuilder [^CodeBuilder xb ^FieldSpec fs ^int param-no]
  (.loadLocal xb (TypeKind/from (.type-desc fs)) (.parameterSlot xb param-no)))
(defn put-field ^CodeBuilder [^CodeBuilder xb ^FieldSpec fs ^int param-no]
  (-> xb (load-receiver) (load-parameter fs param-no)
      (.putfield (.owner fs) (.field-name fs) (.type-desc fs))))
(defn get-field ^CodeBuilder [^CodeBuilder xb ^FieldSpec fs]
  (-> xb (load-receiver)
      (.getfield (.owner fs) (.field-name fs) (.type-desc fs))))

(defn with-field ^ClassBuilder [^ClassBuilder cb ^FieldSpec fs]
  (.withField cb (.field-name fs) (.type-desc fs) (.flags fs)))
(defn with-fields ^ClassBuilder [^ClassBuilder cb ^FieldSpec/1 fields]
  (dotimes [i (alength fields)]
    (with-field cb (aget fields i)))
  cb)

(defn getter-mhd ^DirectMethodHandleDesc [^FieldSpec fs]
  (MethodHandleDesc/ofField DirectMethodHandleDesc$Kind/GETTER
                            (.owner fs) (.field-name fs) (.type-desc fs)))
(defn with-getter ^ClassBuilder [^ClassBuilder cb ^FieldSpec fs]
  (->> (^Consumer fn ^void [^CodeBuilder xb]
        (-> xb (get-field fs) (.return_ (TypeKind/from (.type-desc fs)))))
       (.withMethodBody cb (.field-name fs) (MethodTypeDesc/of (.type-desc fs))
                        (acc* public synthetic))))

(defn mtd-of-fields ^MethodTypeDesc [^FieldSpec/1 fields]
  (->> ^java.util.List (map FieldSpec/.type-desc fields)
       (MethodTypeDesc/of ConstantDescs/CD_void)))
(defn constructor
  (^ClassBuilder [^ClassBuilder cb ^FieldSpec/1 fields ^ClassDesc super-class 
                  ^int flags]
   (constructor cb fields super-class flags (new ClassDesc/1 0)))
  (^ClassBuilder [^ClassBuilder cb ^FieldSpec/1 fields ^ClassDesc super-class 
                  ^int flags ^ClassDesc/1 super-params]
   (let [mtd (-> fields (mtd-of-fields) (.insertParameterTypes 0 super-params))]
     (->> (^Consumer fn ^void [^CodeBuilder xb]
           (let [n (alength super-params)
                 mtd-super (MethodTypeDesc/of ConstantDescs/CD_void super-params)]
             (dotimes [i (alength fields)]
               (put-field xb (aget fields i) (+ i n)))
             (load-receiver xb)
             (dotimes [i n]
               (.loadLocal xb (TypeKind/from (aget super-params i))
                           (.parameterSlot xb i)))
             (.invokespecial xb super-class ConstantDescs/INIT_NAME mtd-super)
             (.return_ xb)))
          (.withMethodBody cb ConstantDescs/INIT_NAME mtd flags)))))

;;; ------------------------------------------------------------------------

(def acc-method-virtual (acc* public final)) ;no override
(def acc-method-static (acc* public static)) ;can be hidden

(defmacro accept->
  ([builder handler]
   (if (symbol? builder)
     (list 'do (list '.accept handler builder) builder)
     `(let [builder# ~builder] (.accept ~handler builder#) builder#)))
  ([builder handler arg]
   (if  (symbol? builder)
     (list 'do (list '.accept handler builder arg) builder)
     `(let [builder# ~builder] (.accept ~handler builder# ~arg) builder#))))

(defmacro with-method-body-> [cb [method-name lookup-type-desc flags] & body]
  `(.withMethodBody ~cb ~method-name ~lookup-type-desc ~flags
                    (^Consumer fn ^void [^CodeBuilder xb#]
                     (-> xb# ~@body))))
