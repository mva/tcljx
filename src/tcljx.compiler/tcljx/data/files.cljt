(ns tcljx.data.files
  (:import
   (java.net URL)
   (java.nio.file Path Files)
   (java.nio.file.attribute PosixFilePermission PosixFilePermissions)
   (java.util Set)
   (java.util.function Predicate)))

(def source-suffix ".cljt")
(def class-suffix ".class")

(defn class-file? ^boolean [^Path x]
  (.endsWith (.toString x) class-suffix))

(defn path-url ^URL [^Path p]
  (-> p (.toUri) (.toURL)))

(defn path-of
  (^Path [^String x]
   (Path/of x))
  (^Path [^String x & ^String/1 more]
   (Path/of x more)))

(defn default-project-name ^String []
  (-> (path-of (System/getProperty "user.dir"))
      (.getFileName)
      (.toString)))

(defn tmp-dest-dir ^Path [^String dir-name]
  (path-of (System/getProperty "java.io.tmpdir")
           (System/getProperty "user.name") "tcljx" dir-name))

(defn namespace-path ^Path [^String ns-str]
  (Path/of (str (.replace ns-str \. \/) source-suffix)))

;;; ------------------------------------------------------------------------

(def ^:private create-directory-permissions
  (-> (Set/of PosixFilePermission/OWNER_READ
              PosixFilePermission/OWNER_WRITE
              PosixFilePermission/OWNER_EXECUTE)
      (PosixFilePermissions/asFileAttribute)))

(defn create-directories ^Path [^Path p]
  (Files/createDirectories p create-directory-permissions))

;;; ------------------------------------------------------------------------

(defn- path-seq ^seq [^Path p ^int max-depth ^Predicate pred]
  (try
    (-> p (Files/walk max-depth) (.filter pred) (.toArray) (seq))
    (catch java.nio.file.NoSuchFileException _ ;thrown if `p` does not exist
      nil)))

(def ^:private file? (^Predicate fn [x] (Files/isRegularFile x)))
(defn recursive-file-seq ^seq [^Path p]
  (path-seq p Integer/MAX_VALUE file?))
(defn child-file-seq ^seq [^Path p]     ;files local to `p` only
  (path-seq p 1 file?))

(def ^:private dir? (^Predicate fn [x] (Files/isDirectory x)))
(defn recursive-directory-seq ^seq [^Path p]
  (path-seq p Integer/MAX_VALUE dir?))
(defn child-directory-seq ^seq [^Path p] ;directories local to `p` only
  (rest (path-seq p 1 dir?)))            ;drop `p` itself
