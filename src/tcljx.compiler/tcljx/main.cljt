;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljx.main
  (:require
   (tcljx.data [config :as cfg] [error :as error])
   (tcljx.main [options :as options] [invoke :as invoke]
               [tclj0 :as tclj0] [tclj1 :as tclj1]))
  (:import
   (tcljx.main.options Options)
   (java.util Optional)
   (java.util.function Consumer)))

(defn print-exception-info [^tcljx.data.error.WrongInfo e]
  (println "WrongInfo:" (ex-message e))
  (run! println (sort-by key (error/wr-data e))))

(defn -main [& args]
  (let [res (try
              (if (contains? #{nil, "--help", "-h"} (first args))
                (do (println (str "usage: java ... [--watch] [-d <dest-dir] "
                                  "[-s <classpath>]... <main-ns>/<run>..."))
                    false)
                (if-some [opts (options/parse args)]
                  (if (.watch-mode? opts)
                    (tclj1/run opts)
                    (tclj0/run opts))
                  false))
              (catch tcljx.data.error.WrongInfo e
                (print-exception-info e)
                false)
              (catch Throwable t
                (.printStackTrace t)
                false)        ;failure because of unhandled expception
              (finally
                (shutdown-agents)))] ;can only shut down when owning the JVM
    (System/exit (if res 0 1)))) ;can only exit when owning the JVM

;;; Start watch/compile/report loop.  After each build attempt,
;;; `value-consumer` is called with an argument of type Optional.  On
;;; success it holds the value of the namespace var to which
;;; `var-name` resolves.  On failure it is empty.  This function never
;;; returns normally.
(defn start
  ^void [^String dest-dir               ;nil means default directory
         ^clojure.lang.IPersistentVector sourcev
         ^clojure.lang.Symbol var-name  ;namespace qualified
         ^java.util.function.Consumer value-consumer]
  (when (nil? (namespace var-name))
    (options/abort (str "error: var name `" var-name
                        "` must be a namespace qualified symbol")))
  (doseq [x sourcev]
    (when-not (instance? java.nio.file.Path x)
      (options/abort (str "error: sourcev entry must java.nio.file.Path"))))
  (let [opts (Options. (options/to-dest-dir dest-dir) ;dest-dir
                       (ClassLoader/getSystemClassLoader) ;parent-class-loader
                       sourcev               ;sourcev
                       (list (str var-name)) ;targets
                       false                 ;deterministic?
                       15000                 ;wait-cyclic-ms
                       true                  ;watch-mode?
                       20)                   ;wait-after-change-ms
        on-completion (^Consumer fn [opt-known-ns]
                       (.accept value-consumer
                                (if (.isEmpty ^Optional opt-known-ns)
                                  (Optional/empty)
                                  (let [known-ns (.get ^Optional opt-known-ns)]
                                    (-> (invoke/namespace-of known-ns var-name)
                                        (invoke/def-value var-name)
                                        (Optional/of))))))]
    (tclj1/run opts on-completion)))
