;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljx.main.tclj0
  (:require
   (tcljx.data [config :as cfg] [files :as files] [error :as error]
               [type :as type])
   (tcljx.nmspgen [runtime :as rt] [destdir :as destdir]
                  [pkggen :as pkggen] [insn :as insn]
                  [primitive :as primitive] [universe :as universe])
   (tcljx.main [options :as options] [invoke :as invoke]))
  (:import
   (tcljx.data.error WrongInfo)
   (tcljx.data.ir NamespaceProvider ProvidedNamespace)
   (tcljx.nmspgen.destdir Destination)
   (tcljx.main.options Options)
   (java.nio.file Path Files)))

(defn prepare-destination ^Destination [^Path path ^boolean keep-files?]
  (if (some? path)         ;wipe *before* any classloaders are created
    (do (when-not keep-files?
          (doseq [^Path x (files/recursive-file-seq path)
                  :when (files/class-file? x)]
            #_(prn :wipe-dest-dir (.toString x))
            (Files/delete x)))
        (destdir/mk-dest-files path))
    destdir/dest-none))

(defn start-universe ^NamespaceProvider [^Options opts
                                         ^boolean load-from-dest-dir?]
  (let [dest (prepare-destination (.dest-dir opts) load-from-dest-dir?)
        app-ld (rt/app-class-loader (.parent-class-loader opts) (.sourcev opts))
        dyn-sourcev (if load-from-dest-dir? [(.dest-dir opts)] [])
        dyn-ld (rt/dyn-class-loader app-ld dyn-sourcev)
        rt (rt/mk-runtime app-ld dyn-ld)
        _ (type/fixup-runtime-types-once! (.lookup rt))
        
        pn-primitive (ProvidedNamespace. cfg/primitive-ns-str
                                         primitive/primitive-nmsp-globals
                                         (new String/1 0) nil nil
                                         insn/insn-empty)
        provided-ns {cfg/primitive-ns-str pn-primitive}]
    (universe/mk-universe nil rt dest provided-ns)))

(defn build-all ^map [^NamespaceProvider universe ^String/1 ns-strs]
  ;; All namespaces required by clojure.core are part of cyclic
  ;; dependencies, but only clojure.core is allowed to initiate the
  ;; cycle.  So *always* require it first, to prevent the other
  ;; namespaces from initiating the cycle.
  (let [preload-ns-strs (doto (new String/1 1) (aset 0 cfg/core-lib))]
    (try
      (.require-namespaces* universe preload-ns-strs nil)
      (.require-namespaces* universe ns-strs nil)
      (catch WrongInfo e
        (error/print-error e)
        (println)))
    (.await-quiescence universe)))

(defn run ^boolean [^Options opts]
  (options/print-options opts)
  (let [ns-strs (options/target-ns-strs opts)
        all-nmsps (-> (start-universe opts false)
                      (build-all ns-strs)
                      (time))]
    (and (every? all-nmsps ns-strs)
         (invoke/invoke-and-report all-nmsps opts))))
