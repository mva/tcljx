;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljx.main.tclj0
  (:require
   (tcljx.data [config :as cfg] [files :as files] wrong [type :as type])
   (tcljx.nmspgen [runtime :as rt] [destdir :as destdir]
                  [pkggen :as pkggen] [insn :as insn]
                  [primitive :as primitive] [universe :as universe])
   (tcljx.main [options :as options] [invoke :as invoke] [efmt :as efmt]))
  (:import
   (tcljx.data.wrong WrongInfo)
   (tcljx.data.ir ProvidedNamespace)
   (tcljx.nmspgen.destdir Destination)
   (tcljx.main.options Options)
   (java.nio.file Path Files)))

(defn- class-file? ^boolean [^Path x]
  (.endsWith (.toString x) files/class-suffix))
(defn prepare-destination ^Destination [^Path path ^boolean keep-files?]
  (if (some? path)         ;wipe *before* any classloaders are created
    (do (when-not keep-files?
          (doseq [^Path x (files/file-seq path) :when (class-file? x)]
            #_(prn :wipe-dest-dir (.toString x))
            (Files/delete x)))
        (destdir/mk-dest-files path))
    destdir/dest-none))

(defn build-all ^map [^Options opts ^boolean load-from-dest-dir?]
  (let [dest (prepare-destination (.dest-dir opts) load-from-dest-dir?)
        app-ld (rt/app-class-loader (.parent-classloader opts) (.sourcev opts))
        dyn-sourcev (if load-from-dest-dir? [(.dest-dir opts)] [])
        dyn-ld (rt/dyn-class-loader app-ld dyn-sourcev)
        rt (rt/mk-runtime app-ld dyn-ld)
        _ (type/fixup-runtime-types! (.lookup rt))
        
        pn-primitive (ProvidedNamespace. cfg/primitive-ns-str
                                         primitive/primitive-nmsp-globals
                                         (new String/1 0) nil insn/insn-empty)
        provided-ns {cfg/primitive-ns-str pn-primitive}

        ;; All namespaces required by clojure.core are part of cyclic
        ;; dependencies, but only clojure.core is allowed to initiate
        ;; the cycle.  So *always* require it first, to prevent the
        ;; other namespaces from initiating the cycle.
        preload-ns-strs (doto (new String/1 1) (aset 0 cfg/core-lib))
        ns-strs (options/target-ns-strs opts)
        all-pns (-> (doto (universe/mk-universe nil rt dest provided-ns)
                      (.require-namespaces* preload-ns-strs nil))
                    (.require-namespaces* ns-strs nil))]
    (into {} (map (juxt identity all-pns)) ns-strs)))

(defn run ^boolean [^Options opts]
  (options/print-options opts)
  (try
    (-> (time (build-all opts false))
        (invoke/invoke-and-report opts))
    (catch WrongInfo e
      (efmt/print-emap e)
      (println)
      false)))
