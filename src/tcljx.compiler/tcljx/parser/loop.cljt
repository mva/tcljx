;; Copyright (c) Michael van Acken. All rights reserved.
;; The use and distribution terms for this software are covered by the
;; Eclipse Public License 2.0 (https://www.eclipse.org/legal/epl-v20.html)
;; which can be found in the file epl-v20.html at the root of this distribution.
;; By using this software in any fashion, you are agreeing to be bound by
;; the terms of this license.
;; You must not remove this notice, or any other, from this software.
(ns tcljx.parser.loop
  (:require
   (tcljx.data [error :as error] [context :as context] [ir :as ir])
   (tcljx.parser [parse :as parse] [syntax :as syntax] [interop :as interop]))
  (:import
   (tcljx.data.ir Expr Exprs Binding Entity Parser)))

(defn parse-loop* ^Expr [^Parser parser ^long fctx [_ bindings & body :as form]]
  (let [bindings (syntax/vector bindings true)
        n (quot (count bindings) 2)
        bnds (new Binding/1 n)]
    (loop [parser parser, i 0]
      (if (< i n)
        (let [nm-sym (nth bindings (* i 2))
              nm (syntax/simple-symbol-str nm-sym)
              init (nth bindings (inc (* i 2)))
              opt-type (interop/resolve-tag (.resolver parser) nm-sym nil)
              bnd (.binding-fresh (.entity-factory parser) fctx opt-type nm
                                  (parse/arg parser fctx init))]
          (aset bnds i bnd)
          (recur (ir/with-local-binding parser bnd) (inc i)))
        (let [fctx' (context/with-recur fctx)
              bnd-vs (parse/binding-variables bnds)
              xf' (.with-recur-variables (.expr-factory parser) bnd-vs)]
          (.block-bindings xf' fctx' bnds false true
                           (-> (.with-expr-factory parser xf')
                               (parse/progn fctx' body))))))))

(defn parse-recur ^Expr [^Parser parser ^long fctx [_ & args :as form]]
  (if (context/can-recur? fctx)
    (->> (parse/args parser fctx args)
         (.recur-expr (.expr-factory parser) fctx))
    (error/throw "cannot recur from this position")))
